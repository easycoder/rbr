!	Calendar

	script Calendar
    
    import div MainPanel
    	and variable Map
        and variable Result

	div Profile1Column
    div ProfileTimeColumn
    div Profile2Column
    div Dialog
    div List
    div DialogRow
    button OKButton
    button HelpButton
    button CancelButton
    button DialogCancelButton
    variable Mobile
    variable Changed
    variable Profile
    variable Profiles
    variable NProfiles
    variable Name
    variable DialogOpen
    variable D
    variable P
    
!    debug step
    
    clear Mobile
    if mobile
        if portrait set Mobile
    
    set the elements of Profile1Column to 7
    set the elements of ProfileTimeColumn to 7
    set the elements of Profile2Column to 7

	put 0 into D
    while D is less than 7
    begin
    	index Profile1Column to D
        index ProfileTimeColumn to D
        index Profile2Column to D
        attach Profile1Column to `profile1-` cat D
        attach ProfileTimeColumn to `profile-time-` cat D
        attach Profile2Column to `profile2-` cat D
    	add 1 to D
    end
    on click Profile1Column
    begin
    	gosub to GetProfile
        print Name
        if Name is not empty set the content of Profile1Column to Name
    end
    on click ProfileTimeColumn
    begin
    	print `ProfileTime - ` cat the index of ProfileTimeColumn
    end
    on click Profile2Column
    begin
    	gosub to GetProfile
        print Name
        if Name is not empty set the content of Profile2Column to Name
    end

    attach OKButton to `calendar-ok-button`
    attach HelpButton to `calendar-help-button`
    attach CancelButton to `calendar-cancel-button`
	attach Dialog to `calendar-dialog`
    attach List to `profile-list`
    attach DialogCancelButton to `calendar-dialog-cancel-button`

    on click OKButton
    begin
    	if DialogOpen stop
        set Changed
    	go to Exit
    end
    
    on click HelpButton
    begin
    	if DialogOpen stop
        put `Help home Calendar` into Result
        exit
    end

	on click CancelButton
    begin
    	if DialogOpen stop
    	clear Changed
        go to Exit
    end

	stop

Exit:
	if Changed
    begin
        put `Changed` into Result
    end
    exit

ExitWithError:
    put `Error` into Result
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Choose a profile
GetProfile:
	put property `profiles` of Map into Profiles
    put the json count of Profiles into NProfiles
    set the elements of DialogRow to NProfiles
    set the elements of Name to NProfiles
    clear List
	put 0 into P
    while P is less than NProfiles
    begin
    	put element P of Profiles into Profile
        index Name to P
        put property `name` of Profile into Name
        index DialogRow to P
        create DialogRow in List
        set style `height` of DialogRow to `1.5em`
        if P modulo 2 set style `background` of DialogRow to `#eee`
        else set style `background` of DialogRow to `#ddd`
        set style `cursor` of DialogRow to `pointer`
        set the content of DialogRow to Name
        add 1 to P
    end
    on click DialogRow
    begin
        put the index of DialogRow into P
    	index Name to P
    	set style `display` of Dialog to `none`
    	clear DialogOpen
    end
    set style `display` of Dialog to `flex`
    on click DialogCancelButton
    begin
    	set style `display` of Dialog to `none`
        index Name to 0
        put empty into Name
    	clear DialogOpen
    end
    set DialogOpen
    while DialogOpen wait 20 ticks
	return