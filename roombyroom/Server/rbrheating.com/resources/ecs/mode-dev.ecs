!	Mode selection

	script Mode
    
    import div MainPanel
    	and div ModePanel
    	and img ModeIcon
        and variable Mode
        and variable Map
    	and variable ClickIndex
        and variable Sensors
        and variable Demo
        and variable Result
    
    div ButtonM
    div ButtonO
    div ButtonC
    div ButtonH
    div ButtonT
    div OnUpButton
    div OnDownButton
    div OnTarget
    div Boost1Button
    div Boost2Button
    div Boost3Button
    div Boost4Button
    div Boost1Text
    div Boost2Text
    div Boost3Text
    div Boost4Text
    div AdvanceButton
    div AdvanceText
    div Mask
    div BannerMask
    div TitleMask
    div MaskLayer
	img TimedEditIcon
    variable Map2
    variable CurrentProfile
    variable Profiles
    variable Profile
    variable Rooms
    variable RoomSpec
    variable SensorIP
    variable Sensor
    variable Target
    variable TargetFlag
    variable RoomName
    variable CurrentMode
    variable PeriodEditScript
    variable ModeWebson
    variable Integer
    variable FlashIndex
    variable Password
    variable Server
    variable Count
    variable Waiting
    variable Flashing
    variable Advance
    variable MAC
    variable Dot
    variable OnUD
    variable Boost
    variable BoostFlag
    variable Flag
    variable L
    variable T
    variable X
    
!    debug step

	put empty into PeriodEditScript
    get Server from storage as `server`
    get MAC from storage as `MAC`
    get Password from storage as `password`

    index Mode to the index of ModeIcon
   	put Mode into CurrentMode
    wait 20 ticks

	clear ModePanel
    gosub to GetCurrentRooms
    put element ClickIndex of Rooms into RoomSpec
    put property `name` of RoomSpec into RoomName
    rest get ModeWebson from `/resources/webson/mode.json?v=` cat now
    replace `/ROOM/` with RoomName in ModeWebson
	render ModeWebson in ModePanel
    attach ButtonT to `mode-timed`
    attach ButtonM to `mode-on`
    attach ButtonO to `mode-off`
    attach ButtonC to `mode-cancel`
    attach ButtonH to `mode-help`
    attach OnDownButton to `on-down`
    attach OnUpButton to `on-up`
    attach OnTarget to `on-target`
    attach Boost1Button to `boost-1`
    attach Boost2Button to `boost-2`
    attach Boost3Button to `boost-3`
    attach Boost4Button to `boost-4`
    attach Boost1Text to `boost-1-text`
    attach Boost2Text to `boost-2-text`
    attach Boost3Text to `boost-3-text`
    attach Boost4Text to `boost-4-text`
    attach TimedEditIcon to `timed-edit`
    attach AdvanceButton to `timed-advance`
    attach AdvanceText to `timed-advance-text`
    attach Mask to `top-level-mask`
    attach BannerMask to `banner-mask`
    attach TitleMask to `title-mask`
    attach MaskLayer to `mask-layer`

    put property `target` of RoomSpec into Target
	set the content of OnTarget to Target cat `&deg;C`
    clear TargetFlag

    clear BoostFlag
    put 0 into Boost
	set style `color` of Boost1Text to `green`

	set style `visibility` of ModePanel to `visible`
    put property `sensor` of RoomSpec into SensorIP
    put property SensorIP of Sensors into Sensor
    put `` cat property `advance` of Sensor into Advance
    if Advance is `-`
    begin
        set style `color` of AdvanceText to `black`
    end
    else
    begin
        set style `color` of AdvanceText to `green`
    end
    on click ButtonT go to SelectTimed
    on click ButtonM go to SelectOn
    on click ButtonO go to SelectOff
    on click ButtonC go to SelectCancel
    on click ButtonH go to SelectHelp
    on click OnDownButton go to OnDown
    on click OnUpButton go to OnUp
    on click Boost1Button go to Boost1
    on click Boost2Button go to Boost2
    on click Boost3Button go to Boost3
    on click Boost4Button go to Boost4
    on click TimedEditIcon go to ShowTimes
    on click AdvanceButton go to DoAdvance

	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	'On' Up and Down buttons
OnDown:
	clear OnUD
    go to OnUpDown

OnUp:
	set OnUD

OnUpDown:
	if Target is empty put `0.0` into Target else put `` cat Target into Target
    put the position of `.` in Target into Dot
    if Dot is -1 multiply the value of Target by 10 giving T
    else
    begin
         put left Dot of Target into Integer
         multiply the value of Integer by 10 giving T
         add 1 to Dot
         add the value of from Dot of Target to T
    end
    if OnUD add 5 to T else if Target is not `0.0` take 5 from T
    if T is less than 10 put `0` cat T into T
    else put `` cat T into T
    put the length of T into L
    take 1 from L
    put left L of T into X
    put X cat `.` into X
    put X cat from L of T into Target
    set the content of OnTarget to Target cat `&deg;C`
    set TargetFlag
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Show the schedule times
ShowTimes:
    set style `display` of Mask to `none`
    set style `display` of BannerMask to `block`
    set style `display` of TitleMask to `block`
    set style `display` of MaskLayer to `block`
    set style `visibility` of ModePanel to `hidden`
    put empty into Result
    if PeriodEditScript is empty
    	rest get PeriodEditScript from `/resources/ecs/periodedit-dev.ecs?v=` cat now
    run PeriodEditScript with MainPanel and Map and CurrentProfile and ClickIndex and Result
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Send an advance or cancel request
DoAdvance:
	if Advance is `-` put `A` into Advance
    else put `C` into Advance
    set property `advance` of RoomSpec to Advance
    set Flag
    go to SelectTimed

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select timed mode
SelectTimed:
   	put `timed` into Mode
    clear BoostFlag
    go to SelectMode

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	'Boost' buttons
Boost1:
	gosub to ResetBoostButtons
    set style `color` of Boost1Text to `#508e62`
    put 0 into Boost
	go to SelectBoost

Boost2:
	gosub to ResetBoostButtons
    set style `color` of Boost2Text to `#508e62`
    put 2 into Boost
	go to SelectBoost

Boost3:
	gosub to ResetBoostButtons
    set style `color` of Boost3Text to `#508e62`
    put 60 into Boost
	go to SelectBoost

Boost4:
	gosub to ResetBoostButtons
    set style `color` of Boost4Text to `#508e62`
    put 120 into Boost
	go to SelectBoost

ResetBoostButtons:
	set style `color` of Boost1Text to `black`
	set style `color` of Boost2Text to `black`
	set style `color` of Boost3Text to `black`
	set style `color` of Boost4Text to `black`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select 'boost' mode
SelectBoost:
	if CurrentMode is `boost`
    begin
        if Boost is 0
        begin
        	put property `prevmode` of RoomSpec into Mode
        	clear BoostFlag
        end
        else set BoostFlag
        go to SelectMode
    end
   	if Boost is 0 go to SelectCancel
   	else
    begin
    	if CurrentMode is not `boost` set property `prevmode` of RoomSpec to CurrentMode
        put `boost` into Mode
        set BoostFlag
       	go to SelectMode
    end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select 'on' mode
SelectOn:
    put `on` into Mode
    clear BoostFlag
    set property `advance` of RoomSpec to `-`
    go to SelectMode

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select off mode
SelectOff:
    put `off` into Mode
    clear BoostFlag
    set property `advance` of RoomSpec to `-`
    go to SelectMode

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select cancel
SelectCancel:
    put CurrentMode into Mode
    clear TargetFlag
    clear BoostFlag
    go to SelectMode

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select help
SelectHelp:
    set style `visibility` of ModePanel to `hidden`
    put `Help home Mode` into Result
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Select the mode and exit
SelectMode:
    set style `visibility` of ModePanel to `hidden`
    if Mode is `timed`
    begin
        set property `boost` of RoomSpec to 0
        if Flag go to ModeChange
    end
    else if Mode is `on`
    begin
    	if TargetFlag
        begin
			set property `target` of RoomSpec to Target
			set property `boost` of RoomSpec to 0
            go to ModeChange
        end
    end
    else if Mode is `boost`
    begin
    	clear Flag
    	if TargetFlag
        begin
			set property `target` of RoomSpec to Target
			set property `boost` of RoomSpec to 0
            set Flag
        end
    	if BoostFlag
        begin
			set property `boost` of RoomSpec to `B` cat Boost
    		set property `advance` of RoomSpec to ``
            set Flag
        end
        if Flag go to ModeChange
    end
	if Mode is CurrentMode exit

ModeChange:
	set property `mode` of RoomSpec to Mode
    set element ClickIndex of Rooms to RoomSpec
    gosub to CopyRoomsToMap
    set property `message` of Map to `confirm`
    fork to PostMap
    fork to FlashModeIndicator
	! Wait for a response from the server
   	if Demo wait 5 seconds
    else
    begin
    	put 10 into Count
        while Count is greater than 0
        begin
            wait 5 seconds
            rest get Map2 from Server cat `/map/` cat MAC cat `?v=` cat now
            if property `message` of Map2 is `OK` go to SM2
            take 1 from Count
        end
    	alert `System is not responding"
		put `Refresh` into Result
        exit
    end
SM2:
	put Map2 into Map
	clear Waiting
    put 0 into Count
    while Count is less than 10
    begin
    	if not Flashing go to SM3
        wait 10 ticks
    	add 1 to Count
    end
SM3:
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Flash the mode indicator button while waiting
FlashModeIndicator:
    set Waiting
    set Flashing
    put the index of ModeIcon into FlashIndex
	while Waiting
    begin
        index ModeIcon to FlashIndex
	    set style `opacity` of ModeIcon to `0.25`
    	wait 50 ticks
        index ModeIcon to FlashIndex
	    set style `opacity` of ModeIcon to `1.0`
        wait 50 ticks
    end
    clear Flashing
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Flash the advance button
FlashAdvance:
    set Waiting
    set Flashing
	while Waiting
    begin
	    set style `opacity` of AdvanceButton to `0.25`
    	wait 50 ticks
	    set style `opacity` of AdvanceButton to `1.0`
        wait 50 ticks
    end
    clear Flashing
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Get the current room set from the map
GetCurrentRooms:
	put property `actual` of Sensors into CurrentProfile
    put property `profiles` of Map into Profiles
    put element CurrentProfile of Profiles into Profile
    put property `rooms` of Profile into Rooms        
	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Copy the current room set to the map
CopyRoomsToMap:
	put property `actual` of Sensors into CurrentProfile
    put property `profiles` of Map into Profiles
    put element CurrentProfile of Profiles into Profile
    set property `rooms` of Profile to Rooms  
    set element CurrentProfile of Profiles to Profile
    set property `profiles` of Map to Profiles
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Post the map to the server
PostMap:
	if Demo put Map into storage as `map`
    else
	begin
   		rest post Map to Server cat `/map/` cat MAC cat `/` cat Password
        or begin
        	alert `Unable to save changes.` cat newline
            	cat `A common cause is having the wrong system password.`
        	continue
        end
	end
	stop