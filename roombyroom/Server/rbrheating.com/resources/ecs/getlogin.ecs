!	Get the login details

	script GetLogin
    
    import div MainPanel
    	and variable MAC
        and variable Password
    	and variable Flag
        and variable Result

	input MACInput
    input PWDInput
    button OKButton
    button CancelButton
    button HelpButton
    a Welcome
    variable Webson

!    debug step

    rest get Webson from `/resources/webson/getlogin.json?v=` cat now
    or begin
		print `A REST error occurred getting 'getlogin.json'`
        put `Error` into Result
        exit
	end
    render Webson in MainPanel
    attach MACInput to `getmac-input`
    attach PWDInput to `getpassword-input`
    attach OKButton to `getlogin-ok`
    attach CancelButton to `getlogin-cancel`
    attach HelpButton to `getlogin-help`
    attach Welcome to `getlogin-welcome`
    if not Flag set style `display` of CancelButton to `none`
    on click OKButton
    begin
    	put the content of MACInput into MAC
    	put the content of PWDInput into Password
        if MAC is empty go to MissingInput
        if Password is empty go to MissingInput
        clear MACInput
        clear PWDInput
      	put empty into Result
      	exit
    end
    on click CancelButton
    begin
    	put `Cancel` into Result
        exit
    end
    on click HelpButton
    begin
    	clear MainPanel
        put `Help` into Result
        exit
    end
   	on click Welcome
    begin
    	clear MainPanel
        put `Welcome` into Result
        exit
    end
    stop

MissingInput:
	alert `The MAC address and password are required.`
    stop
