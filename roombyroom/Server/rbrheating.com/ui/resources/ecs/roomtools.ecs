!	Room Tools
!	This is a menu that allows the order of presentation of rooms to be adjusted,
!	for rooms to be deleted and to call up a panel of status information for a room.

	script RoomTools
    
    import div MainPanel
    	and variable Map
        and variable Sensors
        and variable RoomSpec
        and variable ClickIndex
        and variable Result

	div RoomName
    div DialogPanel
    div DialogTitle
    div DialogText
    button RoomUpButton
    button RoomDownButton
    button RoomEditButton
    button RoomDeleteButton
    button RoomInfoButton
    button HelpButton
    button CancelButton
    button DialogButton1
    button DialogButton2
    variable CurrentProfile
    variable Profiles
    variable Profile
    variable Rooms
    variable ThisRoom
    variable ThatRoom
    variable NewRooms
    variable Webson
    variable Script
    variable Changed
    variable Timestamp
    variable DisplayTime
    variable DateFormat
    variable RequestData
    variable Responses
    variable Response
    variable Name
    variable Item
    variable Temperature
    variable Uptime
    variable State
    variable N
    variable R

!    debug step
    
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! The room tools menu
Editor:
	gosub to GetCurrentRooms
    attach RoomName to `title-room-name`
    attach RoomUpButton to `button-up`
    attach RoomDownButton to `button-down`
    attach RoomEditButton to `button-edit`
    attach RoomDeleteButton to `button-delete`
    attach RoomInfoButton to `button-info`
    attach HelpButton to `button-help`
    attach CancelButton to `button-cancel`

    if property `relayType` of RoomSpec is `Manual`
    	set style `display` of RoomInfoButton to `none`
    
    if ClickIndex is 0 set style `display` of RoomUpButton to `none`
    put the json count of Rooms into N
    take 1 from N
    if ClickIndex is N set style `display` of RoomDownButton to `none`
    
    clear Changed
    
    on click RoomUpButton
    begin
    	put element ClickIndex of Rooms into ThisRoom
        take 1 from ClickIndex
        put element ClickIndex of Rooms into ThatRoom
        set element ClickIndex of Rooms to ThisRoom
        add 1 to ClickIndex
        set element ClickIndex of Rooms to ThatRoom
        set property `action` of RequestData to `rooms`
        set property `rooms` of RequestData to Rooms
        set Changed
        go to Exit
    end
    
    on click RoomDownButton
    begin
    	put element ClickIndex of Rooms into ThisRoom
        add 1 to ClickIndex
        put element ClickIndex of Rooms into ThatRoom
        set element ClickIndex of Rooms to ThisRoom
        take 1 from ClickIndex
        set element ClickIndex of Rooms to ThatRoom
        set property `action` of RequestData to `rooms`
        set property `rooms` of RequestData to Rooms
        set Changed
        go to Exit
    end
    
    on click RoomEditButton
    begin
        clear MainPanel
    	rest get Webson from `/resources/webson/roomeditmenu.json?v=` cat now
        render Webson in MainPanel
        put empty into Result
    	rest get Script from `/resources/ecs/roomedit.ecs?v=` cat now
        run Script with MainPanel and Map and CurrentProfile and ClickIndex and Result
        exit
    end
    
    on click RoomDeleteButton
    begin
        attach DialogPanel to `dialog-panel`
    	rest get Webson from `/resources/webson/dialog-confirm.json?v=` cat now
        render Webson in DialogPanel
        attach DialogTitle to `dialog-title`
        attach DialogText to `dialog-text`
        attach DialogButton1 to `dialog-button1`
        attach DialogButton2 to `dialog-button2`
        set the content of DialogTitle to `Delete '` cat the content of RoomName cat `'`
        set the content of DialogText to `Are you sure you want to delete this room?`
        set the content of DialogButton1 to `Yes`
        set the content of DialogButton2 to `No`
        on click DialogButton1
        begin
            clear DialogPanel
            put `[]` into NewRooms
            put 0 into N
            while N is less than ClickIndex
            begin
                append element N of Rooms to NewRooms
                add 1 to N
            end
            add 1 to N
            while N is less than the json count of Rooms
            begin
                append element N of Rooms to NewRooms
                add 1 to N
            end
        	set property `action` of RequestData to `rooms`
       		set property `rooms` of RequestData to NewRooms
            set Changed
            go to Exit
        end
        on click DialogButton2
        begin
        	clear DialogPanel
            set property `request` of Result to `Redraw`
            go to ExitWithoutChanges
        end
    end
    
    on click RoomInfoButton
    begin
    	print RoomSpec
        set DateFormat to object
        set property `mode` of DateFormat to `date`
        set property `locale` of DateFormat to `en-EN`
        set Item to object
!        set property `year` of Item to `numeric`
        set property `month` of Item to `long`
        set property `day` of Item to `numeric`
        set property `hour` of Item to `numeric`
        set property `minute` of Item to `numeric`
        set property `hour12` of Item to false
        set property `options` of DateFormat to Item

        attach DialogPanel to `dialog-panel`
        rest get Webson from `/resources/webson/dialog-info.json?v=` cat now
        render Webson in DialogPanel
        attach DialogText to `dialog-text`
        attach DialogButton1 to `dialog-okbutton`
        
        put property `temperature` of RoomSpec into Temperature
        
        put property `responses` of RoomSpec into Responses
        put empty into Response
        put the json count of Responses into R
        set the elements of Response to R
        put 0 into N
        while N is less than R
        begin
        	index Response to N
        	put element N of Responses into Item
            put property `uptime` of Item into Uptime
            put property `state` of Item into State
            put `State: ` cat State into Response
            if Uptime is not 0 put Response cat `<br>Uptime: ` cat Uptime into Response
            put Response cat `<br>` into Response
            add 1 to N
        end
        put empty into Responses
        put 0 into N
        while N is less than R
        begin
        	index Response to N
        	put Responses cat Response into Responses
        	add 1 to N
        end
        
    	put element ClickIndex of Rooms into ThisRoom
        put property `name` of ThisRoom into Name
        put property `timestamp` of RoomSpec into Timestamp
        put Timestamp format DateFormat into DisplayTime
        set the content of DialogText to `Information about ` cat Name cat `:<br><br>`
        	cat `-- Sensor --<br>` cat DisplayTime
            cat `<br>Temperature: ` cat Temperature
        	cat `<br><br>-- Relays --<br>` cat Responses

        on click DialogButton1
        begin
            clear DialogPanel
            go to ExitWithoutChanges
        end
        stop
    end

    on click HelpButton
    begin
    	set property `request` of Result to `Help`
        set property `data` of Result to `home RoomTools`
    	exit
    end

    on click CancelButton go to ExitWithoutChanges
    
    put `{}` into Result
    put `{}` into RequestData
    set property `action` of RequestData to empty
    set property `roomnumber` of RequestData to ClickIndex
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Get the current room set from the map
GetCurrentRooms:
	put property `actual` of Sensors into CurrentProfile
   	put property `profiles` of Map into Profiles
    put element CurrentProfile of Profiles into Profile
    put property `rooms` of Profile into Rooms
	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Exit without changes
ExitWithoutChanges:
	set property `request` of Result to `Redraw`
    clear Changed
    go to Exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Exit to the main script
Exit:
	if Changed
    begin
    	set property `request` of Result to `Update`
        set property `data` of Result to RequestData
    end
    else
    begin
    	set property `request` of Result to `Redraw`
    end
    exit