!	Room Editor
!	Manages a menu of actions all relating to room characteristics,
!	specifically to set the name of the room, identify the thermometer and relay(s)
!	used in the room, and call the period editor.

	script RoomEditor
    
    import div MainPanel
    	and variable Map
        and variable CurrentProfile
        and variable ClickIndex
        and variable Result

	div RoomName
    div DialogPanel
    div DialogTitle
    div DialogText
    div DialogText2
    div P100Login
    button EditNameButton
    button EditDevicesButton
    button EditTimesButton
    button HelpButton
    button CancelButton
    button DialogButton1
    button DialogButton2
    button DialogButton3
    input DialogInput
    input ProtectTemp
    input P100Email
    input P100Password
    input Protect
    textarea TextArea
    select RelayTypes
    variable Profiles
    variable Profile
    variable Rooms
    variable Room
    variable Webson
    variable Relays
    variable Item
    variable Items
    variable Name
    variable PeriodEditScript
    variable RequestData
    variable Changed
    variable List
    variable R

!    debug step
    
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! The room editor
Editor:
    attach RoomName to `title-room-name`
    attach EditNameButton to `button-name`
    attach EditDevicesButton to `button-devices`
    attach EditTimesButton to `button-times`
    attach HelpButton to `button-help`
    attach CancelButton to `button-cancel`
    
    gosub to GetCurrentRooms
    put element ClickIndex of Rooms into Room
    set the content of RoomName to property `name` of Room
    
    on click EditNameButton go to NameButtonCB
    
    on click EditDevicesButton go to DevicesButtonCB
 
    on click EditTimesButton go to TimesButtonCB
    
    on click CancelButton go to ExitWithoutChanges
    
    on click HelpButton
    begin
        put `Help home RoomEdit` into Result
        exit
    end
    
    put `{}` into Result
    put `{}` into RequestData
    set property `action` of RequestData to empty
    set property `roomnumber` of RequestData to ClickIndex
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Edit the name
NameButtonCB:
    attach DialogPanel to `dialog-panel`
    rest get Webson from `/resources/webson/dialog-input.json?v=` cat now
    render Webson in DialogPanel
    attach DialogTitle to `dialog-title`
    attach DialogText to `dialog-text`
    attach DialogButton1 to `dialog-button1`
    attach DialogButton2 to `dialog-button2`
    attach DialogInput to `dialog-input`
    set the content of DialogTitle to `Rename '` cat the content of RoomName cat `'`
    set the content of DialogText to `Type the new name for this room`
    set the content of DialogButton1 to `OK`
    set the content of DialogButton2 to `Cancel`
    set the content of DialogInput to the content of RoomName
    
    on click DialogButton1
    begin
    	put the content of DialogInput into Name
        set property `name` of Room to Name
    	set the content of RoomName to Name
        clear DialogPanel
        set property `action` of RequestData to `roomname`
        set property `name` of RequestData to Name
        set Changed
        go to Exit
    end
    
    on click DialogButton2
    begin
        clear DialogPanel
        go to ExitWithoutChanges
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Edit the devices
DevicesButtonCB:
	attach DialogPanel to `dialog-panel`
    set style `width` of DialogPanel to `80%`
    set style `left` of DialogPanel to `10%`
    rest get Webson from `/resources/webson/getdevices.json?v=` cat now
    render Webson in DialogPanel
    attach DialogTitle to `dialog-title`
    attach DialogText to `dialog-text`
    attach DialogText2 to `dialog-text2`
    attach TextArea to `dialog-textarea`
    attach DialogButton1 to `dialog-button1`
    attach DialogButton2 to `dialog-button2`
    attach DialogButton3 to `dialog-button3`
    attach DialogInput to `dialog-input`
    attach RelayTypes to `relayType`
    attach P100Login to `p100-login`
    attach P100Email to `p100-email`
    attach P100Password to `p100-password`
    attach ProtectTemp to `dialog-winter-temp`
    attach Protect to `dialog-winter-protect`
    
    if property `protect` of Room is `yes` set attribute `checked` of Protect to `checked`
    set the content of ProtectTemp to  property `ptemp` of Room
    
    put `[]` into List
    json add `RBR XR` to List
    json add `Shelly One` to List
    json add `Tasmota` to List
    json add `TP-Link P100` to List
    json add `Manual` to List
    set RelayTypes from List as property `relayType` of Room
    if property `relayType` of Room is `TP-Link P100`
    begin
        set the content of P100Email to property `p100Email` of Room
        set the content of P100Password to property `p100Password` of Room
    	set style `display` of P100Login to `block`
    end
    on change RelayTypes
    begin
        set property `relayType` of Room to attribute `value` of RelayTypes
        if attribute `value` of RelayTypes is `TP-Link P100`
        begin
        	set style `display` of P100Login to `block`
            set the content of P100Email to property `p100Email` of Room
            set the content of P100Password to property `p100Password` of Room
        end
        else
        begin
        	set style `display` of P100Login to `none`
            set style `display` of DialogText2 to `block`
            set style `display` of TextArea to `block`
        end
    end
    
    set the content of DialogTitle to `Sensor and relays for '` cat property `name` of Room cat `'`
    set the content of DialogText to `Type the IP address of the temperature sensor`
    set the content of DialogText2 to `Type the relay identifiers (name, address etc.), 1 per line`
    set the content of DialogButton1 to `Save`
    set the content of DialogButton2 to `Help`
    set the content of DialogButton3 to `Cancel`
    set the content of DialogInput to property `sensor` of Room
    put property `relays` of Room into Relays
    put empty into Item
    put 0 into R
    while R is less than the json count of Relays
    begin
        if Item is not empty put Item cat newline into Item
        put Item cat element R of Relays into Item
        add 1 to R
    end
    set the content of TextArea to Item
    
    on click DialogButton1
    begin
    	put `{}` into Items
        set property `sensor` of Items to the content of DialogInput
        if attribute `checked` of Protect
        begin
        	if the content of ProtectTemp is empty set the content of ProtectTemp to `0.0`
        	set property `protect` of Items to `yes`
            set property `ptemp` of Items to the content of ProtectTemp
        end
        else set property `protect` of Items to `no`
        set property `relayType` of Items to property `relayType` of Room
        if property `relayType` of Items is `TP-Link P100`
        begin
            set property `p100Email` of Items to the content of P100Email
            set property `p100Password` of Items to the content of P100Password
        end
        put `[]` into Relays
        split the content of TextArea on newline giving Item
        put 0 into R
        while R is less than the elements of Item
        begin
            index Item to R
            append Item to Relays
            add 1 to R
        end
        set property `relays` of Items to Relays
        clear DialogPanel
        set property `action` of RequestData to `roomdata`
        set property `items` of RequestData to Items
        set Changed
        go to Exit
    end
    
    on click DialogButton2
    begin
    	clear DialogPanel
    	put `{}` into Result
        set property `request` of Result to `Help`
        set property `data` of Result to `home Devices`
		exit
    end
    
    on click DialogButton3
    begin
    	clear DialogPanel
    	go to ExitWithoutChanges
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Get the current room set from the map
GetCurrentRooms:
   	put property `profiles` of Map into Profiles
    put element CurrentProfile of Profiles into Profile
    put property `rooms` of Profile into Rooms        
	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Edit the times
TimesButtonCB:
    rest get PeriodEditScript from `/resources/ecs/periodedit.ecs?v=` cat now
    run PeriodEditScript with MainPanel and Map and CurrentProfile and ClickIndex and Result
	exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Exit without changes
ExitWithoutChanges:
	set property `request` of Result to `Redraw`
    clear Changed
    go to Exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Exit to the main script
Exit:
	if Changed
    begin
    	set property `request` of Result to `Update`
        set property `data` of Result to RequestData
    end
    else
    begin
    	set property `request` of Result to `Redraw`
    end
    exit