!	Config

	script Config

    import variable MAC
        and variable Password
        and variable Map
        and variable Result
    
    div Screen
    div ConfigPanel
    div RoomList
    input SystemName
    input SystemAddress
    input SystemSSID
    input SystemPassword
    input Extender0SSID
    input Extender0Password
    input RoomName
    input RoomSSID
    button SaveButton
    button CancelButton
    variable Profiles
    variable Profile
    variable Rooms
    variable Room
    variable ConfigWebson
    variable RoomWebson
    variable Webson
    variable Extenders
    variable Extender
    variable RequestData
    variable NRooms
    variable Names
    variable SSIDs
    variable R

!	debug step

    put `{}` into Result
    put `{}` into RequestData
    set property `action` of RequestData to empty
    
    put property `profiles` of Map into Profiles
    put element 0 of Profiles into Profile
    put property `rooms` of Profile into Rooms        
    put the json count of Rooms into NRooms
    set the elements of RoomName to NRooms
    set the elements of RoomSSID to NRooms

	attach Screen to `rbr-screen`
    rest get ConfigWebson from `/resources/webson/config.json?v=` cat now or exit
    rest get RoomWebson from `/resources/webson/configroom.json?v=` cat now or exit
    render ConfigWebson in Screen
	attach ConfigPanel to `config-panel`
    attach SystemName to `cf-name`
    attach SystemAddress to `cf-address`
    attach SystemSSID to `cf-system-ssid`
    attach SystemPassword to `cf-system-password`
    attach Extender0SSID to `cf-extender0-ssid`
    attach Extender0Password to `cf-extender0-password`
    attach RoomList to `cf-rooms`
    attach SaveButton to `cf-save-button`
    attach CancelButton to `cf-cancel-button`
    
    set the content of SystemName to property `name` of Map
    set the content of SystemAddress to property `address` of Map
    set the content of SystemSSID to property `ssid` of Map
    set the content of SystemPassword to property `password` of Map
    put property `extenders` of Map into Extenders
    if Extenders is empty
    begin
    	put `[]` into Extenders
    	put `{}` into Extender
        append Extender to Extenders
        append Extender to Extenders
        append Extender to Extenders
        append Extender to Extenders
        set property `extenders` of Map to Extenders
    end
    put element 0 of Extenders into Extender
    set the content of Extender0SSID to property `ssid` of Extender
    set the content of Extender0Password to property `password` of Extender
    
    put 0 into R
    while R is less than NRooms
    begin
    	put element R of Rooms into Room
        index RoomName to R
        index RoomSSID to R
        put RoomWebson into Webson
        replace `/ROOM/` with R in Webson
        render Webson in RoomList
        attach RoomName to `cf-room` cat R cat `-name`
        attach RoomSSID to `cf-room` cat R cat `-ssid`
        set the content of RoomName to property `name` of Room
        set the content of RoomSSID to property `ssid` of Room
        add 1 to R
    end

    on click SaveButton
    begin
		set property `action` of RequestData to `config`
        set property `name` of RequestData to the content of SystemName
        set property `address` of RequestData to the content of SystemAddress
        set property `ssid` of RequestData to the content of SystemSSID
        set property `password` of RequestData to the content of SystemPassword
        put property `extenders` of Map into Extenders
        put element 0 of Extenders into Extender
        set property `ssid` of Extender to the content of Extender0SSID
        set property `password` of Extender to the content of Extender0Password
        set element 0 of Extenders to Extender
        set property `extenders` of RequestData to Extenders
        put `[]` into Names
        put `[]` into SSIDs
        put 0 into R
        while R is less than NRooms
        begin
        	attach RoomName to `cf-room` cat R cat `-name`
        	attach RoomSSID to `cf-room` cat R cat `-ssid`
            append the content of RoomName to Names
            append the content of RoomSSID to SSIDs
        	add 1 to R
        end
        set property `names` of RequestData to Names
        set property `ssids` of RequestData to SSIDs

    	set property `request` of Result to `Update`
		set property `data` of Result to RequestData
print Result
    	remove element ConfigPanel
        exit
	end

    on click CancelButton
    begin
    	remove element ConfigPanel
        exit
	end

	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Get the current room set from the map. Use the first profile.
GetCurrentRooms:
    put property `profiles` of Map into Profiles
    put element 0 of Profiles into Profile
    put property `rooms` of Profile into Rooms        
	return
