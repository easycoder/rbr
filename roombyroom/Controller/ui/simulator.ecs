!   simulator.ecs - the device simulator

    script Simulator

    import dictionary ImportedRoom

    list Times
    list Events
    dictionary Item
    dictionary AllParams
    dictionary Params
    dictionary Period
    dictionary RoomData
    dictionary Message
    dictionary Room
    variable RoomName
    variable TempNow
    variable TempWas
    variable PeriodNow
    variable PeriodWas
    variable PeriodChanged
    variable Advance
    variable Rows
    variable Value
    variable Time
    variable Last
    variable PeriodStart
    variable Elapsed
    variable OnOffTime
    variable OnOffTemp
    variable Delta
    variable Ceiling
    variable AmbientNow
    variable Target
    variable TargetWas
    variable Temp
    variable Boost
    variable Result
    variable Relay
    variable Mode
    variable I
    variable N
    variable P
    variable T

!    debug compile
!    debug step

    copy ImportedRoom to Room
    put entry `name` of Room into RoomName
    put entry `mode` of Room into Mode
    put `` cat entry `advance` of Room into Advance

!   Load the environmental data
    load Rows from `environment.csv`
    split Rows
    reset Times
    put 0 into N
    while N is less than the elements of Rows
    begin
        index Rows to N
        if Rows is not empty
        begin
            put Rows into Value
            split Value on `,`
            if the elements of Value is 3
            begin
                reset Item
                index Value to 0
                set entry `time` of Item to Value
                index Value to 1
                set entry `temperature` of Item to Value
                index Value to 2
                set entry `boost` of Item to Value
                append Item to Times
            end
        end
        increment N
    end
!    print prettify Times

    put entry `events` of Room into Events
!    log prettify Events

!   Initialise the parameters for a single room
    load AllParams from `../config.json`
    if AllParams has entry RoomName put entry RoomName of AllParams into Params
    else
    begin
        reset Params
        set entry `rate-up` of Params to 10
        set entry `rate-down` of Params to 10
        set entry `ceiling` of Params to 23
        set entry `boost` of Params to 50
        set entry RoomName of AllParams to Params
        save prettify AllParams to `../params.json`
    end

    put 1500 into Target
    put 1500 into Temp
    put Temp into OnOffTemp
    put Temp into TempNow
    put now into OnOffTime
    put 0 into Last
    put 0 into PeriodNow
    put 0 into PeriodWas
    set Relay to `off`

    release parent
    on message
    begin
        put the message into Room
        log entry `name` of Room cat ` updated`
        if entry `name` of Room is `Kitchen` log entry `advance` of Room
    end

!   Run the simulator
    put entry `ceiling` of Params into Temp
    gosub to ConvertTempToInt
    put Temp into Ceiling
    while true
    begin
        ! Wait a semi-random period
        put random 3 into T
        add 10 to T
        wait T seconds
        put `` cat entry `advance` of Room into Advance
        if RoomName is `Kitchen` log entry `advance` of Room
        gosub to SimulateStep
    end
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Find the current ambient temperature
FindAmbientTemp:
    set P to 0
FAT2:
    if P is less than the count of Times
    begin
        put item P of Times into Item
        put entry `time` of Item into Time
        gosub to ConvertTimeToInt
        add today to Time
        if now is greater than Time
        begin
            increment P
            go to FAT2
        end
    end
    if P is 0 put the count of Times into P
    decrement P
    put item P of Times into Item
    put entry `temperature` of Item into Temp
    gosub to ConvertTempToInt
    put Temp into AmbientNow
    put entry `boost` of Item into Temp
    gosub to ConvertTempToInt
    put entry `boost` of Params into Boost
    multiply Temp by Boost
    divide Temp by 100
    add Temp to AmbientNow
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Find which period we're in (to get the target temperature)
FindCurrentPeriod:
    put entry `events` of Room into Events
    if the count of Events is 0 return
    put PeriodNow into PeriodWas
    set P to 0
FCP2:
    if P is the count of Events
    begin
        put 0 into P
        put item 0 of Events into Period
    end
    else
    begin
        put item P of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
        add today to Time
        if Time is less than now
        begin
            put P into PeriodNow
            increment P
            go to FCP2
        end
        put item P of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
        put Time into PeriodStart
        add today to PeriodStart
    end
    put entry `temp` of Period into Temp
    gosub to ConvertTempToInt
    put Temp into Target
    if Advance is not `-`
    begin
        if Advance is `A`
        begin
            if P is the count of Events put 0 into P
            put `` cat P into Advance
            set entry `advance` of Room to Advance
            gosub to NotifyMainOfAdvance
        end
        if RoomName is `Kitchen` log RoomName cat `:Current is ` cat PeriodNow cat `: advanced to ` cat Advance
        if Advance is not `-`
        begin
            put the value of Advance into P
            if P is PeriodNow
            begin
                log `Cancel the advance`
                set entry `advance` of Room to `-`
                gosub to NotifyMainOfAdvance
            end
        end
        put item P of Events into Period
        put entry `temp` of Period into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Notify the main that an advance change has occurred
NotifyMainOfAdvance:
    reset Message
    set entry `subject` of Message to `Advance`
    set entry `Advance` of Message to Advance
    set entry `name` of Message to RoomName
    send Message to parent

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert an HH:MM time into an int seconds value
ConvertTimeToInt:
    put `` cat Time into Time
    put the index of `:` in Time into I
    put the value of left I of Time into Result
    multiply Result by 60
    increment I
    put the value of from I of Time into Time
    add Time to Result
    multiply Result by 60000 giving Time
    return

! Convert a temperature string into an int hundredths value
ConvertTempToInt:
    if Temp is empty put 0 into Temp
    put the index of `.` in Temp into I
    if I is less than 0 multiply Temp by 100
    else
    begin
        put the value of left I of Temp into Result
        multiply Result by 100
        increment I
        put the value of from I of Temp into Temp
        add Result to Temp
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Run one step of the simulation
SimulateStep:
    if Room is empty return
    put TempNow into TempWas
    put Target into TargetWas
    gosub to FindAmbientTemp

    if Mode is `timed`
    begin
        gosub to FindCurrentPeriod
    end
    else if Mode is `on`
    begin
    end
    else if Mode is `boost`
    begin
    end
    else put 0 into Target

    ! Calculate the new temperature
    take OnOffTime from now giving Elapsed
    if Relay is `on` put entry `rate-up` of Params into Delta
    else put entry `rate-down` of Params into Delta
    multiply Delta by Elapsed
    divide Delta by 36000
    if Relay is `on`
    begin
        add Delta to OnOffTemp giving TempNow
        if TempNow is greater than Ceiling put Ceiling into TempNow
    end
    else
    begin
        take Delta from OnOffTemp giving TempNow
        if TempNow is less than AmbientNow put AmbientNow into TempNow
    end

    if Mode is `off` put TempNow into OnOffTemp
    else
    begin
        if TempNow is less than Target
        begin
            if Relay is `off`
            begin
                put now into OnOffTime
                put TempNow into OnOffTemp
                set Relay to `on`
            end
        end
        else
        begin
            if Relay is `on`
            begin
                put now into OnOffTime
                put TempNow into OnOffTemp
                set Relay to `off`
            end
        end
    end

    add 600000 to Last giving T
    if now is greater than T
    begin
        reset RoomData
        if PeriodNow is not PeriodWas set PeriodChanged else clear PeriodChanged
        if TempNow is TempWas
            if Target is TargetWas
                if not PeriodChanged
                    return
        set entry `temperature` of RoomData to TempNow
        set entry `target` of RoomData to Target
        set entry `relay` of RoomData to Relay
        set entry `period` of RoomData to PeriodChanged
        reset Message
        set entry `subject` of Message to `roomstate`
        set entry `name` of Message to RoomName
        set entry `data` of Message to RoomData
        send Message to parent
        put now into Last
    end
    return
