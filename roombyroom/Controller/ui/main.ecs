!   main.ecs - the main program script

    script Main

    use mqtt

    dictionary Config
    dictionary Map
    dictionary Profile
    dictionary Room
    dictionary Response
    dictionary Message
    dictionary Sender
    dictionary ReceivedMessage
    dictionary Data
    dictionary WaitForConfirmation
    list Profiles
    list Rooms
    module DeviceModule
    topic ServerTopic
    topic SenderTopic
    variable MQTT_Token
    variable DeviceScript
    variable RoomCount
    variable RoomName
    variable RoomIndex
    variable CurrentProfile
    variable ConfirmationRequested
    variable MapChanged
    variable StateChanged
    variable MessageText
    variable SenderName
    variable SenderQoS
    variable MyID
    variable Subject
    variable Action
    variable Mode
    variable R

!    debug step

    ! Get the MQTT token
    if file `~/.mqqt_token` does not exist
    begin
        log `No MQTT token could be found.`
        exit
    end
    load MQTT_Token from `~/.mqqt_token`

    ! Set up MQTT
    put `38:54:39:34:62:d7/request` into MyID
    init ServerTopic
        name MyID
        qos 1

    mqtt
        token MQTT_Token
        id uuid
        broker `mqtt.flespi.io`
        port 8883
        subscribe ServerTopic
    
    on mqtt connect go to Start
    on mqtt message go to HandleMQTTMessage
    stop

Start:
    gosub to LoadMap

    if file `../config.json` exists load Config from `../config.json`
    else
    begin
        reset Config
        set entry `device_script` of Config to `simulator.ecs`
    end
    put entry `device_script` of Config into DeviceScript

    ! Launch a module for each room
    put 0 into R
    while R is less than RoomCount
    begin
        put item R of Rooms into Room
        put entry `name` of Room into RoomName
        index DeviceModule to R
        run DeviceScript as DeviceModule
        log `Running ` cat entry `name` of Room
        send Room to DeviceModule
        wait 1 tick
        increment R
    end

    on message
    begin
        put the message into Message
        put entry `subject` of Message into Subject
        if Subject is `roomstate`
        begin
!        log Message
            put entry `name` of Message into RoomName
            gosub to GetRoomByName
            set entry `temperature` of Room to entry `temperature` of Message
            set entry `target` of Room to entry `target` of Message
            set entry `period` of Room to entry `period` of Message
            set entry `relay` of Room to entry `relay` of Message
            gosub to UpdateRooms 
        end
        else if Subject is `Advance`
        begin
            put entry `name` of Message into RoomName
            gosub to GetRoomByName
            set entry `temperature` of Room to entry `temperature` of Message
            set entry `advance` of Room to entry `Advance` of Message
            set entry `period` of Room to entry `period` of Message
            gosub to UpdateRooms 
        end
    end

    clear ConfirmationRequested
    set MapChanged
    set StateChanged
    log `All set up and ready`
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LoadMap:
    load Map from `../map.json`
InitVariables:
    put entry `profiles` of Map into Profiles
    put entry `profile` of Map into CurrentProfile
    put item CurrentProfile of Profiles into Profile
    put entry `rooms` of Profile into Rooms
    put the count of Rooms into RoomCount
    set the elements of DeviceModule to RoomCount
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
HandleMQTTMessage:
    put the mqtt message into ReceivedMessage
!    log `Incoming: ` cat ReceivedMessage
    put entry `sender` of ReceivedMessage into Sender
    put entry `action` of ReceivedMessage into Action
    put entry `message` of ReceivedMessage into Data
    if Action is `first`
    begin
        put `map` into Action
        set MapChanged
        set StateChanged
    end
    if Action is `map` go to SendMap
    else if Action is `uirequest` go to ProcessUIRequest
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
GetRoomByName:
    put 0 into RoomIndex
    while RoomIndex is less than RoomCount
    begin
        put item RoomIndex of Rooms into Room
        if entry `name` of Room is RoomName return
        increment RoomIndex
    end
    ! Not found
    reset Room
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
UpdateRooms:
    set item RoomIndex of Rooms to Room
UpdateProfile:
    set entry `rooms` of Profile to Rooms
UpdateProfiles:
    set item CurrentProfile of Profiles to Profile
UpdateMap:
    set entry `profiles` of Map to Profiles
    set MapChanged
    save prettify Map to `../map.json`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ProcessUIRequest:
    put entry `action` of Data into Action
    if Action is `System Name`
    begin
        set entry `name` of Map to entry `System Name` of Data
        log `Set the system name to ` cat entry `name` of Map
    end
    else if Action is `Request Relay`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        log `Set the request relay to ` cat entry `request` of Map
    end
    else if Action is `Add Room`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        put entry `Add Room` of Data into Room
        log `Add room ` cat entry `name` of Room
        append Room to Rooms
        set entry `rooms` of Profile to Rooms
        gosub to UpdateProfiles
    end
    else if Action is `Select Profile`
    begin
        set entry `profile` of Map to entry `Select Profile` of Data
        log `Set profile ` cat entry `profile` of Map
    end
    else if Action is `Operating Mode`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty stop
        put entry `Operating Mode` of Data into Mode
        log `Set Operating Mode of ` cat entry `name` of Room cat ` to ` cat Mode
        set entry `mode` of Room to Mode
        if Mode is `timed`
        begin
            if Data has entry `Advance`
            begin
                if `` cat entry `advance` of Room is `-` set entry `advance` of Room to `A`
                else set entry `advance` of Room to `-`
                index DeviceModule to RoomIndex
!                gosub to InitVariables
!                set Data to item RoomIndex of Rooms
!                set entry `temperature` of Room to entry `temperature` of Data
                log `Update ` cat entry `name` of Room
                send Room to DeviceModule
            end
        end
        gosub to UpdateRooms
    end
    else if Action is `Periods`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty stop
        log `Set the periods of ` cat entry `name` of Room
        set entry `events` of Room to entry `Periods` of Data
        index DeviceModule to RoomIndex
        send Room to DeviceModule
        gosub to UpdateRooms
    end
PUR2:
    set MapChanged

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SendMap:
    if Map is empty gosub to LoadMap
    if MapChanged
    begin
        gosub to InitVariables
        put Map into MessageText
        clear MapChanged
        clear StateChanged
        go to SendReply
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Send a reply message
SendReply:
!    log Sender
    put entry `name` of Sender into SenderName
    put entry `qos` of Sender into SenderQoS
    init SenderTopic
        name SenderName
        qos SenderQoS
    if ConfirmationRequested
    begin
        set entry SenderName of WaitForConfirmation
        set Action to `confirm`
    end
    else set Action to `reply`
!    log left 20 of MessageText
    send to SenderTopic
        action Action
        message MessageText
!    log `Reply sent`
    clear ConfirmationRequested
    stop