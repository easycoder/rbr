!   roomController.ecs - the controller for the room devices

    script RoomController

    list Events
    dictionary Item
    dictionary Params
    dictionary Period
    dictionary Message
    dictionary Room
    variable FirstTime
    variable RoomName
    variable TempNow
    variable TempWas
    variable PeriodNow
    variable PeriodWas
    variable PeriodActive
    variable Advance
    variable Value
    variable Time
    variable Last
    variable Elapsed
    variable OnOffTime
    variable Delta
    variable Ceiling
    variable AmbientNow
    variable Target
    variable TargetWas
    variable Temp
    variable Subject
    variable Result
    variable MessageReceived
    variable RelayState
    variable PreviousRelayState
    variable Mode
    variable EventCount
    variable StepCount
    variable Tenths
    variable I
    variable N
    variable P
    variable T

!   Simulator variables
    dictionary AllParams
    list Times
    variable Rows
    variable SolarBoost

!    debug compile
!    debug Step

    on message go to ProcessMessage
    set FirstTime
    put now into Last
    put 0 into StepCount
    release parent
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a message received from the main script
ProcessMessage:
    set MessageReceived
    put the message into Room
    put entry `mode` of Room into Mode
    put `` cat entry `advance` of Room into Advance
    put entry `events` of Room into Events

    if not FirstTime stop

    put entry `name` of Room into RoomName

    gosub to SimulateEnvironment

    put 1500 into Target
    put 1500 into Temp
    put Temp into TempNow
    put now into OnOffTime
    put 0 into PeriodNow
    put 0 into PeriodWas
    set RelayState to `off`
    clear FirstTime

    ! Run the controller
    while true
    begin
        ! Wait a fixed or semi-random period.
        ! The random part is needed for simulation, otherwise it can be a fixed 10 seconds
        put random 3 into T
        add 10 to T
        multiply T by 10 giving Tenths
        put 0 into T
        while T is less than Tenths
        begin
            wait 10 ticks
            increment T
            if MessageReceived
            begin
                clear MessageReceived
                put Tenths into T
            end
        end
        if Room is not empty gosub to Step
    end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! This is a single step, repeated every 10 seconds
Step:
    increment StepCount
    put TempNow into TempWas
    put Target into TargetWas
    gosub to FindAmbientTemp
    put `` cat entry `advance` of Room into Advance

    if Mode is `timed`
    begin
        gosub to FindCurrentPeriod
    end
    else if Mode is `on`
    begin
    end
    else if Mode is `boost`
    begin
        put entry `target` of Room into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
    else
    begin
        set Mode to `off`
    end

    gosub to Simulate

    ! Decide if the relay should be on or off
    put RelayState into PreviousRelayState
    if Mode is `off` set entry `relay` of Room to `off`
    else 
    begin
        if TempNow is less than Target
        begin
            if RelayState is `off`
            begin
                put now into OnOffTime
                set RelayState to `on`
            end
        end
        else
        begin
            if RelayState is `on`
            begin
                put now into OnOffTime
                set RelayState to `off`
            end
        end
    end
    if PeriodNow is not PeriodWas
    begin
        log RoomName cat `: Period ` cat PeriodWas cat `->` cat PeriodNow
        set PeriodWas to PeriodNow
    end
    if RelayState is not PreviousRelayState
    begin
        put `roomstate` into Subject
        gosub to SendRoomStateMessage
    end
    else
    begin
        ! Don't update until at least a minute since the last change
        add 60000 to Last giving T
!        if RoomName is `Kitchen` log T cat ` ` cat now
        if now is greater than T
        begin
            if TempNow is TempWas
                if Target is TargetWas
                    begin
!                        if RoomName is `Kitchen` log `Nothing to do`
                        put now into Last
                        return
                    end
!            if RoomName is `Kitchen` log `Update the UI`
            put `roomstate` into Subject
            gosub to SendRoomStateMessage
        end
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Find which period we're in (to get the target temperature)
FindCurrentPeriod:
    put entry `events` of Room into Events
    put the count of Events into EventCount
    if EventCount is 0 return
    put PeriodNow into PeriodWas
    set PeriodNow to 0
FCP2:
!    if RoomName is `Kitchen` log `Examine period ` cat PeriodNow
    if PeriodNow is EventCount
    begin
        ! We're past the last event time, so use the first
        put 0 into PeriodNow
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
    end
    else
    begin
        ! Check the current period. If we've passed it, move on.
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
        ! If we've passed this period, get the start of the next period and move on
!        if RoomName is `Kitchen` log `now=` cat now cat `, Time=` cat Time
        if now is greater than Time
        begin
            increment PeriodNow
            go to FCP2
        end
        ! Otherwise, we've found the period
    end
    put PeriodNow into PeriodActive

    ! Now get the target temperature
    put entry `temp` of Period into Temp
    gosub to ConvertTempToInt
    put Temp into Target

    ! Deal with Advance
    put PeriodNow into P
    if Advance is not `-`
    begin
        if Advance is `A`
        begin
            add 1 to PeriodNow giving P
            if P is EventCount put 0 into P
            put `` cat P into Advance
            set entry `advance` of Room to Advance
            set PeriodActive to P
            set entry `subject` of Message to `Advance`
            gosub to SendRoomStateMessage
        end
        if Advance is not `-`
        begin
            put the value of Advance into P
            if P is PeriodNow
            begin
                log `Cancel the advance`
                set entry `advance` of Room to `-`
                set entry `subject` of Message to `Advance`
                gosub to SendRoomStateMessage
            end
        end
        put P into PeriodActive
        put item P of Events into Period
        put entry `temp` of Period into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
!    if RoomName is `Kitchen` log `Step ` cat StepCount cat `: ` cat PeriodNow cat `/` cat PeriodActive
!        cat ` until ` cat entry `until` of Period cat `, Target=` cat Target
!        cat `, Temp=` cat TempNow cat `, Relay=` cat RelayState
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Send a room state message back to the main
SendRoomStateMessage:
!    if RoomName is `Kitchen` log Target cat `/` cat TempNow cat ` - ` cat RelayState
    reset Message
    set entry `subject` of Message to Subject
    set entry `name` of Message to RoomName
    set entry `temperature` of Message to TempNow
    set entry `target` of Message to Target
    set entry `relay` of Message to RelayState
    set entry `period` of Message to PeriodActive
    set entry `Advance` of Message to Advance
    send Message to parent
    put now into Last
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Find the current ambient temperature
FindAmbientTemp:
    set P to 0
FAT2:
    if P is less than the count of Times
    begin
        put item P of Times into Item
        put entry `time` of Item into Time
        gosub to ConvertTimeToInt
        if now is greater than Time
        begin
            increment P
            go to FAT2
        end
    end
    if P is 0 put the count of Times into P
    decrement P
    put item P of Times into Item
    put entry `temperature` of Item into Temp
    gosub to ConvertTempToInt
    put Temp into AmbientNow
    put entry `boost` of Item into Temp
    gosub to ConvertTempToInt
    put entry `boost` of Params into SolarBoost
    multiply Temp by SolarBoost
    divide Temp by 100
    add Temp to AmbientNow
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert an HH:MM time into an int seconds value
ConvertTimeToInt:
    put `` cat Time into Time
    put the index of `:` in Time into I
    put the value of left I of Time into Result
    multiply Result by 60
    increment I
    put the value of from I of Time into Time
    add Time to Result
    multiply Result by 60000 giving Time
    add today to Time
    return

! Convert a temperature string into an int hundredths value
ConvertTempToInt:
    if Temp is empty put 0 into Temp
    put the index of `.` in Temp into I
    if I is less than 0 multiply Temp by 100
    else
    begin
        put the value of left I of Temp into Result
        multiply Result by 100
        increment I
        put the value of from I of Temp into Temp
        add Result to Temp
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! This is the simulator, that changes the temperature of the room
! according to a simple algorithm
Simulate:
    take OnOffTime from now giving Elapsed
    if RelayState is `on` put entry `rate-up` of Params into Delta
    else put entry `rate-down` of Params into Delta
    multiply Delta by Elapsed
    divide Delta by 36000
    if RelayState is `on`
    begin
        add Delta to TempNow
        if TempNow is greater than Ceiling put Ceiling into TempNow
    end
    else
    begin
        take Delta from TempNow
        if TempNow is less than AmbientNow put AmbientNow into TempNow
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Get the simulated environment
SimulateEnvironment:
    load Rows from `environment.csv`
    split Rows
    reset Times
    put 0 into N
    while N is less than the elements of Rows
    begin
        index Rows to N
        if Rows is not empty
        begin
            put Rows into Value
            split Value on `,`
            if the elements of Value is 3
            begin
                reset Item
                index Value to 0
                set entry `time` of Item to Value
                index Value to 1
                set entry `temperature` of Item to Value
                index Value to 2
                set entry `boost` of Item to Value
                append Item to Times
            end
        end
        increment N
    end

    ! Initialise the parameters for this room
    load AllParams from `../config.json`
    if AllParams has entry RoomName put entry RoomName of AllParams into Params
    else
    begin
        reset Params
        set entry `rate-up` of Params to 10
        set entry `rate-down` of Params to 10
        set entry `ceiling` of Params to 23
        set entry `boost` of Params to 50
        set entry RoomName of AllParams to Params
        save prettify AllParams to `../params.json`
    end

    put entry `ceiling` of Params into Temp
    gosub to ConvertTempToInt
    put Temp into Ceiling
    return