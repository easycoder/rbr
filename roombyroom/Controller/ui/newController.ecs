!   newController.ecs - the main program script

    script Controller

    use mqtt

    dictionary Map
    dictionary Profile
    dictionary Room
    dictionary Period
    dictionary Sender
    dictionary ReceivedMessage
    dictionary Data
    dictionary WaitForConfirmation
    list Profiles
    list Rooms
    list Events
    topic ServerTopic
    topic SenderTopic
    variable MQTT_Token
    variable RoomCount
    variable RoomName
    variable RoomIndex
    variable CurrentProfile
    variable Interrupt
    variable TempNow
    variable TempWas
    variable Target
    variable TargetWas
    variable PeriodNow
    variable PeriodWas
    variable PeriodActive
    variable RelayState
    variable RelayStateWas
    variable EventCount
    variable LastMapUpdate
    variable MapHasChanged
    variable LastUpdateTime
    variable Advance
    variable Time
    variable Temp
    variable ImmediateUpdate
    variable ConfirmationRequested
    variable StateChanged
    variable MessageText
    variable SenderName
    variable SenderQoS
    variable MyID
    variable Subject
    variable Action
    variable Mode
    variable I
    variable P
    variable R
    variable T
    module Simulator

!    debug step

    run `simulator.ecs` as Simulator with TempNow
    set LastMapUpdate to now
    clear MapHasChanged

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set up MQTT
    if file `~/.mqqt_token` does not exist
    begin
        log `No MQTT token could be found.`
        exit
    end
    load MQTT_Token from `~/.mqqt_token`

    ! Set up MQTT
    put `38:54:39:34:62:d7/request` into MyID
    init ServerTopic
        name MyID
        qos 1

    mqtt
        token MQTT_Token
        id uuid
        broker `mqtt.flespi.io`
        port 8883
        subscribe ServerTopic
    
    on mqtt connect go to Start
    on mqtt message go to HandleMQTTMessage

    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   The main start point. This may be revisited.
Start:
    log `Load the system map`
    load Map from `../map.json`
InitVariables:
    put entry `profiles` of Map into Profiles
    put entry `profile` of Map into CurrentProfile
    put item CurrentProfile of Profiles into Profile
    put entry `rooms` of Profile into Rooms
    put the count of Rooms into RoomCount
    set the elements of Room to RoomCount

    set the elements of Target to RoomCount
    set the elements of TargetWas to RoomCount
    set the elements of PeriodWas to RoomCount
    set the elements of RelayStateWas to RoomCount

    set R to 0
    while R is less than RoomCount
    begin
        index Room to R
        put item R of Rooms into Room

        ! Housekeeping
        delete entry `advance-rq` of Room
        delete entry `boost` of Room
        delete entry `responses` of Room

        ! Initialisation
        index PeriodWas to R
        set PeriodWas to 0
        set RelayStateWas to empty
        index Target to R
        set Target to 0
        set TargetWas to 0
        increment R
    end

    clear Interrupt

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   This the head of each loop
MainLoop:
!    log `Main loop`
    set T to 100
    while T is greater than 0
    begin
        wait 10 ticks
        decrement T
        ! Short-circuit if a change needs handling
        if Interrupt
        begin
            clear Interrupt
            set T to 0
        end
    end

    ! Examine the state of each room
    put 0 into RoomIndex
    while RoomIndex is less than RoomCount
    begin
        index Room to RoomIndex
        index Target to RoomIndex
        index TargetWas to RoomIndex
        index PeriodWas to RoomIndex
        index RelayStateWas to RoomIndex

        put entry `name` of Room into RoomName
        gosub to ProcessRoom
        increment RoomIndex
    end

    ! Update the map
    if ImmediateUpdate
    begin
        set MapHasChanged
        clear ImmediateUpdate
        gosub to WriteMap
    end
    else
    begin
        add 60000 to LastMapUpdate giving T
        if T is less than now
        begin
            gosub to WriteMap
            set LastUpdateTime to now
        end
    end
    go to MainLoop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a single room
ProcessRoom:
    put Target into TargetWas
    put entry `temperature` of Room into TempNow
    put entry `relay` of Room into RelayState
    put entry `mode` of Room into Mode
    if Room has entry `period` put entry `period` of Room into PeriodNow
    else put 0 into PeriodNow
    put `` cat entry `advance` of Room into Advance

    if Mode is `timed`
    begin
        gosub to FindCurrentPeriod
    end
    else if Mode is `on`
    begin
    end
    else if Mode is `boost`
    begin
        put entry `target` of Room into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
    else
    begin
        set Mode to `off`
    end

    ! Build a pack of data and send it to the device controller or simulator
    ! It will open/close the relay and return the current temperature of the room
    reset Data
    set entry `room name` of Data to RoomName
    set entry `temperature` of Data to TempNow
    set entry `relay state` of Data to RelayState
!    if RoomName is `Kitchen` log Data
    send Data to Simulator
    on message go to PR2
    stop

!   Here when the device controller returns the current temperature
PR2:
    put TempNow into TempWas
    put the message into TempNow

    ! Decide if the relay should be on or off
    put RelayState into RelayStateWas
    if Mode is `off` set entry `relay` of Room to `off`
    else 
    begin
        if TempNow is less than Target
        begin
            if RelayState is `off`
            begin
                set RelayState to `on`
            end
        end
        else
        begin
            if RelayState is `on`
            begin
                set RelayState to `off`
            end
        end
    end

    ! Update the room record
    set entry `temperature` of Room to TempNow
    set entry `relay` of Room to RelayState
    set entry `period` of Room to PeriodNow
    ! If the period or the relay state have changed, signal an immediate update,
    ! otherwise just update the map and wait for the next update time
    if PeriodNow is not PeriodWas
    begin
!        log RoomName cat `: Period ` cat PeriodWas cat `->` cat PeriodNow
        set ImmediateUpdate
        set PeriodWas to PeriodNow
    end
    if RelayState is not RelayStateWas
    begin
!        log RoomName cat `: RelayState ` cat RelayStatedWas cat `->` cat RelayState
        set ImmediateUpdate
        set RelayStateWas to RelayState
    end
    if TempNow is not TempWas set MapHasChanged
    gosub to UpdateRooms
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Write the map file
WriteMap:
    if MapHasChanged log `Saving map`
!    if MapHasChanged
!    begin
!        put item 7 of Rooms into Room
!        log entry `name` of Room
!    end
    if MapHasChanged save prettify Map to `../map.json`
    clear MapHasChanged
    set LastMapUpdate to now
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Update parts of the map
UpdateRooms:
!    log `Update ` cat RoomName cat ` (` cat RoomIndex cat ` )`
    set item RoomIndex of Rooms to Room
UpdateProfile:
    set entry `rooms` of Profile to Rooms
UpdateProfiles:
    set item CurrentProfile of Profiles to Profile
UpdateMap:
    set entry `profiles` of Map to Profiles
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Find which period we are in (to get the target temperature)
FindCurrentPeriod:
    put entry `events` of Room into Events
    put the count of Events into EventCount
    if EventCount is 0 return
    put PeriodNow into PeriodWas
    set PeriodNow to 0
FCP2:
!    if RoomName is `Kitchen` log `Examine period ` cat PeriodNow
    if PeriodNow is EventCount
    begin
        ! We are past the last event time, so use the first
        put 0 into PeriodNow
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
    end
    else
    begin
        ! Check the current period. If we have passed it, move on.
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
        ! If we have passed this period, get the start of the next period and move on
!        if RoomName is `Kitchen` log `now=` cat now cat `, Time=` cat Time
        if now is greater than Time
        begin
            increment PeriodNow
            go to FCP2
        end
        ! Otherwise, we've found the period
    end
    put PeriodNow into PeriodActive

    ! Now get the target temperature
    put entry `temp` of Period into Temp
    gosub to ConvertTempToInt
    put Temp into Target

    ! Deal with Advance
    put PeriodNow into P
    if Advance is not `-`
    begin
        if Advance is `A`
        begin
            add 1 to PeriodNow giving P
            if P is EventCount put 0 into P
            put `` cat P into Advance
            set entry `advance` of Room to Advance
            set PeriodActive to P
            gosub to SendAdvanceMessage
        end
        if Advance is not `-`
        begin
            put the value of Advance into P
            if P is PeriodNow
            begin
                log `Cancel the advance`
                set entry `advance` of Room to `-`
                gosub to SendAdvanceMessage
            end
        end
        put P into PeriodActive
        put item P of Events into Period
        put entry `temp` of Period into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
!    if RoomName is `Kitchen` log `Period ` cat PeriodNow cat `/` cat PeriodActive
!        cat ` until ` cat entry `until` of Period cat `, Target=` cat Target
!        cat `, Temp=` cat TempNow cat `, Relay=` cat RelayState
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Send an advance message back to the main
SendAdvanceMessage:
    put `Advance` into Subject
    go to SendMessage

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert an HH:MM time into an int seconds value
ConvertTimeToInt:
    put `` cat Time into Time
    put the index of `:` in Time into I
    put the value of left I of Time into T
    multiply T by 60
    increment I
    put the value of from I of Time into Time
    add Time to T
    multiply T by 60000 giving Time
    add today to Time
    return

! Convert a temperature string into an int hundredths value
ConvertTempToInt:
    if Temp is empty put 0 into Temp
    put the index of `.` in Temp into I
    if I is less than 0 multiply Temp by 100
    else
    begin
        put the value of left I of Temp into T
        multiply T by 100
        increment I
        put the value of from I of Temp into Temp
        add T to Temp
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   This section handles messages from and to the UI
HandleMQTTMessage:
    put the mqtt message into ReceivedMessage
!    log `Incoming: ` cat ReceivedMessage
    put entry `sender` of ReceivedMessage into Sender
    put entry `action` of ReceivedMessage into Action
    put entry `message` of ReceivedMessage into Data
    if Action is `first`
    begin
        set MapHasChanged
        set StateChanged
        put `map` into Action
    end
    if Action is `map` go to SendMap
    else if Action is `uirequest` go to ProcessUIRequest
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
GetRoomByName:
    put 0 into RoomIndex
    while RoomIndex is less than RoomCount
    begin
        put item RoomIndex of Rooms into Room
        if entry `name` of Room is RoomName return
        increment RoomIndex
    end
    ! Not found
    reset Room
    return

!ListRooms:
!    put entry `profiles` of Map into Profiles
!    put entry `profile` of Map into CurrentProfile
!    put item CurrentProfile of Profiles into Profile
!    put entry `rooms` of Profile into Rooms
!    set R to 0
!    while R is less than RoomCount
!    begin
!        put item R of Rooms into Room
!        log entry `name` of Room
!        increment R
!    end
!    put item 7 of Rooms into Room
!    log entry `name` of Room
!    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a request from the UI arriving by MQTT
ProcessUIRequest:
    log Data
    put entry `action` of Data into Action
    if Action is `System Name`
    begin
        set entry `name` of Map to entry `System Name` of Data
        log `Set the system name to ` cat entry `name` of Map
    end
    else if Action is `Request Relay`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        log `Set the request relay to ` cat entry `request` of Map
    end
    else if Action is `Add Room`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        put entry `Add Room` of Data into Room
        log `Add room ` cat entry `name` of Room
        append Room to Rooms
        set entry `rooms` of Profile to Rooms
        gosub to UpdateProfiles
    end
    else if Action is `Select Profile`
    begin
        set entry `profile` of Map to entry `Select Profile` of Data
        log `Set profile ` cat entry `profile` of Map
    end
    else if Action is `Operating Mode`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty stop
        put entry `Operating Mode` of Data into Mode
        log `Set Operating Mode of ` cat entry `name` of Room cat ` to ` cat Mode
        set entry `mode` of Room to Mode
        if Mode is `timed`
        begin
            if Data has entry `Advance`
            begin
                if `` cat entry `advance` of Room is `-`
                    set entry `advance` of Room to `A`
                else set entry `advance` of Room to `-`
                gosub to UpdateRooms
                set MapHasChanged
                gosub to WriteMap
            end
        end
        else if Mode is `boost`
        begin
            set T to entry `duration` of Data
            multiply T by 60000
            add now to T
            set entry `until` of Room to T
            set entry `target` of Room to entry `target` of Data
        end
        gosub to UpdateRooms
        set MapHasChanged
        gosub to WriteMap
    end
    else if Action is `Periods`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty stop
        log `Set the periods of ` cat entry `name` of Room
        set entry `events` of Room to entry `Periods` of Data
!        log RoomIndex cat ` - ` cat Room
        gosub to UpdateRooms
!        gosub to ListRooms
        set MapHasChanged
        gosub to WriteMap
!        gosub to ListRooms
!        log Map
    end
PUR2:
    set MapHasChanged

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   If the map has changed, send it to the UI
!   Otherwise, send an empty dictionary
SendMap:
    if Map is empty
    begin
        gosub to LoadMap
        set MapHasChanged
    end
    if MapHasChanged set MessageText to Map
    else
    begin
        reset Data
        set MessageText to Data
    end
    go to SendReply

    begin
        gosub to InitVariables
        put Map into MessageText
        clear MapHasChanged
        clear StateChanged
        go to SendReply
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Send a reply message
SendReply:
!    log Sender
    put entry `name` of Sender into SenderName
    put entry `qos` of Sender into SenderQoS
    init SenderTopic
        name SenderName
        qos SenderQoS
    if ConfirmationRequested
    begin
        set entry SenderName of WaitForConfirmation
        set Action to `confirm`
    end
    else set Action to `reply`
!    log left 20 of MessageText
    send to SenderTopic
        action Action
        message MessageText
!    log `Reply sent`
    clear ConfirmationRequested
    stop