!   events.ecs

    script Events

    use graphics
    use plugin RBR_UI from /home/graham/dev/rbr/rbr_ui.py

    import rbrwin Window
        and variable RoomSpec
        and variable Request
    
    widget Container
    layout Layout
    layout Panel
    label Label
    pushbutton PeriodButton
    pushbutton SaveButton
    pushbutton CancelButton
    variable Style
    variable Periods
    variable Period
    variable NPeriods
    variable Time
    variable Temp
    variable Text
    variable P
    variable Q

!    debug step

    put property `events` of RoomSpec into Periods
!    log Periods
    
    ! Convert 'until' to 'from' for all periods
    put the count of Periods into NPeriods
	if NPeriods is not 1
    begin
    	set the elements of Temp to NPeriods
        put 0 into P
        while P is less than NPeriods
        begin
        	add 1 to P giving Q
            if Q is NPeriods put 0 into Q
            put element Q of Periods into Period
            index Temp to P
            put property `temp` of Period into Temp
            put element P of Periods into Period
            add 1 to P
        end
        put 0 into  P
        while P is less than NPeriods
        begin
        	put element P of Periods into Period
        	index Temp to P
            set property `temp` of Period to Temp
            set element P of Periods to Period
        	add 1 to P
        end
    end
!    log Periods
    
    ! Draw the panel
    set the elements of PeriodButton to NPeriods
    create Container
    set Container as other view of Window
    set the style of Container to `background: #ffc;border: 1px solid gray;`
    create Layout type QVBoxLayout
    set the layout of Container to Layout
    create Panel type QHBoxLayout
    add Panel to Layout
    add stretch to Panel
    create Label text `Schedule for ` cat property `name` of RoomSpec
    set the style of Label to `border: none; margin-top: 1px; font-weight: bold; font-size: 20px; color: blue`
    add Label to Panel
    add stretch to Panel
    put `background: #ffc;border: 1px solid gray; background: white; height: 30px; `
    cat `border-radius: 5px;` into Style
    put 0 into P
    while P is less than NPeriods
    begin
        put element P of Periods into Period
        index PeriodButton to P
        put property `until` of Period into Time
        put property `temp` of Period into Temp
        put `From ` cat Time cat `: Target = ` cat Temp cat `ÂºC` into Text
        create PeriodButton text Text
        set the style of PeriodButton to Style
        on click PeriodButton go to OnClickPeriodInfo
        add PeriodButton to Layout
        increment P
    end
    add stretch to Layout
    create Panel type QHBoxLayout
    add Panel to Layout
    create SaveButton text `Save`
    set the style of SaveButton to Style
    add SaveButton to Panel
    create CancelButton text `Cancel`
    set the style of CancelButton to Style
    add CancelButton to Panel
    add spacer 10 to Layout
    show view `other` of Window
    on click SaveButton go to ResumeAfterEdit
    on click CancelButton go to ResumeAfterEdit
    stop

ResumeAfterEdit:
    show view `main` of Window
    exit

OnClickPeriodInfo:
    log the index of PeriodButton
    stop
