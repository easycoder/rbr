!   controller.ecs

    script Controller

    use graphics

    import variable HomeDir and variable ControllerSpec

    window HostWindow
    layout Panel
    layout Layout
    label Label
    lineinput HostInput
    lineinput UserInput
    lineinput PasswordInput
    pushbutton OKButton
    pushbutton CancelButton
    variable Path
    variable Item

    put HomeDir cat `/.controller.json` into Path
    if file Path exists load ControllerSpec from Path
    else
    begin
        put json `{}` into ControllerSpec
        create Panel type QVBoxLayout
        create Layout type QHBoxLayout
        add Layout to Panel
        create Label text `Host:`
        add Label to Layout
        create HostInput size 30
        set the width of HostInput to 200
        add HostInput to Layout
        create Layout type QHBoxLayout
        add Layout to Panel
        create Label text `User:`
        add Label to Layout
        create UserInput size 30
        set the width of UserInput to 200
        add UserInput to Layout
        create Layout type QHBoxLayout
        add Layout to Panel
        create Label text `Password:`
        add Label to Layout
        create PasswordInput size 30
        set the width of PasswordInput to 200
        add PasswordInput to Layout
        create Layout type QHBoxLayout
        add Layout to Panel
        create OKButton text `OK`
        on click OKButton
        begin
            if HostInput is empty stop
            if UserInput is empty stop
            if PasswordInput is empty stop
            put json `{}` into Item
            set property `user` of Item to UserInput
            set property `password` of Item to PasswordInput
            set property `host` of ControllerSpec to HostInput
            set property HostInput of ControllerSpec to Item
            set property `current` of ControllerSpec to HostInput
            save ControllerSpec to Path
            wait 20 ticks
            close HostWindow
            exit
        end
        add OKButton to Layout
        create CancelButton text `Cancel`
        on click CancelButton exit
        add CancelButton to Layout
        create HostWindow title `Host SSH Parameters` size 300 200
        set the layout of HostWindow to Panel
        show HostWindow
        log ControllerSpec
        stop
    end
    exit