!    RBR Controller script for Python/EasyCoder

!   This script manages the local hardware - sensors and relays - to act
!   in accordance with the rules in the system map.

    script RBR

    use psutil
    use plugin P100 from plugins/ec_p100.py

    file Logger
    dictionary Config
    dictionary RegData
    dictionary SystemData
    dictionary Map
    dictionary Profile
    dictionary RoomSpec
    dictionary RoomStatus
    dictionary Devices
    dictionary Device
    dictionary MasterDevice
    dictionary AdvanceData
    dictionary Mijia
    dictionary Request
    dictionary Day
    dictionary Logs
    dictionary LogData
    dictionary Status
    dictionary Event
    dictionary Items
    dictionary Item
    dictionary Dict
    dictionary Dict2
    list Profiles
    list RoomNames
    list RoomSSIDs
    list Rooms
    list Relays
    list Keys
    list Calendar
    list Events
    list Responses
    variable RamDisk
    variable NetworkName
    variable RBRServer
    variable Version
    variable RepeatCount
    variable CurrentProfile
    variable ProfileName
    variable SystemName
    variable RoomNumber
    variable RoomName
    variable RoomCount
    variable FileName
    variable Mode
    variable IPAddr
    variable MasterIPAddr
    variable MasterMAC
    variable DeviceMAC
    variable URL
    variable Path
    variable Sensor
    variable Relay
    variable RelayType
    variable Locked
    variable BadResponses
    variable Timestamp
    variable Temperature
    variable Battery
    variable NPeriods
    variable Until
    variable Target
    variable SensorValues
    variable SensorRSS
    variable LastReported
    variable Now
    variable Boundary
    variable OnOff
    variable MAC
    variable Password
    variable Advance
    variable Boost
    variable Response
    variable NetworkErrors
    variable RequestCount
    variable SSID
    variable TargetTemp
    variable Action
    variable Key
    variable Value
    variable Value2
    variable Values
    variable Changed
    variable Confirm
    variable LogText
    variable WarningMessages
    variable WarningMessage
    variable MailMessage
    variable Message
    variable Mail
    variable HaltRequested
    variable TPEmail
    variable TPPassword
    variable Name
    variable RA
    variable E
    variable EE
    variable L
    variable N
    variable P
    variable R
    variable T
    variable W
    variable P1
    variable T1
    variable T2
    variable TS

!    debug compile
!    debug step

    put `wlan0` into NetworkName ! Set this to your network interface name
    put `/mnt/data/` into RamDisk

    if file RamDisk cat `lock` exists
    begin
        load Locked from RamDisk cat `lock`
        if Locked is not empty
        begin
            log `Locked`
            exit
        end
    end

    load Version from `version`
    save Version to RamDisk cat `version`

    save system `uptime | awk -F'( |,|:)+' '{
    `d=h=m=0;
    `if ($7=="min")
    `    m=$6;
    `else {
    `    if ($7~/^day/) { d=$6; h=$8; m=$9}
    `    else {h=$6;m=$7}
    `    }
    `}
    `{
    `    print d+0,"days,",h+0,"hours,",m+0,"minutes."
    `}'` to RamDisk cat `uptime`

    put system `hostname -I` into IPAddr
    put the position of ` ` in IPAddr into P
    if P is greater than 0  put left P of IPAddr into IPAddr
    log `My IP address is ` cat IPAddr

    load RBRServer from `server.txt`
    put trim RBRServer into RBRServer
    put RBRServer cat `/resources/php/rest.php` into RBRServer

    put the timestamp into Timestamp
    multiply Timestamp by 1000 giving T
    log datime T format `%b %d %H:%M:%S`

    if file RamDisk cat `network-errors` exists
    begin
        load NetworkErrors from RamDisk cat `network-errors`
        if NetworkErrors is empty put 0 into NetworkErrors
        put the value of NetworkErrors into NetworkErrors
        log NetworkErrors cat ` network errors`
    end
    else
    begin
        put -1 into NetworkErrors
        gosub to NetworkError
    end

    if file RamDisk cat `mijia.json` exists load Mijia from RamDisk cat `mijia.json`
    else
    begin
!        reset Mijia
        save Mijia to RamDisk cat `mijia.json`
    end

    ! Get the system MAC address
    put system `nmcli -g GENERAL.HWADDR device show ` cat NetworkName
        cat ` | tr -d '\\' | tr '[:upper:]' '[:lower:]'` into MAC
    log `MAC address is ` cat MAC
    if MAC is empty
    begin
        log `Unable to get my MAC address`
        exit
    end
    put trim MAC into MAC
    save MAC to RamDisk cat `mac`

    put 0 into RepeatCount

    clear Changed
    clear Confirm
    clear HaltRequested

    reset RegData

    load Map from `map.json`
    save prettify Map to RamDisk cat `map.json`

    load Config from `config.json`
    log `Config: ` cat prettify Config
    save prettify Config to RamDisk cat `config.json`
    put empty into MasterDevice
    put empty into MasterIPAddr
    if Config has entry `devices`
    begin
        put entry `devices` of Config into Devices
        put the keys of Devices into Keys
        put 0 into N
        while N is less than the count of Keys
        begin
            put item N of Keys into Key
            put entry Key of Devices into Device
            if entry `master` of Device is true
            begin
                put Device into MasterDevice
                put entry `ssid` of Device into SSID
                put from 8 of SSID into MasterMAC
            end
            increment N
        end
    end
    if MasterDevice is not empty
    begin
        put entry `ipaddr` of MasterDevice into MasterIPAddr
        put MasterDevice into Device
        put `channel` into Message
        gosub to MessageESPDevice
        if Response is not empty
        begin
            if left 3 of Response is `OK `
            begin
                put from 3 of Response into Value
                log `Channel ` cat Value
            end
        end
    end
    else print `No master device`


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Main start
Start:
    add 9 to now giving Timestamp

S1:
    if Map is empty
    begin
        put `{"profiles":[{"name":"Unnamed","rooms":[{"name":"Unnamed","sensor":"","relays":[""],"mode":"off","target":"0.0","events":[]}]}],"profile":0,"name":"New system"}` into Map
        save Map to `map.json`
    end
    put entry `profiles` of Map into Profiles
    put entry `profile` of Map into CurrentProfile
    if `` cat CurrentProfile is empty put 0 into CurrentProfile

    put entry `name` of Map into SystemName
    replace `%20` with ` ` in SystemName
    log `System Name: ` cat SystemName
    save SystemName to RamDisk cat `name`

    ! If the Calendar is on, find which profile is active
    if Map has entry `calendar`
    begin
        if entry `calendar` of Map is `on`
        begin
            put entry `calendar-data` of Map into Calendar
            if Calendar is empty put entry `profile` of Map into CurrentProfile
            else
            begin
                ! Examine the data for today
                put the weekday into W
                put item W of Calendar into Day
                if stringify Day is not `{}` put entry `day` cat W cat `-profile` of Day into ProfileName
                put 0 into P
                while P is less than the count of Profiles
                begin
                    put item P of Profiles into Profile
                    if entry `name` of Profile is ProfileName
                    begin
                        put P into CurrentProfile
                        go to S2
                    end
                    add 1 to P
                end
            end
        end
    end

S2:
    save `` cat CurrentProfile to RamDisk cat `profile`
    put item CurrentProfile of Profiles into Profile
    log `Profile: ` cat entry `name` of Profile

    put entry `rooms` of Profile into Rooms

    gosub to ProcessRequest
    delete file RamDisk cat `request`

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process the map

!   Process all the rooms
    reset SystemData

    set entry `actual` of SystemData to CurrentProfile
    put 0 into BadResponses
    put empty into WarningMessages
    put the count of Rooms into RoomCount
    if RoomCount is 0
    begin
        log `There are no rooms in this profile`
    end
    else
    begin
        ! Set up a map of relay counts against sensor IDs
        put 0 into N
        while N is less than RoomCount
        begin
            put item N of Rooms into RoomSpec
            if RoomSpec has entry `sensor` put entry `sensor` of RoomSpec into Sensor
            else reset Sensor
            if RoomSpec has entry `relayType`
            begin
                put entry `relayType` of RoomSpec into RelayType
                if RelayType is `Manual`
                begin
                    put `Manual` into Sensor
                    set entry `sensor` of RoomSpec to Sensor
                    set item N of Rooms to RoomSpec
                end
            end
            add 1 to N
        end

        ! Process all the rooms
        log RoomCount cat ` rooms`
        put 0 into RequestCount
        put 0 into RoomNumber
        while RoomNumber is less than RoomCount
        begin
            put item RoomNumber of Rooms into RoomSpec
            gosub to ProcessRoom
            set item RoomNumber of Rooms to RoomSpec
            set entry RoomName of SystemData to RoomSpec
            add 1 to RoomNumber
        end
        log `-------------------- All rooms processed`

        ! Deal with the request relay
        if Map has entry `request`
        begin
            put entry `request` of Map into Name
            if Name is not empty
            begin
                if RequestCount is 0 put `off` into OnOff
                else put `on` into OnOff
                log RequestCount cat ` requests: Turn ` cat Name cat ` ` cat OnOff
                if Devices has entry Name
                begin
                    put Name into RoomName
                    put entry Name of Devices into Device
                    put OnOff into Message
                    gosub to MessageESPDevice
                    if Response is empty log Name cat `: No response`
                    else gosub to RecordMijia
                end
                else log `No request relay called ` cat Name
            end
        end
    end

    set entry `version` of SystemData to Version

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Repeat everything another 5 times
Repeat:
    if HaltRequested
    begin
        system `touch /mnt/data/halt`
        exit
    end

    log `Memory used: ` cat memory cat ` MB`

    gosub to PostSensorData
    save Mijia to RamDisk cat `mijia.json`

    add 1 to RepeatCount
    log `Repeat ` cat RepeatCount cat ` done`
    if RepeatCount is less than 6
    begin
        while now is less than Timestamp wait 50 ticks
        go to Start
    end

    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a single room
ProcessRoom:
    if RoomSpec has entry `name` put entry `name` of RoomSpec into RoomName
    else put `unknown` into RoomName
    log `-------------------- ` cat RoomName

    if RoomSpec has entry `sensor` put entry `sensor` of RoomSpec into Sensor
    else put empty into Sensor
    put empty into RelayType
    if RoomSpec has entry `relayType` put entry `relayType` of RoomSpec into RelayType

    ! Get the room status
    put RamDisk cat `status.json` into FileName
    if file FileName exists load Status from FileName else reset Status
    if Status has entry RoomName put entry RoomName of Status into RoomStatus
    else
    begin
        reset RoomStatus
        set entry `status` of RoomStatus to `good`
        set entry `errors` of RoomStatus to 0
        set entry RoomName of Status to RoomStatus
        save Status to FileName
    end
    set entry `status` of RoomSpec to entry `status` of RoomStatus
    set entry `timestamp` of RoomSpec to the timestamp

    put empty into SensorRSS
    reset Response
    if RelayType is not `Manual`
    begin
        if Sensor is empty return
        if left 2 of Sensor is `T-`
        begin
            if Devices does not have entry Sensor
            begin
                log `Device '` cat Sensor cat `' is not in the map`
                return
            end
            ! Ignore repeats if this device is faulty
            if RepeatCount is not 0
            begin
                if entry `errors` of RoomStatus is not 0
                begin
                    if RoomStatus has entry `temperature`
                        set entry `temperature` of RoomSpec to entry `temperature` of RoomStatus
                    else set entry `temperature` of RoomSpec to 0
                    return
                end
            end

            put entry Sensor of Devices into Device
            put `temp` into Message
            gosub to MessageESPDevice
!            log `Sensor: ` cat Response
            if left 3 of Response is `OK `
            begin
                split Response on ` `
                index Response to 1
                set entry `temperature` of RoomSpec to Response
                set entry `battery` of RoomSpec to `100`
                if entry `errors` of RoomStatus is not 0 set entry `temperature` of RoomStatus to Response
                index Response to 2
                put `` cat Response into SensorRSS
                gosub to ResetErrorCount
            end
            else
            begin
                if RoomStatus has entry `temperature`
                begin
                    set entry `temperature` of RoomSpec to entry `temperature` of RoomStatus
                end
                else
                begin
                    set entry `temperature` of RoomSpec to `0`
                end
                gosub to BumpErrorCount
            end
        end
        else
        begin
            if Mijia has entry Sensor
            begin
                put entry Sensor of Mijia into Dict
                put entry `ts` of Dict into LastReported
                take LastReported from the timestamp giving LastReported
                if LastReported is greater than 1800
                begin
                    set entry `temperature` of RoomSpec to empty
                    put `Sensor for ` cat RoomName cat ` has not reported recently` into WarningMessage
                    gosub to AddWarning
                end
                else
                begin
                    set entry `temperature` of RoomSpec to entry `temp` of Dict
                    set entry `battery` of RoomSpec to entry `batt` of Dict
                end
            end
            else
            begin
                put `Sensor for ` cat RoomName cat ` has not reported` into WarningMessage
                set entry `temperature` of RoomSpec to empty
                gosub to AddWarning
                log WarningMessage
            end
        end
    end
!    log RoomSpec
    if RoomSpec has entry `target` put entry `target` of RoomSpec into Target
    else put `0` into Target
    if RoomSpec has entry `temperature` put `` cat entry `temperature` of RoomSpec into Temperature
    else put empty into Temperature
    if Temperature is empty set entry `temperature` of RoomSpec to empty
    else
    begin
        put the value of Temperature into T
        put T modulo 10 into T1
        divide T by 10
        set entry `temperature` of RoomSpec to T cat `.` cat T1
    end
    if RoomSpec has entry `battery` put `` cat entry `battery` of RoomSpec into Battery
    else set entry `battery` of RoomSpec to `0`

    ! Get the advance data for this room
GetAdvance:
    put `-` into Advance
    if file RamDisk cat `advance.json` exists
    begin
        load AdvanceData from RamDisk cat `advance.json`
        if AdvanceData has entry RoomName put entry RoomName of AdvanceData into Advance
    end
    else reset AdvanceData
    set entry `advance` of RoomSpec to Advance

    if Battery is empty put `0` into Battery

    ! See if this room is set for winter protection
    if entry `protect` of RoomSpec is empty set entry `protect` of RoomSpec to `no`
    if entry `protect` of RoomSpec is `yes`
    begin
        set entry `protect` of SystemData to empty
        if entry `temperature` of RoomSpec is not empty
        begin
            put entry `ptemp` of RoomSpec into Target
            gosub to MultiplyTT
            if Temperature is not greater than Target
            begin
                log `Frost protect ON`
                add 1 to RequestCount
                set entry `protect` of SystemData to Sensor
            end
        end
    end

    ! Deal with mode requests
    put entry `mode` of RoomSpec into Mode
    if Mode is empty put `off` into Mode

    if Mode is `boost`
    begin
        put `` cat entry `boost` of RoomSpec into Boost
        if left 1 of Boost is `B`       ! Set up a boost
        begin
            put from 1 of Boost into Target
            multiply Target by 60
            add the timestamp to Target
            set entry `boost` of RoomSpec to Target
            set entry `message` of Map to `confirm`
            gosub to UpdateRoom
            put `-` into Advance
            set entry `advance` of RoomSpec to Advance
            gosub to WriteAdvance
            put entry `target` of RoomSpec into TargetTemp
            set entry `target` of RoomSpec to TargetTemp
        end
        else if Boost is empty      ! Cancel the boost
        begin
            set entry `boost` of RoomSpec to 0
            set entry `mode` of RoomSpec to entry `prevmode` of RoomSpec
            gosub to UpdateRoom
            put 0 into TargetTemp
        end
        else if Boost is not 0
        begin
            ! Boost contains the timestamp of the target (the end of the boost period).
            put integer Boost into Boost
            take the timestamp from Boost
            if Boost is less than 0
            begin
                ! Deal with the end of the boost period
                log `Boost finished`
                set entry `boost` of RoomSpec to 0
                set entry `mode` of RoomSpec to entry `prevmode` of RoomSpec
                gosub to UpdateRoom
                put 0 into TargetTemp
            end
        end
        if RoomSpec has entry `linked`
        begin
            if entry `linked` of RoomSpec is not `yes`
            begin
                gosub to MultiplyTT ! not needed: just to make the log consistent
                put 0 into TargetTemp
                go to TurnOn
            end
        end
        ! Use the target setting for 'on' mode
        if RoomSpec has entry `target` put entry `target` of RoomSpec into Target
        else reset Target
        if `` cat Target is not empty
        begin
            gosub to MultiplyTT
            if Temperature is less than Target
            begin
                put Target into TargetTemp
                go to TurnOn
            end
        end
        put 0 into TargetTemp
        go to TurnOff
    end

    if Mode is `off`
    begin
        gosub to MultiplyTT
        put 0 into TargetTemp
        set entry `target` of RoomSpec to `0.0`
        go to TurnOff
    end

    if Mode is `on`
    begin
        if entry `linked` of RoomSpec is not `yes` go to TurnOn
        else
            gosub to MultiplyTT
!            log RoomName cat `: ` cat Temperature cat ` ` cat Target
            set entry `target` of RoomSpec to Target
            if Temperature is empty go to TurnOff
            if Temperature is less than Target go to TurnOn
            go to TurnOff
        end
    end

    ! Default: timed mode. Check each of the events
    put entry `events` of RoomSpec into Events
    if Events is empty
    begin
        gosub to MultiplyTT
        put Target into TargetTemp
        if RoomName is `Kitchen` log Temperature cat `-` cat Target
        if Temperature is empty go to TurnOff
        if Temperature is less than Target go to TurnOn else go to TurnOff
    end
    take 1 from the count of Events giving NPeriods
    put `timed` into Mode
    put 0 into E
    while E is less than the count of Events
    begin
        put item E of Events into Event
        put entry `until` of Event into Until
        put the timestamp of Until format `%H:%M` into Boundary
        put now into Now
        ! Check if this is the right time segment
        if Now is less than Boundary
        begin
            ! We've found the current time segment
            go to CheckAdvance
        end
        else
        begin
            if E is NPeriods
            begin
                ! We've gone past the last time segment so use the first
                put 0 into E
                go to CheckAdvance
            end
        end
        add 1 to E
    end
    return

!   Multiply Target by 10 so it can be compared
MultiplyTT:
    if `` cat Target is empty put `16` into Target
    else put `` cat Target into Target
    put the position of `.` in Target into P1
    if P1 is less than 0
    begin
        multiply Target by 10
    end
    else
    begin
        put left P1 of Target into T1
        multiply T1 by 10
        add 1 to P1
        put from P1 of Target into T2
        add T2 to T1 giving Target
    end
!    log `Target: ` cat Target cat `, Temperature: ` cat Temperature
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   This is called in 'timed' mode to deal with the Advance setting
CheckAdvance:
!    log `CheckAdvance`
!    if RoomName is `Sunroom` log Now cat `/` cat the timestamp cat `/` cat Boundary
    put `` cat E into EE
    set entry `target` of RoomSpec to entry `temp` of Event
    put entry `advance` of RoomSpec into Advance
    if RoomSpec has entry `advance-rq` put entry `advance-rq` of RoomSpec into RA
    else put `-` into RA
!    log `Current Advance is ` cat Advance cat `, new is ` cat RA
    if RA is `C` go to CancelAdvance
    if `` cat Advance is `-`
    begin
        if RA is `A`
        begin
            set entry `boost` of RoomSpec to 0
            put EE into Advance
            set entry `advance-rq` of RoomSpec to `-`
            go to WriteAdvance
        end
    end
    else if Advance is not EE go to CancelAdvance
    go to TurnOnOrOff

CancelAdvance:
    put `-` into Advance

WriteAdvance:
    if Advance is empty put `-` into Advance
    set entry RoomName of AdvanceData to Advance
    save AdvanceData to RamDisk cat `advance.json`
    set entry `advance` of RoomSpec to Advance
    set entry `advance-rq` of RoomSpec to `-`
    set entry RoomName of SystemData to RoomSpec

!   Update various parts of the map
UpdateRoom:
    set item RoomNumber of Rooms to RoomSpec
UpdateRooms:
    set entry `rooms` of Profile to Rooms
UpdateProfiles:
    set item CurrentProfile of Profiles to Profile
    set entry `profiles` of Map to Profiles
UpdateMap:
    put stringify Map into Map
    set Changed
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Turn a relay on or off
TurnOnOrOff:
    if Battery is `0` go to TurnOff
    if `` cat Advance is not `-`
    begin
        add 1 to E
        if E is greater than NPeriods put 0 into E
        log `Advance to period ` cat E
    end
    put item E of Events into Event
    put entry `temp` of Event into Target
    gosub to MultiplyTT
    put Target into TargetTemp
    if `` cat Temperature is empty goto TurnOff
    if Temperature is less than TargetTemp go to TurnOn

TurnOff:
    put `off` into OnOff
    go to UpdateLog

TurnOn:
    add 1 to RequestCount
    put `on` into OnOff

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update the log if the temperature has changed outside a 10-minute window
UpdateLog:
    if Temperature is empty put `off` into OnOff
    set entry `relay` of RoomSpec to OnOff
    put RamDisk cat `logs.json` into FileName
    if file FileName exists load Logs from FileName else reset Logs
    if Logs has entry RoomName
    begin
        put entry RoomName of Logs into LogData
        put entry `ts` of LogData into TS
        put entry `temp` of LogData into T
    end
    else
    begin
        reset LogData
        put 0 into TS
        put 0 into T
    end

!   Skip if less than 10 minutes from the last change
    add 600 to TS
    if now is less than TS go to SetRelays

!   Test if the temperature has changed
    if `` cat T is `` cat Temperature go to SetRelays

!   There's been a change, so write a log and update the data file
    log `Temperature change in ` cat RoomName
!    if Password is not empty post stringify RoomSpec to RBRServer cat `/sensorlog/` cat MAC cat `/` cat Password
    set entry `temp` of LogData to Temperature
    set entry `ts` of LogData to now
    set entry RoomName of Logs to LogData
    save Logs to FileName

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set the relay(s) in a single room
SetRelays:
    put entry `temperature` of RoomSpec into Temperature
    put entry `battery` of RoomSpec into Battery
    put entry `relays` of RoomSpec into Relays
    put entry `relays` of RoomSpec into Relays
    if Relays is empty reset Relays
    if the count of Relays is 0 log `Sensor: ` cat Sensor cat ` ` cat Temperature cat ` ` cat SensorRSS
    else log `Sensor: ` cat Sensor cat ` T:A=` cat Target cat `:` cat Temperature cat ` ` cat Mode cat `:`
        cat OnOff cat ` ` cat SensorRSS
    if RelayType is empty put `RBR-Now` into RelayType
! log RoomName cat ` Relays: ` cat Relays
    reset Responses
    put 0 into R
    while R is less than the count of Relays
    begin
        put item R of Relays into Relay
        reset Response
        if RelayType is `RBR-Now`
        begin
            if Devices does not have entry RoomName
            begin
                log `No relay info available for ` cat RoomName
                go to SR2
            end
            put entry RoomName of Devices into Device
            put OnOff into Message

            gosub to MessageESPDevice
            if Response is not empty
            begin
                gosub to RecordMijia
                put Response into Value
                reset Dict
                set entry `response` of Dict to Value
                split Value on ` `
                if the elements of Value is not less than 3
                begin
                    index Value to 1
                    if Value is `ON` set entry `state` of Dict to `on`
                    else set entry `state` of Dict to `off`
                    index Value to 2
                    put the position of `+` in Value into P
                    set entry `uptime` of Dict to left P of Value
                    if the elements of Value is greater than 3
                    begin
                        index Value to 3
                        set entry `RSS` of Dict to Value
                    end
                    else set entry `RSS` of Dict to empty
                end
            end
            set the elements of Value to 1
            put Dict into Response
        end
        else if RelayType is `Shelly One`
        begin
            put `http://` cat Relay cat `/relay/0?turn=` cat OnOff into URL
            get Response from url URL
            or begin
                reset Response
                gosub to NetworkError
                go to SR1
            end
            put Response into Dict2
            reset Dict
            set entry `uptime` of Dict to 0
            if Dict2 has entry `ison`
            begin
                if entry `ison` of Dict2 is true
                    set entry `state` of Dict to `on`
                else set entry `state` of Dict to `off`
                gosub to ClearNetworkErrors
            end
            else gosub to BumpErrorCount
            put Dict into Response
        end
        else if RelayType is `Tasmota`
        begin
            put `http://` cat Relay cat `/cm?cmnd=power%20` cat OnOff into URL
            get Response from url URL
            or begin
                reset Response
                gosub to NetworkError
                go to SR1
            end
            reset Dict
            set entry `uptime` of Dict to 0
            set entry `state` of Dict to `?`
            put Dict into Response
            gosub to ClearNetworkErrors
        end
        else if RelayType is `TP-Link P100`
        begin
            put entry `p100Email` of RoomSpec into TPEmail
            put entry `p100Password` of RoomSpec into TPPassword
            relay Relay TPEmail TPPassword OnOff
        end
    SR1:
        if Response is empty log RoomName cat `: No response`
        else log `Relay: ` cat Response
        append Response to Responses
    SR2:
        add 1 to R
    end
    set entry `relay` of RoomSpec to OnOff
    set entry `responses` of RoomSpec to Responses
    if RelayType is not `Manual`
    begin
        set entry `relay` of RoomSpec to OnOff
        set entry `advance` of RoomSpec to Advance
    end
    set entry RoomName of SystemData to RoomSpec
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Deal with the error count for a room
ResetErrorCount:
    put entry `errors` of RoomStatus into E
    if E is 0 return
    set entry `errors` of RoomStatus to 0
    set entry `status` of RoomStatus to `good`
    go to SaveErrorCount

BumpErrorCount:
    put entry `errors` of RoomStatus into E
    add 1 to E
    set entry `errors` of RoomStatus to E
    if E is greater than 20 set entry `status` of RoomStatus to `fail`
    else set entry `status` of RoomStatus to `suspect`
    log E cat ` errors`

SaveErrorCount:
    set entry RoomName of Status to RoomStatus
    save Status to RamDisk cat `status.json`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Post data to the RBR server. This also posts the IP address and the map
PostSensorData:
    log `PostSensorData`
    if Changed save prettify Map to `map.json`
    save prettify Map to RamDisk cat `map.json`
    save prettify SystemData to RamDisk cat `systemdata.json`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Record information about Mijia thermometers
RecordMijia:
    put Response into Value
    split Value on ` `
    index Value to 2
    put Value into Value2
    split Value2 on `+`
    if the elements of Value2 is 1 return
    index Value2 to 1
    if Value2 is not empty
    begin
        log `Mijia: ` cat  RoomName cat ` ` cat Value2
        split Value2 on `;`
        if the elements of Value2 is not less than 5
        begin
            reset Item
            set entry `ts` of Item to now
            index Value2 to 1
            set entry `rssi` of Item to Value2
            index Value2 to 2
            set entry `temp` of Item to Value2
            index Value2 to 3
            set entry `hum` of Item to Value2
            index Value2 to 4
            set entry `batt` of Item to Value2
            index Value2 to 0
            set entry `a4:c1:38:` cat Value2 of Mijia to Item
        end
        set the elements of Value to 1
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Select a room by name
GetRoomByName:
    put 0 into RoomNumber
    while RoomNumber is less than the count of Rooms
    begin
        put item RoomNumber of Rooms into RoomSpec
        if RoomName is entry `name` of RoomSpec return
        increment RoomNumber
    end
    put -1 into RoomNumber
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Add a warning message
AddWarning:
    add 1 to BadResponses
    if WarningMessages is not empty put WarningMessages cat `<br>` into WarningMessages
    put WarningMessages cat WarningMessage into WarningMessages
    set entry `status` of SystemData to `suspect`
    set entry Sensor of SystemData to SensorValues
    log `Warning: ` cat WarningMessage
    put empty into WarningMessage
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Clear the network error counter
ClearNetworkErrors:
    put -1 into NetworkErrors
!   Bump the network error counter
NetworkError:
    add 1 to NetworkErrors
    save  `` cat NetworkErrors to RamDisk cat `network-errors`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a message to an ESP-Now device
MessageESPDevice:
    if MasterIPAddr is empty return
!    log `Send ` cat Message cat ` to ` cat Name
    put entry `ssid` of Device into DeviceMAC
    put from 8 of DeviceMAC into DeviceMAC
    put `http://` cat MasterIPAddr cat `/?mac=` into URL
    if Device has entry `path` put entry `path` of Device into Path else put empty into Path
    if Path is empty put URL cat DeviceMAC cat `&msg=` cat Message into URL
    else
    begin
        put the position of `,` in Path into P
        if P is greater than 0
        begin
            put URL cat left P of Path into URL
            increment P
            put URL cat `&msg=!` cat from P of Path into URL
            put URL cat DeviceMAC cat `,` cat Message into URL
        end
        else put URL cat Path cat `&msg=!` cat DeviceMAC cat `,` cat Message into URL
    end
!    log URL
    get Response from url URL
    or begin
!        log `!!!!!!!! Message to '` cat Name cat `' failed`
        reset Response
    end
    put from 6 of DeviceMAC into DeviceMAC
    log `?mac=` cat DeviceMAC cat `&msg=` cat Message cat ` -> ` cat Response
    if left 2 of Response is not `OK`
    begin
        if the position of `ESP_ERR_ESPNOW_CHAN` in Response is greater than 0
        begin
            put RamDisk cat `channel` into FileName
            if file FileName exists load Value from FileName else put `0` into Value
            put the value of Value into Value
            increment Value
            save `` cat Value to FileName
            log Value cat ` channel errors`
            if Value is greater than 20
            begin
                save `0` to FileName
                put MasterDevice into Device
                put `reset` into Message
                gosub to MessageESPDevice
                wait 5
                exit
            end
        end
!        log `Bad response: ` cat Response
        reset Response
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a request from the UI
ProcessRequest:
    reset Dict
    if file RamDisk cat `request` exists
    begin
        load Dict from RamDisk cat `request`
    end
    if Dict is empty return

    log `Request: ` cat Dict
    put Dict into LogText
    gosub to Log
    set Confirm
    put entry `action` of Dict into Action
    log Action
    if Action is `SystemName`
    begin
        set entry `name` of Map to entry `SystemName` of Dict
        set Changed
    end
    else if Action is `map`
    begin
        put entry `map` of Dict into Map
        set Changed
        return
    end
    else if Action is `addroom`
    begin
        put entry `spec` of Dict into RoomSpec
        append RoomSpec to Rooms
        gosub to UpdateRooms
    end
    else if Action is `rooms`
    begin
        put entry `rooms` of Dict into Rooms
        gosub to UpdateRooms
    end
    else if Action is `devices`
    begin
        put entry `room` of Dict into RoomName
        gosub to GetRoomByName
        if RoomNumber is not -1
        begin
            set entry `sensor` of RoomSpec to entry `sensor` of Dict
            set entry `relays` of RoomSpec to entry `relays` of Dict
            set entry `linked` of RoomSpec to entry `linked` of Dict
            set entry `protect` of RoomSpec to entry `protect` of Dict
            set entry `ptemp` of RoomSpec to entry `ptemp` of Dict
            gosub to UpdateRoom
        end
    end
    else if Action is `profiles`
    begin
        put entry `profiles` of Dict into Profiles
        put entry `profile` of Dict into CurrentProfile
        set entry `profiles` of Map to Profiles
        set entry `profile` of Map to CurrentProfile
        set entry `calendar` of Map to entry `calendar` of Dict
        set entry `calendar-data` of Map to entry `calendar-data` of Dict
        gosub to UpdateMap
    end
    else if Action is `mode`
    begin
    log prettify Dict
        put entry `mode` of Dict into Mode
        put entry `roomnumber` of Dict into RoomNumber
        put item RoomNumber of Rooms into RoomSpec
        put entry `name` of RoomSpec into RoomName
        set entry `advance-rq` of RoomSpec to `-`
        if Mode is `timed`
        begin
            if Dict has entry `advance`
            begin
                set entry `advance-rq` of RoomSpec to entry `advance` of Dict
            end
        end
        else if Mode is `on`
        begin
            if Dict has entry `target`
                set entry `target` of RoomSpec to entry `target` of Dict
        end
        if Mode is `boost`
        begin
            if RoomSpec has entry `mode` put entry `mode` of RoomSpec into Value else put `off` into Value
            if Dict has entry `target`
                set entry `target` of RoomSpec to entry `target` of Dict
            if Value is not `boost`
                set entry `prevmode` of RoomSpec to Value
            set entry `boost` of RoomSpec to entry `boost` of Dict
        end
        set entry `mode` of RoomSpec to Mode
        put `Mode ` cat Mode cat ` set in ` cat RoomName into LogText
        gosub to Log
        gosub to UpdateRoom
    end
    else if Action is `roomname`
    begin
        put entry `roomnumber` of Dict into RoomNumber
        put item RoomNumber of Rooms into RoomSpec
        set entry `name` of RoomSpec to entry `name` of Dict
        gosub to UpdateRoom
    end
    else if Action is `roomdata`
    begin
        put entry `roomnumber` of Dict into RoomNumber
        put item RoomNumber of Rooms into RoomSpec
        put entry `items` of Dict into Items
        put the keys of Items into Keys
        put 0 into N
        while N is less than the count of Keys
        begin
            put item N of Keys into Key
            set entry Key of RoomSpec to entry Key of Items
            add 1 to N
        end
        gosub to UpdateRoom
    end
    else if Action is `config`
    begin
        set entry `name` of Map to entry `name` of Dict
        set entry `address` of Map to entry `address` of Dict
        set entry `ssid` of Map to entry `ssid` of Dict
        set entry `password` of Map to entry `password` of Dict
        put entry `names` of Dict into RoomNames
        put entry `ssids` of Dict into RoomSSIDs
        put item 0 of Profiles into Profile
        put entry `rooms` of Profile into Rooms
        put 0 into R
        while R is less than the count of Rooms
        begin
            put item R of Rooms into RoomSpec
            set entry `name` of RoomSpec to item R of RoomNames
            set entry `ssid` of RoomSpec to item R of RoomSSIDs
            set item R of Rooms to RoomSpec
            add 1 to R
        end
        set entry `rooms` of Profile to Rooms
        set item 0 of Profiles to Profile
        set entry `profiles` of Map to Profiles
        put stringify Map into Map
        put item CurrentProfile of Profiles into Profile
        put entry `rooms` of Profile into Rooms
    end
    else if Action is `periods`
    begin
        put entry `roomnumber` of Dict into RoomNumber
        put item RoomNumber of Rooms into RoomSpec
        set entry `events` of RoomSpec to entry `periods` of Dict
        gosub to UpdateRoom
    end
    else if Action is `reload`
    begin
        delete file RamDisk cat `map`
    end
    else if Action is `request`
    begin
        set entry `request` of Map to entry `request` of Dict
        gosub to UpdateMap
    end
    else if Action is `halt`
    begin
        log `Request to halt the system`
        set HaltRequested
    end
!    delete file RamDisk cat `request`
    save stringify Dict to `/mnt/data/confirm.txt`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Log a message
Log:
    open Logger `log.txt` for appending
    write LogText cat newline to Logger
    close Logger
    return
