!   newController.ecs - the main program script

    script Controller

    use mqtt

    dictionary Map
    dictionary Profile
    dictionary Room
    dictionary Period
    dictionary Sender
    dictionary Senders
    dictionary ReceivedMessage
    dictionary Thermometers
    dictionary Thermometer
    dictionary Temperatures
    dictionary RoomSpec
    dictionary Data
    dictionary WaitForConfirmation
    queue MessageQueue
    list Profiles
    list Rooms
    list Events
    list SenderKeys
    list Replies
    topic ServerTopic
    topic SenderTopic
    ssh SSH
    variable MQTT_Token
    variable RoomCount
    variable RoomName
    variable RoomIndex
    variable SelectedProfile
    variable TempNow
    variable TempWas
    variable Target
    variable TargetWas
    variable PeriodNow
    variable PeriodWas
    variable PeriodActive
    variable Sensor
    variable Relays
    variable RelayType
    variable RelayState
    variable RelayStateWas
    variable EventCount
    variable LastMapSave
    variable MapHasChanged
    variable HeatingRequested
    variable Simulate
    variable Time
    variable Temp
    variable ImmediateUpdate
    variable ConfirmationRequested
    variable StateChanged
    variable MessageText
    variable SenderName
    variable SenderQoS
    variable SenderKey
    variable Reply
    variable MyID
    variable Action
    variable Mode
    variable Value
    variable Value2
    variable ThermometerUpdate
    variable LoopCount
    variable WaitCounter
    variable I
    variable P
    variable R
    variable S
    variable T
    module DeviceModule

!    debug step

    if file `sim` exists set Simulate else clear Simulate

    if Simulate run `simulator.ecs` as DeviceModule
    else run `deviceControl.ecs` as DeviceModule
    set LastMapSave to now
    clear MapHasChanged
    clear ThermometerUpdate
    clear HeatingRequested
    reset MessageQueue
    reset ReceivedMessage
    reset Temperatures
    set LoopCount to 0
    if not Simulate gosub to SetupSSH

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set up MQTT
    if file `~/.mqtt_token` does not exist
    begin
        log `No MQTT token could be found.`
        exit
    end
    load MQTT_Token from `~/.mqtt_token`

    ! Set up MQTT
    load MyID from `serverID`
    set MyID to trim MyID
    log `My ID is ` cat MyID
    init ServerTopic
        name MyID
        qos 1

    mqtt
        token MQTT_Token
        id uuid
        broker `mqtt.flespi.io`
        port 8883
        subscribe ServerTopic
    
    on mqtt connect go to Start
    on mqtt message append the mqtt message to MessageQueue
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   The main start point. This may be revisited.
Start:
    log `Load the system map`
    load Map from `map.json`
    put entry `profiles` of Map into Profiles
    put entry `profile` of Map into SelectedProfile
    if file `thermometers.json` exists load Thermometers from `thermometers.json`
    else reset Thermometers
    gosub to PopulateMapVariables

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   This the head of each loop
MainLoop:
    ! log `MainLoop`
    if not Simulate save `lock` to SSH `/mnt/data/lock`
    ! Wait for 5 seconds
    set WaitCounter to 50
    while WaitCounter is greater than 0
    begin
        wait 10 ticks
        decrement WaitCounter
        ! Short-circuit if a message arrives or an immediate update is requested
        if MessageQueue is not empty go to HandleMessages
        if ImmediateUpdate set WaitCounter to 0
        ! If the map has changed, update the UI now
        gosub SendMapToUI
    end

    ! log `Repeat ` cat LoopCount
    increment LoopCount
    ! Examine the state of each room
    put 0 into R
    while R is less than RoomCount
    begin
        index Room to R
        index TargetWas to R
        index PeriodWas to R
        index PeriodActive to R
        index RelayStateWas to R
        set RoomIndex to R
        gosub to ProcessRoom
        increment R
    end
    if not Simulate gosub to ProcessRequestRelay

    ! Signal the map has changed
    if ImmediateUpdate
    begin
        set MapHasChanged
        clear ImmediateUpdate
    end

    ! Check for incoming messages from the UI
HandleMessages:
    while MessageQueue is not empty
    begin
        pop ReceivedMessage from MessageQueue
        log ReceivedMessage
        put entry `sender` of ReceivedMessage into Sender
        set entry `last` of Sender to now
        ! Build a dictionary of senders
        put entry `name` of Sender into SenderName
        set entry SenderName of Senders to Sender
        put entry `action` of ReceivedMessage into Action
        put entry `message` of ReceivedMessage into Data
        if Action is `first`
        begin
            set MapHasChanged
            set StateChanged
            put `map` into Action
        end
        else if Action is `uirequest` gosub to ProcessUIRequest
    end

    ! Save files periodically
    add 60000 to LastMapSave giving T
    if T is less than now
    begin
!        log `Save the map`
        if Simulate save prettify Map to `map-sim.json` else save prettify Map to `map.json`
        put now into LastMapSave
        if ThermometerUpdate save prettify Thermometers to `thermometers.json`
    end

    ! If no messages have beeen received for 60 seconds, remove the sender
    put the keys of Senders into SenderKeys
    set S to 0
    while S is less than the count of SenderKeys
    begin
        put item S of SenderKeys into SenderKey
        put entry SenderKey of Senders into Sender
        put entry `last` of Sender into T
        add 100000 to T
        if now is greater than T
        begin
            put entry `name` of Sender into SenderName
            if Senders has entry SenderName delete entry SenderName of Senders
        end
        increment S
    end
    go to MainLoop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Populate all the main map variables, then do a complete cycle of the system
PopulateMapVariables:
    put item SelectedProfile of Profiles into Profile
    put entry `rooms` of Profile into Rooms
    put the count of Rooms into RoomCount
    set the elements of Room to RoomCount
    set the elements of TargetWas to RoomCount
    set the elements of PeriodWas to RoomCount
    set the elements of PeriodActive to RoomCount
    set the elements of RelayStateWas to RoomCount

    set R to 0
    while R is less than RoomCount
    begin
        index Room to R
        put item R of Rooms into Room

        ! Housekeeping
        delete entry `advance-rq` of Room
        delete entry `boost` of Room
        delete entry `responses` of Room

        ! Initialisation

        index PeriodWas to R
        set PeriodWas to 0
        index PeriodActive to R
        set PeriodActive to 0
        set RelayStateWas to empty
        index TargetWas to R
        set TargetWas to 0
        increment R
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a single room
ProcessRoom:
    put entry `name` of Room into RoomName
    put entry `target` of Room into TargetWas
    put entry `sensor` of Room into Sensor
    reset RoomSpec
!    log `Process ` cat RoomName

    ! Get the current temperature
    if Simulate
    begin
        put entry `temperature` of Room into TempNow
        if TempNow is not numeric
        begin
            put TempNow into Temp
            gosub to ConvertTempToInt
            put Temp into TempNow
        end
    end
    else
    begin
        set TempNow to empty
        if Thermometers has entry Sensor
        begin
            put entry Sensor of Thermometers into Thermometer
            put entry `ts` of Thermometer into T
            add 1800000 to T
            if T is greater than now
            begin
                set TempNow to entry `temp` of Thermometer
                set entry `battery` of RoomSpec to entry `batt` of Thermometer
            end
            else log RoomName cat ` sensor ` cat Sensor cat ` has not reported recently`
        end
        else log RoomName cat ` sensor ` cat Sensor cat ` has not yet reported`
    end
    put entry `temperature` of Room into TempWas
    set entry `temperature` of Room to TempNow
    if TempNow is not TempWas set MapHasChanged
    put entry `relays` of Room into Relays
    put entry `relayType` of Room into RelayType
    put entry `relay` of Room into RelayState
    put entry `mode` of Room into Mode
    if Room has entry `period` put entry `period` of Room into PeriodNow
    else put 0 into PeriodNow

    if Mode is `timed`
    begin
        gosub to FindCurrentPeriod
        put item PeriodActive of Events into Period
        put entry `temp` of Period into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
    else if Mode is `on`
    begin
    end
    else if Mode is `boost`
    begin
        put entry `target` of Room into Temp
        gosub to ConvertTempToInt
        put Temp into Target
    end
    else
    begin
        set Mode to `off`
        set RelayState to `off`
    end
    if TempNow is empty set RelayState to `off`

    ! Build a pack of data and send it to the device controller or the simulator
    ! It will open/close the relay and return the current temperature of the room
    set entry `room name` of RoomSpec to RoomName
    set entry `temperature` of RoomSpec to TempNow
    set entry `relays` of RoomSpec to Relays
    set entry `relay type` of RoomSpec to RelayType
    set entry `relay state` of RoomSpec to RelayState
!    log `Send room spec for ` cat RoomName
    send RoomSpec to DeviceModule
    on message go to ProcessReply
    stop
        
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Process the request relay, if any
ProcessRequestRelay:
    if Map has entry `request`
    begin
        put entry `request` of Map into RoomName
        if RoomName is empty log `No request relay`
        else
        begin
            if HeatingRequested put `on` into RelayState else put `off` into RelayState
            clear HeatingRequested
!            log RoomName cat `: ` cat RelayState
            reset RoomSpec
            set entry `request` of RoomSpec to RoomName
            set entry `relay state` of RoomSpec to RelayState
            send RoomSpec to DeviceModule
            on message go to ProcessReply
            stop
        end
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Here when the device controller returns
ProcessReply:
    put TempNow into TempWas
    put the message into Replies
!    log Replies
    if Replies is not empty
    begin
        put item 0 of Replies into Value
        if Value is numeric put Value into TempNow
        else
        begin
            put Value into Reply
            if Reply is not empty gosub to RecordThermometer
        end
        gosub to SetRelay
!        log RoomName cat `: ` cat Replies cat ` - ` cat entry `temperature` of Room
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Decide if a relay should be on or off
SetRelay:
    if Mode is `off` set RelayState to `off`
    begin
        put RelayState into RelayStateWas
        if Mode is `off` set RelayState to `off`
        else 
        begin
!            log RoomName cat ` ` cat TempNow cat `/` cat Target
            set RelayState to `off`
            if TempNow is not empty 
                if TempNow is less than Target set RelayState to `on` else set RelayState to `off`
        end
    end
    set entry `relay` of Room to RelayState
    if RelayState is `on` set HeatingRequested

    ! Update the room record
    set entry `temperature` of Room to TempNow
    set entry `relay` of Room to RelayState
    set entry `period` of Room to PeriodActive
    if Mode is `timed`
    begin
        ! If the period or the relay state have changed, signal an immediate update,
        ! otherwise just update the map and wait for the next update time
        if PeriodActive is not PeriodWas
        begin
            ! log RoomName cat `: Period ` cat PeriodWas cat `->` cat PeriodNow
            gosub to ForceUpdate
        end
        if RelayState is not RelayStateWas
        begin
            ! log RoomName cat `: RelayState ` cat RelayStateWas cat `->` cat RelayState
            set RelayStateWas to RelayState
            gosub to ForceUpdate
        end
    end
!    if RoomName is `Sunroom` log `Temp/Was: ` cat TempNow cat `/` cat TempWas

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Update part or all of of the map
UpdateRooms:
!    log `Update ` cat RoomName cat ` (` cat RoomIndex cat ` )`
    set item RoomIndex of Rooms to Room
UpdateProfile:
    set entry `rooms` of Profile to Rooms
UpdateProfiles:
    set item SelectedProfile of Profiles to Profile
UpdateMap:
    set entry `profiles` of Map to Profiles
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Force an update now. This will short-circuit the 10-second main loop
ForceUpdate:
    set ImmediateUpdate
    set MapHasChanged
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Find which period we are in (to get the target temperature)
FindCurrentPeriod:
    put entry `events` of Room into Events
    put the count of Events into EventCount
    if EventCount is 0 return
    set PeriodNow to 0
FCP2:
    if PeriodNow is EventCount
    begin
        ! We are past the last event time, so use the first
        put 0 into PeriodNow
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
    end
    else
    begin
        ! Check the current period. If we have passed it, move on.
        put item PeriodNow of Events into Period
        put entry `until` of Period into Time
        gosub to ConvertTimeToInt
        ! If we have passed this period, get the start of the next period and move on
        if now is greater than Time
        begin
            increment PeriodNow
            go to FCP2
        end
        ! Otherwise, we've found the period
    end
    set PeriodActive to PeriodNow

    ! Now get the target temperature
    put entry `temp` of Period into Temp
    gosub to ConvertTempToInt
    put Temp into Target

    ! Deal with Advance
    if entry `advance` of Room is `A`
    begin
        ! Compute the active period
        add 1 to PeriodNow giving P
        if P is EventCount put 0 into P
        set PeriodActive to P
        ! This is the period that applies for the advance
        set entry `period` of Room to PeriodActive
        ! If there's a period change while in advance, cancel the advance
        if PeriodNow is not PeriodWas
        begin
            log RoomName cat `: Cancelling the advance`
            set entry `advance` of Room to `-`
        end
    end

    ! Save the non-advance current period
    set PeriodWas to PeriodNow
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Record information about Mijia thermometers
RecordThermometer:
    if Reply is empty return
!    log Reply
    put Reply into Value
    split Value on ` `
    if the elements of Value is 1 return
    index Value to 2
    put Value into Value2
    split Value2 on `+`
    if the elements of Value2 is 1 return
    index Value2 to 1
    if Value2 is not empty
    begin
        split Value2 on `;`
        if the elements of Value2 is not less than 5
        begin
            reset Thermometer
            set entry `ts` of Thermometer to now
            index Value2 to 1
            set entry `rssi` of Thermometer to Value2
            index Value2 to 2
            multiply Value2 by 10
            set entry `temp` of Thermometer to Value2
            index Value2 to 3
            set entry `hum` of Thermometer to Value2
            index Value2 to 4
            set entry `batt` of Thermometer to Value2
            index Value2 to 0
            set entry `a4:c1:38:` cat Value2 of Thermometers to Thermometer
            set ThermometerUpdate
!            log RoomName cat ` thermometer: ` cat Thermometer
        end
        set the elements of Value to 1
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert an HH:MM time into an int seconds value
ConvertTimeToInt:
    put `` cat Time into Time
    put the index of `:` in Time into I
    put the value of left I of Time into T
    multiply T by 60
    increment I
    put the value of from I of Time into Time
    add Time to T
    multiply T by 60000 giving Time
    add today to Time
    return

! Convert a temperature string into an int hundredths value
ConvertTempToInt:
    if Temp is empty put 0 into Temp
    put the index of `.` in Temp into I
    if I is less than 0 multiply Temp by 100
    else
    begin
        put the value of left I of Temp into T
        multiply T by 100
        increment I
        put the value of from I of Temp into Temp
        add T to Temp
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
GetRoomByName:
    set RoomIndex to 0
    while RoomIndex is less than RoomCount
    begin
        index Room to RoomIndex
        if entry `name` of Room is RoomName return
        increment RoomIndex
    end
    ! Not found
    set RoomIndex to -1
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a request from the UI arriving by MQTT
ProcessUIRequest:
!    gosub to ForceUpdate
    log Data
    if Data does not have entry `action`
    begin
        log `Missing 'action' entry`
        return
    end
    put entry `action` of Data into Action
    if Action is `Test`
    begin
        log `Test message received`
    end
    else if Action is `System Name`
    begin
        set entry `name` of Map to entry `System Name` of Data
        log `Set the system name to ` cat entry `name` of Map
    end
    else if Action is `Request Relay`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        log `Set the request relay to ` cat entry `request` of Map
    end
    else if Action is `Add Room`
    begin
        set entry `request` of Map to entry `Request Relay` of Data
        put entry `Add Room` of Data into Room
        log `Add room ` cat entry `name` of Room
        append Room to Rooms
        set entry `rooms` of Profile to Rooms
        gosub to UpdateProfiles
    end
    else if Action is `Select Profile`
    begin
        put entry `Select Profile` of Data into SelectedProfile
        log `Select profile ` cat SelectedProfile
        set entry `profile` of Map to SelectedProfile
        gosub to ForceUpdate
        gosub to PopulateMapVariables
    end
    else if Action is `Operating Mode`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty return
        put entry `Operating Mode` of Data into Mode
!        log `Set Operating Mode of ` cat RoomName cat ` to ` cat Mode
        set entry `mode` of Room to Mode
        if Mode is `timed`
        begin
            if Data has entry `Advance`
            begin
                ! Toggle the advance
                if entry `advance` of Room is `-`
                begin
                    ! Not already advanced, so set it up
                    log RoomName cat ` set Advance`
                    set entry `advance` of Room to `A`
                end
                else
                begin
                    ! Already advanced so cancel it
                    log RoomName cat ` cancel Advance`
                    set entry `advance` of Room to `-`
                end
                gosub to UpdateRooms
                gosub to ForceUpdate
            end
        end
        else if Mode is `boost`
        begin
            set T to entry `duration` of Data
            multiply T by 60000
            add now to T
            set entry `until` of Room to T
            set entry `target` of Room to entry `target` of Data
        end
        gosub to UpdateRooms
    end
    else if Action is `Periods`
    begin
        put entry `room name` of Data into RoomName
        gosub to GetRoomByName
        if Room is empty return
        log `Set the periods of ` cat entry `name` of Room cat ` to ` cat newline cat entry `Periods` of Data
        set entry `events` of Room to entry `Periods` of Data
        gosub to UpdateRooms
        gosub to ForceUpdate
    end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   If the map has changed, send it to the UI. There may be more than one.
!   If the map has not changed, do nothing.
SendMapToUI:
    if Map is empty
    begin
        gosub to LoadMap
        set MapHasChanged
    end
    if MapHasChanged
    begin
        log `Send map to UI`
        set MessageText to Map
        clear MapHasChanged
        put the keys of Senders into SenderKeys
        set S to 0
        while S is less than the count of SenderKeys
        begin
            put item S of SenderKeys into SenderKey
            put entry SenderKey of Senders into Sender
            gosub to SendMessage
            increment S
        end
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Send a message to the UI
SendMessage:
    if Sender is empty return
    put entry `name` of Sender into SenderName
    put entry `qos` of Sender into SenderQoS
    init SenderTopic
        name SenderName
        qos SenderQoS
    if ConfirmationRequested
    begin
        set entry SenderName of WaitForConfirmation
        set Action to `confirm`
    end
    else set Action to `reply`
!    log left 20 of MessageText
    send to SenderTopic
        action Action
        message MessageText
    clear ConfirmationRequested
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SetupSSH:
    set SSH
        host `192.168.1.214`
        user `linaro`
        password `r00m8Yr00m`
    if error in SSH
    begin
        log `SSH error: ` cat the error of SSH
        stop
    end
    log `SSH set up successfully`
    return
