!   XR-NOW updater

    script Updater

    variable SourceName
    variable FileName
    variable File
    variable Segment
    variable Sequence
    variable Result
    variable Length
    variable Total
    variable Start
    variable Finish
    variable Blocksize
    variable FileSize
    variable MAC
    variable IPAddr
    variable URL

Start:
    set the encoding to `hex`
    put 100 into Blocksize

    put `9c9e6e0ba955` into MAC
    put `192.168.1.101` into IPAddr
    put `room2.json` into SourceName
    put `config.json` into FileName

    print `Updating ` cat FileName
    load File from SourceName
    put the length of File into FileSize
    print `File size: ` cat FileSize
    put 0 into Sequence
    put 0 into Start
    put 0 into Total
    while Start is less than FileSize
    begin
        if Finish is greater than FileSize put FileSize into Finish
        add Blocksize to Start giving Finish
        put from Start to Finish of File into Segment
        put the length of Segment into Length
        add Length to Total
        put encode Segment into Segment
        put `http://` cat IPAddr cat `/?mac=` cat MAC cat `&msg=part:`
            cat Sequence cat `,text:` cat Segment into URL
        get Result from URL or go to Restart
!        log Result
        if Result is not `True`
            if Result is not Length print `Mismatched length: ` cat Length cat ` - ` cat Result
        put Finish into Start
        increment Sequence
    end

    get Result from `http://` cat IPAddr cat `/?mac=` cat MAC cat `&msg=save:` cat FileName
        or go to Restart
    if Result is not `True`
    begin
        if Result is Total print Result cat ` chars transferred`
        else print `Mismatched length: ` cat Total cat ` - ` cat Result
    end
    wait 3
    get Result from `http://` cat IPAddr cat `/?mac=` cat MAC cat `&msg=reset`
    or exit

    exit

Restart:
    print `An error occurred - restarting`
    wait 2
    go to Start
