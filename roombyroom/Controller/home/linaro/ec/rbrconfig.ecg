!	RBR-Now configuration utility

	script RBRConfig

    layout Layout
    layout Row
    layout Row2
    layout Columns
    column Column
    frame Frame
    window Window
    window SystemsWindow
    variable Systems
    variable System
    variable SystemMAC
    variable SystemPassword
    variable HostSSID
    variable HostPassword
    variable Config
    variable IPAddr
    variable Slaves
    variable ScanResult
    variable IsMaster
    variable MasterName
    variable MasterSSID
    variable MasterMAC
    variable MasterIPAddr
    variable Name
    variable SSID
    variable MAC
    variable List
    variable Info
    variable APInfo
    variable Keys
    variable Files
    variable FileName
    variable FileSize
    variable Sequence
    variable MasterDevice
    variable SelectedDevice
    variable Device
    variable Devices
    variable Start
    variable Finish
    variable Total
    variable Segment
    variable Length
    variable URL
    variable Value
    variable Result
    variable Content
    variable SendFail
    variable Pins
    variable Pin
    variable Flag
    variable Ready
    variable D
    variable F
    variable K
    variable N
    variable P
    variable S

!    debug step

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Initialisation

    clear Ready
    if file `.rbrsystems.json` exists
    begin
        load Systems from `.rbrsystems.json`
        put property `name` of Systems into Name
        put property Name of Systems into System
        put property `systemmac` of System into SystemMAC
        put property `systempass` of System into SystemPassword
    end
    else
    begin
        put json `{}` into Systems
        put empty into Name
        gosub to SelectSystem
        if Name is empty exit
    end
    get Config from
        `https://rbrheating.com/home/resources/php/rest.php/config/` cat SystemMAC cat `/` cat SystemPassword
    or
    begin
        print `Can't read the config file`
        exit
    end

    if Config is empty
    begin
        put json `{}` into Config
        put json `{}` into Devices
        set property `devices` of Config to Devices
        set property `ssid` of Config to empty
        set property `password` of Config to empty
    end
    else put json Config into Config
    put property `devices` of Config into Devices
    put property `ssid` of Config into HostSSID
    put property `password` of Config into HostPassword

    ! Scan the devices to find the master, if there is one
    put empty into MasterSSID
    put the keys of Devices into Keys
    put 0 into N
    while N is less than the count of Keys
    begin
        put element N of Keys into Name
!        if Name is `(empty)` put empty into Name
        put property Name of Devices into Device
        if property `master` of Device is true
        begin
            put Name into MasterName
            put property `ssid` of Device into MasterSSID
            put property `ipaddr` of Device into MasterIPAddr
        end
        increment N
    end

    put empty into IPAddr

    set the encoding to `hex`

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Draw the UI

    init Layout     ! The main layout
    init Columns    ! The two columns

!   The left-hand column
    init Column

    init Row
    add Button button_text `Reset config` and size `20 1` to Row
    add Row to Column
    init Row
    add Button button_text `Scan network` and size `20 1` to Row
    add Row to Column
    init Row
    add Button button_text `Select Master` and size `20 1` to Row
    add Row to Column
    init Row
    add Button button_text `Select Slave` and size `20 1` to Row
    add Row to Column
    init Row
    add Button button_text `Update Files` and size `20 1` to Row
    add Row to Column
    init Row
    add Button button_text `Exit` and size `20 1` to Row
    add Row to Column
    add Column to Columns

!   The right-hand column
    init Column

!   The system name
    init Row
    init Frame
    init Row2
    add Text key `-SYSTEMNAME-` and size `30 1` to Row2
    add Button button_text `Select system` and size `20 1` to Row2
    add Row2 to Frame
    add Frame title `System Name` and expand_x true and pad 10 to Row
    add Row to Column

!   The host SSID and password
    init Row
    init Frame
    init Row2
    add Text text `SSID:` and size `10 1` to Row2
    add Input key `-HOST_SSID-` and expand_x true to Row2
    add Row2 to Frame

    init Row2
    add Text text `Password:` and size `10 1` to Row2
    add Input key `-HOST_PASS-` and expand_x true to Row2
    add Row2 to Frame

    init Row2
    add Text size `10 1` to Row2
    add Button button_text `Save Host Details` to Row2
    add Row2 to Frame

    add Frame title `Host` and expand_x true and pad 10 to Row
    add Row to Column

!   Known devices
    init Row
    init Frame
    init Row2
    add Listbox key `-APLIST-` and size `60 10` and pad 10 to Row2
    add Row2 to Frame
    add Frame title `Slave devices` and expand_x true and pad 10 to Row
    add Row to Column

!   The master device
    init Row
    init Frame
    init Row2
    add Text key `-MASTER-` and expand_x true to Row2
    add Row2 to Frame
    add Frame title `Master device` and expand_x true and pad 10 to Row
    add Row to Column

!   The selected device
    init Row
    init Frame
    init Row2
    add Text text `Name:` to Row2
    add Input key `-NAME-` and size `12 1` to Row2
    add Text text `Channel:` to Row2
    add Input key `-CHANNEL-` and size `2 1` to Row2
    add Text expand_x true to Row2
    add Text key `-MS-` to Row2
    add Row2 to Frame
    init Row2
    add Text text `LED Pin #` and size `11 1` to Row2
    add Input key `-LED_PIN-` and size `3 1` to Row2
    add Checkbox text `Inverted pin logic` and  key `-LED_INVERT-` to Row2
    add Row2 to Frame
    init Row2
    add Text text `Relay Pin #` and size `11 1` to Row2
    add Input key `-RELAY_PIN-` and size `3 1` to Row2
    add Checkbox text `Inverted pin logic` and  key `-RELAY_INVERT-` to Row2
    add Row2 to Frame
    init Row2
    add Text text `DHT22 Pin #` and size `11 1` to Row2
    add Input key `-DHT22_PIN-` and size `3 1` to Row2
    add Row2 to Frame
    add Frame title `Selected device` and expand_x true and pad 10 to Row
    add Button button_text `Update` and size `10 1` to Row
    add Row to Column

!   The status message
    init Row
    add Text key `-STATUS-` to Row
    add Row to Column

    add Column expand_x true to Columns
    add Columns to Layout

    create Window `RBR-Now Configurator`
        layout Layout

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Deal with events

    on event `Select system` in Window
    begin
        gosub to SelectSystem
        stop
    end

    on event `Reset config` in Window
    begin
        set property `devices` of Config to json `{}`
        gosub to PostConfigData
        gosub to RefreshUI
    end

    on event `Scan network` in Window
    begin
        gosub to ScanNetwork
        gosub to ListConfiguredSlaves
        stop
    end

    on event `Select Master` in Window go to SelectMaster

    on event `Select Slave` in Window go to SelectSlave

    on event `Update Files` in Window go to UpdateFiles

    on event `Update` in Window go to UpdateProperties

    on event `Exit` in Window
    begin
        set property `ssid` of Config to the value of key `-HOST_SSID-` in Window
        set property `password` of Config to the value of key `-HOST_PASS-` in Window
        gosub to PostConfigData
        close Window
        exit
    end
    gosub to RefreshUI
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Refresh the UI.
RefreshUI:
    set property `disabled` of `Update Files` in Window to true
    put empty into SelectedDevice
!    set property `value` of `-SYSTEMNAME-` in Window to property `name` of Systems
    set property `value` of `-HOST_SSID-` in Window to HostSSID
    set property `value` of `-HOST_PASS-` in Window to HostPassword
    put json `[]` into Slaves
    put the keys of Devices into Keys
    put 0 into N
    while N is less than the count of Keys
    begin
        put element N of Keys into Name
        put property Name of Devices into Device
        if property `master` of Device is true
        begin
            put Device into MasterDevice
            print MasterSSID cat ` ` cat MasterName cat ` ` cat MasterIPAddr
            set property `value` of `-MASTER-` in Window to MasterSSID cat ` ` cat MasterName cat ` ` cat MasterIPAddr
        end
        else append SSID cat ` ` cat Name to Slaves
        increment N
    end
    if the count of Slaves is 0 set property `disabled` of `Select Slave` in Window to true
    set property `values` of `-APLIST-` in Window to Slaves
    if MasterSSID is empty
    begin
        set property `disabled` of `-NAME-` in Window to true
        set property `disabled` of `-CHANNEL-` in Window to true
        set property `disabled` of `-LED_PIN-` in Window to true
        set property `disabled` of `-LED_INVERT-` in Window to true
        set property `disabled` of `-RELAY_PIN-` in Window to true
        set property `disabled` of `-RELAY_INVERT-` in Window to true
        set property `disabled` of `-DHT22_PIN-` in Window to true
        set property `disabled` of `Select Master` in Window to true
    end
    else
    begin
        set property `disabled` of `-NAME-` in Window to false
        set property `disabled` of `-CHANNEL-` in Window to false
        set property `disabled` of `-LED_PIN-` in Window to false
        set property `disabled` of `-LED_INVERT-` in Window to false
        set property `disabled` of `-RELAY_PIN-` in Window to false
        set property `disabled` of `-RELAY_INVERT-` in Window to false
        set property `disabled` of `-DHT22_PIN-` in Window to false
        set property `disabled` of `Select Master` in Window to false
    end
    return

AskForHostInfo:
    popup `Please provide the host SSID and password information`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Scan the local network
ScanNetwork:
    clear Flag
    if HostSSID is empty
    begin
        put the value of key `-HOST_SSID-` in Window into HostSSID
        if HostSSID is empty go to AskForHostInfo
        set Flag
    end
    if HostPassword is empty
    begin
        put the value of key `-HOST_PASS-` in Window into HostPassword
        if HostPassword is empty go to AskForHostInfo
        set Flag
    end
    if Flag gosub to PostConfigData

    set property `value` of `-STATUS-` in Window to `Scanning the local network...`
    wait 10 ticks   ! for the screen to update
    put system `nmcli device wifi list` into ScanResult
!    log ScanResult
    split ScanResult
    put json `[]` into List
    put 1 into N
    while N is less than the elements of ScanResult
    begin
        index ScanResult to N
        put ScanResult into SSID
        if left 1 of SSID is `*` begin end
        else
        begin
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put from P of SSID into SSID
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put left P of SSID into SSID
            if left 7 of SSID is `RBR-Now`
            begin
                append SSID to List
            end
        end
        increment N
    end
    put json `[]` into Slaves
!    set property `values` of `-APLIST-` in Window to List
    put 0 into N
SNLoop:
    while N is less than the count of List
    begin
        put element N of List into SSID
        log `Device ` cat N cat `: ` cat SSID
        log `SSID=` cat SSID cat `, MasterSSID=` cat MasterSSID
        if SSID is not MasterSSID
        begin
            put 0 into S
            while S is less than the count of Slaves
            begin
                log `Element ` cat S cat ` of Slaves; SSID=` cat SSID
                if element S of Slaves is SSID
                begin
                    log `Skip this one`
                    increment N
                    go to SNLoop
                end
                increment S
            end
            set property `value` of `-STATUS-` in Window to `Connecting to ` cat SSID
            wait 10 ticks
            gosub to ConnectToAP
        end
        increment N
    end
    gosub to ListConfiguredSlaves

Reconnect:
    print `Reconnect to default AP`
    set property `value` of `-STATUS-` in Window to `Reconnecting to default AP`
    system `nmcli dev wifi connect ` cat HostSSID cat ` password ` cat HostPassword
    if the count of Slaves is 0
    begin
        set property `values` of `-APLIST-` in Window to json `[]`
    end
    else set property `values` of `-APLIST-` in Window to Slaves
    set property `value` of `-STATUS-` in Window to `Ready`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Connect to the access point given by SSID
ConnectToAP:
    log `Connect to ` cat SSID cat `: MasterSSID=` cat MasterSSID
    set property `value` of `-STATUS-` in Window to `Activate ` cat SSID
    wait 10 ticks
    system `nmcli dev wifi connect ` cat SSID cat ` password 00000000`
    wait 1
    put system `hostname -I` into IPAddr
    put the position of the last `.` in IPAddr into D
    increment D
    put left D of IPAddr into IPAddr
    put IPAddr cat `1` into IPAddr
    put `http://` cat IPAddr into URL
    set property `value` of `-STATUS-` in Window to `Request AP details for ` cat SSID
    wait 10 ticks
    get APInfo from URL
    or begin
        print `Failed to interrogate ` cat SSID
        set property `value` of `-STATUS-` in Window to `Failed to interrogate ` cat SSID
        wait 2
        return
    end
    put trim APInfo into APInfo
    put APInfo into Info
    split Info on ` `
    index Info to 2
    put Info into Name
    index Info to 0
    put Info into MAC
    ! If this one is known, ignore it
    if Devices has property Name return
    ! Check if this is the first device to be configured. If so, make it the master.
    if MasterSSID is empty
    begin
        set IsMaster
        put SSID into MasterSSID
        put Name into MasterName
        gosub to CreateDevice
        put Device into MasterDevice
        ! Send the config data to the device
        put `config.json` into FileName
        put stringify MasterDevice into Content
        put MAC into MasterMAC
        put IPAddr into MasterIPAddr
        gosub to SendFile
        if SendFail return
        gosub to RequestReset
        if SendFail return
        set property `value` of `-MASTER-` in Window to MasterSSID cat ` ` cat MasterName
        set property `disabled` of `Select Master` in Window to false
        wait 5
        put `http://` cat MasterIPAddr cat `/?mac=` cat MasterMAC cat `&msg=ipaddr` into URL
        gosub to LogURL
        set property `value` of `-STATUS-` in Window to `Get the Master IP address`
        wait 10 ticks
        log `Connect to ` cat MasterSSID cat `/00000000`
        system `nmcli dev wifi connect ` cat MasterSSID cat ` password 00000000`
        wait 1
        get MasterIPAddr from URL
        or begin
            print `Failed to get Master ip address ` cat MasterSSID
            set property `value` of `-STATUS-` in Window to `Failed to get Master ip address`
            wait 2
            return
        end
        log `Master ip address is ` cat MasterIPAddr
        set property `value` of `-STATUS-` in Window to `Master ip address is ` cat MasterIPAddr
        set property `value` of `-MASTER-` in Window to MasterSSID cat ` ` cat MasterName cat ` ` cat MasterIPAddr
        set property `ipaddr` of MasterDevice to MasterIPAddr
        set property MasterName of Devices to MasterDevice
        set property `devices` of Config to Devices
        gosub to RequestReset
    end
    else
    begin
        log `Deal with slave ` cat Name
        append SSID cat ` ` cat Name to Slaves
        set property `values` of `-APLIST-` in Window to Slaves
        set property `disabled` of `Select Slave` in Window to false
        clear IsMaster
        gosub to CreateDevice
        put `config.json` into FileName
        put stringify Device into Content
        put from 8 of SSID into MAC
        gosub to SendFile
        gosub to RequestReset
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Create a device entry in the config file
CreateDevice:
    put json `{}` into Device
    if IsMaster
    begin
        log `Create master device ` cat Name
        set property `hostssid` of Device to HostSSID
        set property `hostpass` of Device to HostPassword
        set property `ipaddr` of Device to empty
    end
    else log `Create slave device ` cat Name
    set property `master` of Device to IsMaster
    set property `name` of Device to Name
    set property `ssid` of Device to SSID
    set property `channel` of Device to 1
    put json `{}` into Pins
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `invert` of Pin to false
    set property `led` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `invert` of Pin to false
    set property `relay` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `dht22` of Pins to Pin
    set property `pins` of Device to Pins
    set property Name of Devices to Device
    set property `devices` of Config to Devices
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SelectMaster:
    print MasterDevice
    set property `value` of `-NAME-` in Window to MasterName
    set property `value` of `-CHANNEL-` in Window to property `channel` of MasterDevice
    set property `value` of `-MS-` in Window to `Master`
    put property `pins` of MasterDevice into Pins
    put property `led` of Pins into Pin
    set property `value` of `-LED_PIN-` in Window to property `pin` of Pin
    set property `value` of `-LED_INVERT-` in Window to property `invert` of Pin
    if Pins has property `relay`
    begin
        put property `relay` of Pins into Pin
        set property `value` of `-RELAY_PIN-` in Window to property `pin` of Pin
        set property `value` of `-RELAY_INVERT-` in Window to property `invert` of Pin
    end
    if Pins has property `thermometer`
    begin
        put property `thermometer` of Pins into Pin
        set property `value` of `-DHT22_PIN-` in Window to property `pin` of Pin
    end
    set property `disabled` of `Update Files` in Window to false
    put MasterDevice into SelectedDevice
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SelectSlave:
    put the value of key `-APLIST-` in Window into Info
    if Info is empty stop
    split Info on ` `
    index Info to 0
    put Info into SSID
    index Info to 1
    put Info into Name
    if Devices has property Name
    begin
        put property Name of Devices into Device
        set property `disabled` of `Update Files` in Window to false
        set property `value` of `-NAME-` in Window to Name
        set property `value` of `-CHANNEL-` in Window to property `channel` of Device
        set property `value` of `-MS-` in Window to `Slave`
        put property `pins` of Device into Pins
        put property `led` of Pins into Pin
        set property `value` of `-LED_PIN-` in Window to property `pin` of Pin
        set property `value` of `-LED_INVERT-` in Window to property `invert` of Pin
        if Pins has property `relay`
        begin
            put property `relay` of Pins into Pin
            set property `value` of `-RELAY_PIN-` in Window to property `pin` of Pin
            set property `value` of `-RELAY_INVERT-` in Window to property `invert` of Pin
        end
        if Pins has property `thermometer`
        begin
            put property `thermometer` of Pins into Pin
            set property `value` of `-DHT22_PIN-` in Window to property `pin` of Pin
        end
        set property `disabled` of `Update Files` in Window to false
        put Device into SelectedDevice
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! List all the configured slaves
ListConfiguredSlaves:
    put json `[]` into Slaves
    put the keys of Devices into Keys
    put 0 into N
    while N is less than the count of Keys
    begin
        put element N of Keys into Name
        put property Name of Devices into Device
        if property `master` of Device is not true
        begin
            put property `ssid` of Device into SSID
            append SSID cat ` ` cat Name to Slaves
        end
        increment N
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update device properties
UpdateProperties:
    if SelectedDevice is empty stop
    put SelectedDevice into Device
    put property `name` of Device into Name
    delete property Name of Devices
    put the value of key `-NAME-` in Window into Name
    set property `name` of Device to Name
    put the value of key `-MS-` in Window into Value
    if Value is `Master` set property `master` of Device to true
    else set property `master` of Device to false
    set property `channel` of Device to the value of key `-CHANNEL-` in Window
    put json `{}` into Pins
    put json `{}` into Pin
    set property `pin` of Pin to the value of key `-LED_PIN-` in Window
    set property `invert` of Pin to the value of key `-LED_INVERT-` in Window
    set property `led` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to the value of key `-RELAY_PIN-` in Window
    set property `invert` of Pin to the value of key `-RELAY_INVERT-` in Window
    set property `relay` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to the value of key `-DHT22_PIN-` in Window
    set property `dht22` of Pins to Pin
    set property `pins` of Device to Pins
    set property Name of Devices to Device
    set property `devices` of Config to Devices
!   Save to the device
    put `config.json` into FileName
    put stringify Device into Content
    put property `ssid` of Device into SSID
    put from 8 of SSID into MAC
    gosub to SendFile
    wait 2
    if SendFail stop
    gosub to RequestReset
    wait 2
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update all the files on a device
UpdateFiles:
    if SelectedDevice is empty stop
    put property `ssid` of SelectedDevice into SSID
    split SSID on ` `
    print `Updating ` cat SSID
    set property `value` of `-STATUS-` in Window to `Updating ` cat SSID cat `... `
    put from 8 of SSID into MAC

    load Files from `files.txt`
    split Files
    put 0 into F
    while F is less than the elements of Files
    begin
        index Files to F
        put trim Files into FileName
        if FileName is not empty gosub to UpdateOne
        increment F
    end
    gosub to RequestReset
    set property `value` of `-STATUS-` in Window to `All files updated`
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update a single file
UpdateOne:
    if file FileName exists begin end
    else
    begin
        print `File ` cat FileName cat ` does not exist`
        return
    end
    print `Update ` cat FileName
    load Content from FileName

SendFile:
    put the length of Content into FileSize
    set property `value` of `-STATUS-` in Window to `Updating ` cat FileName cat ` (size=` cat FileSize cat `)`
    wait 10 ticks
    clear SendFail
    put 0 into Sequence
    put 0 into Start
    put 0 into Total
    while Start is less than FileSize
    begin
        if Finish is greater than FileSize put FileSize into Finish
        add 100 to Start giving Finish
        put from Start to Finish of Content into Segment
        put the length of Segment into Length
        add Length to Total
        put encode Segment into Segment
!        log `Send part ` cat Sequence
        put `http://` cat MasterIPAddr cat `/?mac=` cat MAC cat `&msg=part:`
            cat Sequence cat `,text:` cat Segment into URL
        gosub to LogURL
        get Result from URL
        or begin
            log `Part ` cat Sequence cat ` of ` cat FileName cat ` failed`
            set SendFail
            return
        end
        if Result is not Length
        begin
            set property `value` of `-STATUS-` in Window to `Failed to send part ` cat Sequence
            go to SendFail
        end
        put Finish into Start
        increment Sequence
    end
!    log `Send the save message for ` cat FileName
    put `http://` cat MasterIPAddr cat `/?mac=` cat MAC cat `&msg=save:` cat FileName into URL
    gosub to LogURL
    get Result from URL
    or begin
        log `Save failed`
        set SendFail
        return
    end
    if Result is not Total
    begin
        log `Mismatched length: ` cat Total cat ` - ` cat Result
        set property `value` of `-STATUS-` in Window to `Mismatched length: ` cat Total cat ` - ` cat Result
    end
    set property `value` of `-STATUS-` in Window to `File ` cat FileName cat ` saved`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a reset message to a device
RequestReset:
    log `Send a reset message via ` cat MasterIPAddr cat ` to ` cat MAC
    put `http://` cat MasterIPAddr cat `/?mac=` cat MAC cat `&msg=reset` into URL
    gosub to LogURL
    get Result from URL or go to SR2
    if Result is `OK`
    begin
        set property `value` of `-STATUS-` in Window to `Device has been reset`
        return
    end
SR2:
    log `Reset failed`
    set property `value` of `-STATUS-` in Window to `Device reset failed`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Log a URL
LogURL:
!    log URL
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Post the config data
PostConfigData:
    save Config to `.config.json`
    post stringify Config to
        `https://rbrheating.com/home/resources/php/rest.php/config/` cat SystemMAC cat `/` cat SystemPassword
    or
    begin
        print `I couldn't post the config file`
        return
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Select a system to configure
SelectSystem:
    ! TODO: List the known systems and offer a choice. If none exist, add a new one
    if Systems has property `name` go to ShowSystems

AddSystem:
    init Layout
    init Row
    add Text text `Name:` and size `10 1` to Row
    add Input key `-NAME-` and size `40 1` to Row
    add Row to Layout
    init Row
    add Text text `MAC:` and size `10 1` to Row
    add Input key `-MAC-` and size `40 1` to Row
    add Row to Layout
    init Row
    add Text text `Password:` and size `10 1` to Row
    add Input key `-PASSWORD-` and size `40 1` to Row
    add Row to Layout
    init Row
    add Text size `10 1` to Row
    add Button button_text `Add system` and size `20 1` to Row
    add Button button_text `Cancel` and expand_x true to Row
    add Row to Layout

    create SystemsWindow `System Info`
        layout Layout

    on event `Add system` in SystemsWindow
    begin
        put the value of key `-NAME-` in SystemsWindow into Name
        put the value of key `-MAC-` in SystemsWindow into SystemMAC
        put the value of key `-PASSWORD-` in SystemsWindow into SystemPassword
        if Name is empty go to Retry
        if SystemMAC is empty go to Retry
        if SystemPassword is empty go to Retry
        put json `{}` into Info
        set property `systemmac` of Info to SystemMAC
        set property `systempass` of Info to SystemPassword
        set property Name of Systems to Info
        set property `name` of Systems to Name
        save Systems to `.rbrsystems.json`
        close SystemsWindow
        return
    end

    on event `Cancel` in SystemsWindow go to Cancel
    stop

Retry:
    popup `Please give the name, MAC address and password of your system`
    stop

Cancel:
    put empty into Name
    close SystemsWindow
    return

ShowSystems:
    init Layout
    init Row
    add Listbox key `-SYSTEMS-` and size `60 10` and pad 10 to Row
    add Row to Layout
    init Row
    add Text size `10 1` to Row
    add Button button_text `Select` and size `10 1` to Row
    add Button button_text `Add` and size `10 1` to Row
    add Button button_text `Cancel` and expand_x true to Row
    add Row to Layout

    create SystemsWindow `Select System`
        layout Layout

    put json `[]` into List
    put the keys of Systems into Keys
    put 0 into K
    while K is less than the count of Keys
    begin
        put element K of Keys into Name
        if Name is not `name` append Name to List
        increment K
    end
    set property `values` of `-SYSTEMS-` in SystemsWindow to List

    on event `Select` in SystemsWindow
    begin
        put the value of key `-SYSTEMS-` in SystemsWindow into Name
        print `Selected ` cat Name
        set property `name` of Systems to Name
        put property Name of Systems into System
        put property `systemmac` of System into SystemMAC
        put property `systempass` of System into SystemPassword
        close SystemsWindow
        return
    end

    on event `Add` in SystemsWindow
    begin
        close SystemsWindow
        gosub to AddSystem
        set property `value` of `-SYSTEMNAME-` in SystemsWindow to property `name` of Systems
        stop
    end

    on event `Cancel` in SystemsWindow go to Cancel
    stop
