!   Health checker

    script HealthCheck

    file File
    variable Line
    variable Devices
    variable IPAddr
    variable Addresses
    variable Time
    variable Array
    variable Item
    variable Hours
    variable Minutes
    variable Seconds
    variable Current
    variable Diff
    variable ErrorCount
    variable A
    variable L
    variable M
    variable N
    variable P

!    debug step

    put json `{}` into Devices
    put 0 into ErrorCount

!   Read the log file, line by line
    open File `log.txt` for reading
    while true
    begin
        read line Line from File
        if Line is empty
        begin
            close File
            go to Analyse
        end
        put trim Line into Line
        gosub to ProcessLine
    end

!   Process a line, ignoring any that don't look like H&T messages
ProcessLine:
    if Line is empty return
    split Line on ` `
    if the elements of Line is not 10 return
    index Line to 0
    put Line into IPAddr
    index Line to 4
    take 1 from the length of Line giving L
    put left L of Line into Time
    if property IPAddr of Devices is empty put json `[]` into Array
    else put property IPAddr of Devices into Array
    append Time to Array
    set property IPAddr of Devices to Array
    return

!   Analyse the data collected
Analyse:
    put the keys of Devices into Addresses
    put 0 into A
    while A is less than the count of Addresses
    begin
        put element A of Addresses into IPAddr
!        print IPAddr
        put property IPAddr of Devices into Array
        put 0 into N
        while N is less than the count of Array
        begin
            put element N of Array into Item
!            print Item
            gosub to GetSeconds
            if N is 0 put Seconds into Current
            else gosub to CheckDifference
            add 1 to N
        end
        put now modulo 86400 into Seconds
        gosub to CheckDifference
        add 1 to A
    end
    if ErrorCount is 0 print `No problems detected`
    exit

!   Check the difference between 2 times
CheckDifference:
    take Current from Seconds giving Diff
    put Seconds into Current
    if Diff is less than 0 add 86400 to Diff
    if Diff is greater than 700
    begin
        take 1 from N giving M
        put element M of Array into Item
        print IPAddr cat `: Excessive delay after ` cat Item
        add 1 to ErrorCount
    end
    return

!   Get the seconds for an HH:MM:SS timestamp
GetSeconds:
    put the position of `:` in Item into P
    put left P of Item into Hours
    add 1 to P
    put from P of Item into Item
    put the position of `:` in Item into P
    put left P of Item into Minutes
    add 1 to P
    put from P of Item into Seconds
    multiply Minutes by 60
    add Minutes to Seconds
    multiply Hours by 3600
    add Hours to Seconds
!    print Seconds
    return
