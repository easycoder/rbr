!   Get the number of extenders in this system

	script ExtenderCount

    import variable Map and variable Result

    div LeftButton
    div RightButton
    input CountInput
    variable Extenders
    variable Count
    variable Data
    variable N

    attach CountInput to `cf-count`
    attach LeftButton to `left-button`
    attach RightButton to `right-button`

    put property `extenders` of Map into Extenders
    if Extenders is empty
    begin
        put `[]` into Extenders
        put 0 into storage as `extender-index`
    end
    put the json count of Extenders into Count
    set the content of CountInput to Count

    on click LeftButton
    begin
        put `back` into Result
        exit
    end
    on click RightButton
    begin
        set style `display` of LeftButton to `none`
        set style `display` of RightButton to `none`
        put the content of CountInput into Count
        while Count is greater than the json count of Extenders
        begin
            append `{}` to Extenders
        end
        while Count is less than the json count of Extenders
        begin
            put the json count of Extenders into N
            take 1 from N
            json delete element N of Extenders
        end
        set property `extenders` of Map to Extenders

        put `url=http://172.24.1.1/resources/php/rest.php/map&data=` cat Map into Data
        rest post Data to `/config/post` or alert `POST error`
        put `extender` into Result
        exit
    end
    set style `display` of LeftButton to `block`
    set style `display` of RightButton to `block`

    stop
