!   Get the number of relays

	script RelayCount

    import variable Map and variable Result

    div LeftButton
    div RightButton
    input CountInput
    variable Extenders
    variable Extender
    variable Relays
    variable Count
    variable Data
    variable EX
    variable N

    attach CountInput to `cf-count`
    attach LeftButton to `left-button`
    attach RightButton to `right-button`

    get EX from storage as `extender`
    put property `extenders` of Map into Extenders
    put element EX of Extenders into Extender
    put property `relays` of Extender into Relays
    if Relays is empty
    begin
        put `[]` into Relays
        put `ex-` cat EX cat `-0` into storage as `relay-index`
    end
    put the json count of Relays into Count
    set the content of CountInput to Count

    on click LeftButton
    begin
        put `back` into Result
        exit
    end
    on click RightButton
    begin
        put the content of CountInput into Count
        while Count is greater than the json count of Relays
        begin
            append `{}` to Relays
        end
        while Count is less than the json count of Relays
        begin
            put the json count of Relays into N
            take 1 from N
            json delete element N of Relays
        end
        set property `relays` of Extender to Relays

        set element EX of Extenders to Extender
        set property `extenders` of Map to Extenders
        put `url=http://172.24.1.1/resources/php/rest.php/map&data=` cat Map into Data
        rest post Data to `/config/post` or alert `POST refused`
        put `end` into Result
    end

    stop
