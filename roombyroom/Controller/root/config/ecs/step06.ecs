!   Step 6 - Get an extender

    script Step06

    import variable Map and variable Result

    div LeftButton
    div RightButton
    div SSID
    div SSIDs
    div TempDiv
    div SSIDChoice
    variable Scan
    variable Values
    variable Value
    variable Controllers
    variable Controller
    variable SSIDTemplate
    variable CurrentSSID
    variable Webson
    variable Extenders
    variable Extender
    variable EX
    variable N
    variable S
    variable NC

    attach SSID to `cf-ssid`
    attach SSIDs to `cf-ssids`
    attach LeftButton to `left-button`
    attach RightButton to `right-button`

    put property `ssid` of Map into CurrentSSID

    on click LeftButton stop
    on click RightButton stop

    set the content of SSID to `Scanning for extenders...`
Rescan:
    rest get Scan from `/config/scan`
    print `Scan: ` cat Scan
    if Scan is empty
    begin
        wait 1 second
        go to Rescan
    end
    split Scan on newline into Values
    print the elements of Values
    put `[]` into Controllers
    put 0 into S
    while S is less than the elements of Values
    begin
        index Values to S
        put Values into Value
        if left 7 of Value is `RBR-ex-` append Value to Controllers
        add 1 to S
    end
    print Controllers

    put the json count of Controllers into NC
    if NC is 0 go to Rescan
    else if NC is 1
    begin
        put element 0 of Controllers into Controller
        set the content of SSID to Controller
    end
    else
    begin
        set the elements of SSIDChoice to NC
        set style `display` of SSID to `none`
        set style `display` of SSIDs to `block`
        attach TempDiv to `cf-text3`
        set  style `display` of TempDiv to `block`
        rest get SSIDTemplate from `/config/webson/ssidTemplate.json?v=` cat now
        put 0 into N
        while N is less than NC
        begin
            index SSIDChoice to N
            put SSIDTemplate into Webson
            replace `/ID/` with N in Webson
            render Webson in SSIDs
            attach SSIDChoice to `cf-ssid-` cat N
            set the content of SSIDChoice to element N of Controllers
            if element N of Controllers is CurrentSSID
            begin
                put CurrentSSID into Controller
                set style `background` of SSIDChoice to `lightgreen`
            end
            add 1 to N
        end
        on click SSIDChoice
        begin
            put the index of SSIDChoice into S
            put the content of SSIDChoice into Controller
            put 0 into N
            while N is less than NC
            begin
                index SSIDChoice to N
                if N is S set style `background` of SSIDChoice to `lightgreen`
                else set style `background` of SSIDChoice to `pink`
                add 1 to N
            end
        end
    end

    on click LeftButton
    begin
        put `Previous` into Result
        exit
    end
    on click RightButton
    begin
        put property `extenders` of Map into Extenders
        if Extenders is empty put `[{},{},{},{}]` into Extenders
        get EX from storage as `extender`
        put element EX of Extenders into Extender
        set property `ssid` of Extender to Controller
        set element EX of Extenders to Extender
print Extenders
        set property `extenders` of Map to Extenders
        put `Next` into Result
        exit
    end

    stop
