!   Get the controller AP password

	script Controller-Password

    import variable Map and variable Result

    div LeftButton
    div RightButton
    input PasswordInput
    variable SSID
    variable Password
    variable Response
    variable Map2
    variable Count
    variable Data

    attach PasswordInput to `cf-password`
    attach LeftButton to `left-button`
    attach RightButton to `right-button`

    put property `ssid` of Map into SSID
    put property `password` of Map into Password
    if Password is empty get Password from storage as SSID cat `-password`

    set the content of PasswordInput to Password

    set style `display` of LeftButton to `block`
    on click LeftButton
    begin
        set style `display` of LeftButton to `none`
        set style `display` of RightButton to `none`
        put `back` into Result
        exit
    end
    on click RightButton
    begin
        put the content of PasswordInput into Password
        set property `password` of Map to Password
        set the content of PasswordInput to `Connecting... ` cat Count
        set style `display` of LeftButton to `none`
        set style `display` of RightButton to `none`
        go to Connect
    end

    stop

!   Connect to the selected controller
Connect:
    rest get Response from `/config/connect?ssid=` cat SSID cat `&password=` cat Password
    if Response is not `connected`
    begin
        wait 1 second
        go to Connect
    end
!    print `Connected to ` cat SSID

!   Get the system map for the selected controller
    set the content of PasswordInput to `Getting the system map...`
GetMap:
    rest get Map2 from `/config/request?req=http://172.24.1.1/resources/php/rest.php/map`
    if Map2 is empty
    begin
        wait 1 second
        go to GetMap
    end
    put 0 into Count
    put Password into storage as SSID cat `-password`
    put Map2 into Map
    set property `ssid` of Map to SSID
    set property `password` of Map to Password
    put `url=http://172.24.1.1/resources/php/rest.php/map&data=` cat Map into Data
    rest post Data to `/config/post`
    put `system-name` into Result
    exit
