!	Room By Room 
!	This is the main program for the RBR Heating user interface. It maintains the screen,
!	populating it with information loaded from the website, and it calls other modules as they are needed.

	script RBR

    div Body
    div MainPanel
    div Mask
    div BannerMask
    div TitleMask
    div SystemTitlePanel
    div SystemNamePanel
    div OuterPanel
    div HelpPanel
    div StatisticsPanel
    div RoomNamePanel
    div RoomTempPanel
    div RoomToolsButton
    div RoomStatusButton
    div ModePanel
    div ModeHolder
    div TargetTemp
    div BoostName
    div DialogPanel
    div DialogTitle
    div DialogText
    div ModeText
    div ModeInfo
    div Alive
    div Banner
    div Snowflake
    div Date
    div VersionNumber
    div StatusFlag
    div SystemStatusPanel
    input Input
    img StatisticsIcon
    img CalendarIcon
    img Hamburger
    img ModeIcon
    img Hourglass
    button ProfilesButton
    button SystemManagerButton
    button ConfigButton
    button AddButton
    button GetSystemNameButton
    button GetRequestButton
    button CopyMapButton
    button ModeButton
    button HaltButton
    button OKButton
    button CancelButton
    button BackupButton
    button RestoreButton
    button WelcomeButton
    button HelpButton
    button DialogButton1
    button DialogButton2
    module ProfileModule
    module Storyteller
    module DebugModule
    topic ServerTopic
    topic MyTopic
    variable Mobile
    variable System
    variable Server
    variable Revisit
    variable Prompt
    variable Webson
    variable HelpScript
    variable ModeScript
    variable Script
    variable MyID
    variable MAC
    variable Password
    variable SystemName
    variable SystemStatus
    variable CurrentProfile
    variable Profiles
    variable Profile
    variable RequestIP
    variable Map
    variable Config
    variable Rooms
    variable RoomSpec
    variable RoomCount
    variable RoomIndex
    variable ClickIndex
    variable Redraw
    variable Refresh
    variable Sensor
    variable SensorIP
    variable Relays
    variable RelayState
    variable RelayType
    variable Temperature
    variable StaticData
    variable RoomData
    variable RoomName
    variable RoomStatus
    variable Timestamp
    variable IsAlive
    variable Battery
    variable Color
    variable Events
    variable Event
    variable Event2
    variable Blocked
    variable Mode
    variable Flag
    variable Error
    variable SID
    variable TID
    variable Result
    variable Request
    variable RequestData
    variable Confirm
    variable Finish
    variable ReceivedMessage
    variable Name
    variable DateFormat
    variable BannerClicks
    variable BannerClickGap
    variable MapErrorCount
    variable RestErrorCount
    variable Debugging
    variable TVal
    variable Item
    variable Elapsed
    variable Boost
    variable Advance
    variable Now
    variable UntilTime
    variable UntilTemp
    variable Waiting
    variable Flashing
    variable Flashes
    variable Value
    variable Count
    variable NGood
    variable NBad
    variable Keys
    variable Key
    variable FH
    variable FM
    variable NR
    variable E
    variable F
    variable H
    variable M
    variable N
    variable R
    variable T

    ! Webson specs
    variable MainScreenWebson
    variable MainMenuWebson
    variable RoomWebson
    variable RoomToolsMenuWebson
    variable SysParamsWebson
    variable GetNameWebson
    variable GetRequestWebson
    variable TimedWebson
    variable BoostWebson
    variable OnWebson
    variable OffWebson
    variable NoneWebson
    variable DialogWebson

!    debug step

!   Set up MQTT
    put `RBR-` cat random 999999 into MyID
    print `MyID = ` cat MyID
    
    init ServerTopic
        name `38:54:39:34:62:d7/request`
        qos 1

    init MyTopic
        name MyID
        qos 1

    mqtt
        token `kgDkpyDUubtAchcj9ts85QPJOwE1C2MKjNuoeb4nisd23pM33uTb5m7AGuMNhJMP`
        id MyID
        broker `mqtt.flespi.io`
        port 443
        subscribe MyTopic

    on mqtt connect
    begin
        print `Connected`
        go to Connected
    end
    
    ! Handle incoming messages
    on mqtt message
    begin
        put the mqtt message into ReceivedMessage
    end
    stop

Connected:
!	The following allows some debugging when the UI is running on a device with a keyboard
!	and a console.
	on key
    begin
    	if the key is `m` print MAC
    	else if the key is `p` print Password
    	else if the key is `M` print Map
    	else if the key is `D`
        begin
        	set Debugging
            debug step
        end
    	else if the key is `d`
        begin
        	clear Debugging
            debug stop
        end
    end
    
    clear Debugging
    put `first` into Prompt

	! Refresh the UI every 5 seconds
	fork to Main
    wait 3 seconds
!    go to Start             ! TODO: remove this
    every 20 seconds
    begin
    	if not Blocked go to Start
    	wait 1
    	if not Blocked go to Start
    	wait 1
    	if not Blocked go to Start
    	wait 1
    	if not Blocked go to Start
    	wait 1
    	if not Blocked go to Start
    end
    stop

Main:
    put 0 into MapErrorCount
	set Blocked
    set Redraw
    put empty into ModeScript

    set the title to `RBR`

    clear Mobile
    if mobile
    begin
!    	print `Mobile browser detected`
        if portrait
        begin
!        	print `In portrait mode`
        	set Mobile
        end
    end
!    else print `PC browser detected`

    get StaticData from storage as `StaticData`
    if StaticData is empty put `{}` into StaticData
!    print StaticData

    ! Get the URL of the REST server
!  	json parse url the location as Args
!    put property `url` of Args into Server
!    put the position of the last `/` in Server into N
!    add 1 to N
!    put left N of Server into Server
!    put Server cat `resources/php/rest.php` into Server
!    put Server into storage as `server`
    put `/` into Server

	create Body
    if Mobile
    begin
    	set style `width` of Body to `100%`
    end
    else
    begin
        put the height of the window into H
        multiply H by 9 giving N
        divide N by 16
    	set style `width` of Body to N cat `px`
        set style `margin` of Body to `0 auto`
        set style `border` of Body to `1px solid lightgray`
    end
    set style `height` of Body to `calc(100vh - 1em)`
!    put empty into storage as `MAC`

!	First get EasyCoder scripts and the various layout scripts
    rest get HelpScript from `/resources/ecs/storyteller.ecs?v=` cat now
    	or go to AbandonShip

    put empty into MainMenuWebson
    put empty into RoomWebson
    put empty into RoomToolsMenuWebson
    put empty into GetNameWebson
    put empty into GetRequestWebson
    put empty into TimedWebson
    put empty into OnWebson
    put empty into OffWebson
    put empty into NoneWebson
    put empty into DialogWebson

!	Render the main screen layout
    rest get MainScreenWebson from `/resources/webson/rbr.json?v=` cat now
    	or go to AbandonShip
	render MainScreenWebson in Body
    attach Hourglass to `hourglass`
    attach Mask to `top-level-mask`
    attach BannerMask to `banner-mask`
    attach TitleMask to `title-mask`
    attach Banner to `rbr-banner`
    attach SystemTitlePanel to `system-title`
    attach SystemNamePanel to `system-name`
    attach MainPanel to `mainpanel`
    attach OuterPanel to `outerpanel`
    attach HelpPanel to `helppanel`
    attach StatisticsPanel to `statisticspanel`
    attach Alive to `alive`
    attach ModePanel to `mode-panel`
    attach StatisticsIcon to `statistics-icon`
    attach CalendarIcon to `calendar-icon`
    attach Date to `calendar-date`
    attach Hamburger to `hamburger-icon`
    attach ProfilesButton to `profile-button`

!	Start the debug module
    if DebugModule is not running
    begin
        rest get Script from `/resources/ecs/debug.ecs?v=` cat now
        run Script with Mobile and Body and Map and MAC and Password as DebugModule
    	put 0 into BannerClicks
    end
    on click Banner
    begin
    	add 1 to BannerClicks
    	if BannerClicks is 1 add 1000 to the millisecond giving BannerClickGap
        else if BannerClicks is 2
        begin
        	if the millisecond is less than BannerClickGap
            begin
            	send `Run` to DebugModule
    			put 0 into BannerClicks
            end
            else
            begin
            	put 1 into BannerClicks
                add 1000 to the millisecond giving BannerClickGap
            end
        end
    end

!	Set up the statistics and hamburger icons
    on click StatisticsIcon
    begin
    	if Storyteller is running
        begin
        	close Storyteller
            set style `display` of OuterPanel to `block`
            set style `display` of HelpPanel to `none`
            clear MainPanel
            clear Blocked
            set Redraw
        end
        else if not Blocked go to Statistics
    end
    on click Hamburger
    begin
    	if Storyteller is running
        begin
        	close Storyteller
            set style `display` of OuterPanel to `block`
            set style `display` of HelpPanel to `none`
            clear MainPanel
            clear Blocked
            set Redraw
        end
        else if not Blocked go to ShowMenu
    end

    get Revisit from storage as `revisit`
    if Revisit is empty
    begin
    	put `yes` into storage as `revisit`
        put `home` into SID
        put `Welcome` into TID
      	go to RunHelp
    end
    clear Blocked

    !	Make sure we have a MAC address; if not, go ask for one
    clear Flag
    get MAC from storage as `MAC`
    if MAC is empty go to SystemManager ! TODO: Ask for the MAC?
    print `MAC: ` cat MAC

    !	Make sure we have the password; if not, ask for it
    get Password from storage as `password`
    print `Password: ` cat Password
    if Password is empty go to GetLogin

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Main start point
Start:
    gosub to Unmask
	put 0 into RestErrorCount
    send `Count` to DebugModule

	if not Redraw go to GetMap
    clear MainPanel
    put 0 into RoomCount
    clear Error
!    put `System healthy` into SystemStatus

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Get the map
GetMap:
	print `Get the map`
	set Blocked
    send to ServerTopic
        sender MyTopic
        action Prompt

    while ReceivedMessage is empty
    begin
        wait 10 ticks
    end
    put ReceivedMessage into Map
    print `Got the map`
    if Map is empty
    begin
        if Prompt is `first`
        begin
    	    print `Map not available; using local data`
    	    get Map from storage as `map`
       	    continue
        end
        else stop   ! No map, so no action needed
    end
    put `refresh` into Prompt

    if Map is empty
    begin
    	print `Map is empty`
    	stop
    end
    put Map into storage as `map`
    if ReceivedMessage is not empty
    begin
        print `Map has changed`
        set Redraw
    end

    put property `name` of Map into SystemName
    replace `%20` with ` ` in SystemName

    if SystemName is empty
    begin
        get SystemName from storage as `SystemName`
        if SystemName is empty go to GetSystemName
    end
    
    if property `calendar` of Map is `on` set style `display` of CalendarIcon to `block`
    else set style `display` of CalendarIcon to `none`
    set the content of Date to the day number

	! Show the system
    set the content of SystemNamePanel to SystemName
    on click ProfilesButton go to DoProfiles

	! Get the current sensor values

!	rest get Sensors from Server cat `/sensors/` cat MAC cat `?v=` cat now
!    or go to HandleRestError
!    print `Sensors: ` cat Sensors

	attach VersionNumber to `systemversion`
    if not Mobile set style `color` of VersionNumber to `black`
!    set the content of VersionNumber to `v:`
    attach StatusFlag to `statusflag`

!    if property `system-status` of Sensors is `good`
!    begin
!        set style `background` of StatusFlag to `lightgreen`
!        set style `border` of StatusFlag to `1px solid green`
!    end
!    else if property `system-status` of Sensors is `suspect`
!    begin
!        set style `background` of StatusFlag to `gold`
!        set style `border` of StatusFlag to `1px brown`
!    end
!    else
!    begin
!        set style `background` of StatusFlag to `red`
!        set style `border` of StatusFlag to `1px solid lightred`
!    end
    
    on click StatusFlag go to DoSystemParameters
    
!   print Map
    print `Get the map elements`
    put property `profiles` of Map into Profiles
	put property `profile` of Map into CurrentProfile
    if CurrentProfile is empty put 0 into CurrentProfile
    if Profiles is empty put `{}` into Profile else put element CurrentProfile of Profiles into Profile
    put property `name` of Profile into Name
    set the text of ProfilesButton to `Profile: ` cat Name
    put property `rooms` of Profile into Rooms

    put the json count of Rooms into R
    if R is RoomCount clear Redraw
    else
    begin
    	put R into RoomCount
        set Redraw
    end
    if Redraw
    begin
    	clear MainPanel
    	set Refresh
    end
    wait 2 seconds
    print `Start the redraw`

    set the elements of Mode to RoomCount
    set the elements of ModeHolder to RoomCount
    set the elements of ModeButton to RoomCount
    set the elements of RoomSpec to RoomCount
    set the elements of RoomNamePanel to RoomCount
    set the elements of RoomTempPanel to RoomCount
    set the elements of RoomToolsButton to RoomCount
    set the elements of RoomStatusButton to RoomCount
    set the elements of RoomStatus to RoomCount
    set the elements of RelayState to RoomCount
    set the elements of ModeHolder to RoomCount
    set the elements of ModeButton to RoomCount
    set the elements of ModeIcon to RoomCount
    set the elements of ModeText to RoomCount
    set the elements of Snowflake to RoomCount

    gosub to UpdateRooms

!	Get the system status
	put 0 into NGood
    put 0 into NBad
    put 0 into RoomIndex
    while RoomIndex is less than RoomCount
    begin
    	index RoomStatus to RoomIndex
        if RoomStatus is `good` add 1 to NGood
        else if RoomStatus is `fail` add 1 to NBad
    	add 1 to RoomIndex
    end
    if NGood is RoomCount
    begin
        set style `background` of StatusFlag to `lightgreen`
        set style `border` of StatusFlag to `1px solid green`
    end
    else if NBad is RoomCount
    begin
        set style `background` of StatusFlag to `red`
        set style `border` of StatusFlag to `1px solid lightred`
    end
    else
    begin
        set style `background` of StatusFlag to `gold`
        set style `border` of StatusFlag to `1px brown`
    end
    
    on click RoomTempPanel
    begin
    	put the index of RoomTempPanel into RoomIndex
		put element RoomIndex of Rooms into RoomSpec
        put `` cat property `sensor` of RoomSpec into SensorIP
        if SensorIP is not empty
        begin
            if Battery is greater than 40 put `lightgreen` into Color
            else if Battery is greater than 25 put `orange` into Color
            else put `deeppink` into Color
        	set style `color` of RoomTempPanel to Color
        	set the content of RoomTempPanel to Battery cat `%`
        end
    end
    
!    put property `status` of Sensors into SystemStatus

    put StaticData into storage as `StaticData`

    clear Refresh
    clear Redraw
    clear Blocked

    set style `background` of Alive to `#fee`
    wait 50 ticks
    set style `background` of Alive to `#228`
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update al the rooms
UpdateRooms:
    print `Update rooms`
	! Draw a row for each room
    put 0 into RoomIndex
    while RoomIndex is less than RoomCount
    begin
        index Mode to RoomIndex
        index ModeHolder to RoomIndex
        index ModeButton to RoomIndex
        index ModeIcon to RoomIndex
        index RoomSpec to RoomIndex
        index RelayState to RoomIndex
        index RoomNamePanel to RoomIndex
        index RoomTempPanel to RoomIndex
        index RoomToolsButton to RoomIndex
        index RoomStatusButton to RoomIndex
        index RoomStatus to RoomIndex
        index ModeText to RoomIndex
        index Snowflake to RoomIndex

        put property `room-` cat RoomIndex of StaticData into RoomData
        if RoomData is empty
        begin
        	put `{}` into RoomData
            set property `alive` of RoomData to `true`
        end

        print `Redraw=` cat Redraw
        if Redraw
        begin
            ! Render the Webson spec for a room
            if RoomWebson is empty
    			rest get RoomWebson from `/resources/webson/room.json?v=` cat now
            		or go to HandleRestError
        	put RoomWebson into Webson
        	replace `/ROOM/` with RoomIndex in Webson
        	render Webson in MainPanel
        end

		! Attach to various elements of the display
        attach ModeHolder to `room-` cat RoomIndex cat `-mode-holder` or
        begin
        	print `Can't find room-` cat RoomIndex cat `-mode-holder`
        	stop
        end
        attach RoomNamePanel to `room-name-` cat RoomIndex or
        begin
        	print `Can't find room-name-` cat RoomIndex
        	stop
        end
		attach RoomTempPanel to `room-temp-` cat RoomIndex or
        begin
        	print `Can't find room-temp-` cat RoomIndex
        	stop
        end
        attach RoomToolsButton to `room-tools-` cat RoomIndex or
        begin
        	print `Can't find room-tools-` cat RoomIndex
        	stop
        end
        attach RoomStatusButton to `room-status-` cat RoomIndex or
        begin
        	print `Can't find room-status-` cat RoomIndex
        	stop
        end
        attach Snowflake to `room-snowflake-` cat RoomIndex or
        begin
        	print `Can't find room-snowflake-` cat RoomIndex
        	stop
        end

		! Set the room name
		put element RoomIndex of Rooms into RoomSpec
        put property `name` of RoomSpec into RoomName
        set the content of RoomNamePanel to RoomName
        
        ! Get the mode
        put property `mode` of RoomSpec into Mode
        if Mode is empty put `off` into Mode
        
        ! Show the current temperature and relay state
!        print RoomSpec
        put property `sensor` of RoomSpec into SensorIP
        put property `relayType` of RoomSpec into RelayType
        put property `relay` of RoomSpec into RelayState
        put property `temperature` of RoomSpec into Temperature
    	put property `battery` of RoomSpec into Battery
        put property `timestamp` of RoomSpec into Timestamp
        put property `advance` of RoomSpec into Advance
        put property `status` of RoomSpec into RoomStatus
        if RelayType is `Manual`
        begin
            set the content of RoomTempPanel to `Manual`
            if Mode is `on` set style `background` of RoomTempPanel to `red`
            else if Mode is `boost` set style `background` of RoomTempPanel to `red`
            else set style `background` of RoomTempPanel to `blue`
        end
        else if SensorIP is not empty
        begin
            put the timestamp into Now
            if Timestamp is not empty
            begin
            	take Timestamp from Now giving Elapsed
!      print `Elapsed: ` cat Elapsed
				if Elapsed is greater than 3660
                begin
                	if property `alive` of RoomData is `true`
                    begin
                    	put `The sensor for ` cat RoomName cat ` is not reporting. ` into SystemStatus
!                    	alert `The sensor for ` cat RoomName cat ` has not reported recently. `
!                        	cat `If it doesn't come back by itself, power it down then re-power it.`
                    	set property `alive` of RoomData to `false`
                    end
                end
                else set property `alive` of RoomData to `true`
            end
            set property `room-` cat RoomIndex of StaticData to RoomData
            if property `relays` of RoomSpec is empty put `true` into IsAlive
!            else if property `linked` of RoomSpec is `no` put `true` into IsAlive
            else put property `alive` of RoomData into IsAlive
            put `true` into IsAlive
            if IsAlive is `true`
            begin
            	set style `color` of RoomTempPanel to `white`
                if Temperature is 0 set the content of RoomTempPanel to empty 
                else
                begin
                    put `` cat Temperature modulo 100 into T
                    divide Temperature by 100
                    put `` cat Temperature cat `.` cat left 1 of T into Temperature
                end
                set the content of RoomTempPanel to Temperature cat `&deg;C`
            	if RelayState is not `off` set style `background` of RoomTempPanel to `red`
            	else set style `background` of RoomTempPanel to `blue`
            end
            else
            begin
        		set the content of RoomTempPanel to `--.-&deg;C`
                set style `background` of RoomTempPanel to `blue`
            end
        end

		! Deal with the Protect flag
        set style `display` of Snowflake to `none`
        if SensorIP is not empty
        begin
!			if property `protect` of Sensors is SensorIP
!        	begin
!        		set style `display` of Snowflake to `block`
!            end
        end

		if RoomStatus is `good`
        begin
            set style `background` of RoomStatusButton to `lightgreen`
            set style `border` of RoomStatusButton to `1px solid green`
        end
        else if RoomStatus is `fail`
        begin
            set style `background` of RoomStatusButton to `red`
            set style `border` of RoomStatusButton to `1px solid darkred`
        end
        else
        begin
            set style `background` of RoomStatusButton to `gold`
            set style `border` of RoomStatusButton to `1px solid brown`
        end

        ! Show the mode indicator
        put property `relays` of RoomSpec into Relays
        put the json count of Relays into NR
        if NR is 1
        	if element 0 of Relays is empty put 0 into NR
        if NR is 0
        begin
            clear ModeHolder
            if NoneWebson is empty
            rest get NoneWebson from `/resources/webson/none.json?v=` cat now
            	or go to HandleRestError
            put NoneWebson into Webson
            replace `/ROOM/` with RoomIndex in Webson
            render Webson in ModeHolder
        end
        else
        begin
            clear ModeHolder
        	if Mode is `timed`
            begin
                if TimedWebson is empty
                	rest get TimedWebson from `/resources/webson/timed.json?v=` cat now
                		or go to HandleRestError
                put TimedWebson into Webson
            end
            else if Mode is `boost`
            begin
                if BoostWebson is empty
                	rest get BoostWebson from `/resources/webson/boost.json?v=` cat now
                		or go to HandleRestError
                put BoostWebson into Webson
            end
            else if Mode is `on`
            begin
                if OnWebson is empty
                	rest get OnWebson from `/resources/webson/on.json?v=` cat now
                		or go to HandleRestError
                put OnWebson into Webson
            end
            else if Mode is `off`
            begin
                if OffWebson is empty
                	rest get OffWebson from `/resources/webson/off.json?v=` cat now
                		or go to HandleRestError
                put OffWebson into Webson
            end
            replace `/ROOM/` with RoomIndex in Webson
            render Webson in ModeHolder
        	attach ModeIcon to `room-` cat RoomIndex cat `-mode-icon`
    	    attach ModeText to `room-` cat RoomIndex cat `-mode-text`
	        attach ModeInfo to `room-` cat RoomIndex cat `-mode-info`
        	if Mode is `timed`
            begin
                if RoomStatus is `good`
                begin
                	put `` cat Advance into Advance
                    if Advance is not empty
                    begin
                        if Advance is `-`
                        begin
                	       	set attribute `src` of ModeIcon to `resources/icon/clock.png`
                         	set the content of ModeText to `Timed`
                            set style `font-size` of ModeText to `1.2em`
                        end
                        else
                        begin
                            if Advance is not `C`
                            begin
                        		set attribute `src` of ModeIcon to `resources/icon/advance.png?v=12345`
                                set the content of ModeText to `Advance`
                              	set style `font-size` of ModeText to `1.0em`
                            end
                        end
                    end
                end

                put property `events` of RoomSpec into Events
                divide now by 60 giving N
                put N modulo 24*60 into N
                divide N by 60 giving H
                put N modulo 60 into M
                put 0 into E
                while E is less than the json count of Events
                begin
                    put element E of Events into Event
                    put property `until` of Event into UntilTime
                    put property `temp` of Event into UntilTemp
                    split UntilTime on `:` giving Finish
                    
                    if Advance is not `-`
                    begin
                    	add 1 to E giving F
                        if F is not less than the json count of Events put 0 into F
                        put element F of Events into Event2
                        put property `until` of Event2 into UntilTime
                    	put property `temp` of Event2 into UntilTemp
                    end
                    index Finish to 0
                    put the value of Finish into FH
                    if FH is 0 put 24 into FH
                    if H is less than FH
                    begin
                        set the content of ModeInfo to UntilTemp
                            cat `&deg;C->` cat UntilTime
                        go to HandleClicks
                    end
                    else if H is FH
                    begin
                        index Finish to 1
                        put the value of Finish into FM
                        if M is less than FM
                        begin
                            set the content of ModeInfo to UntilTemp
                                cat `&deg;C->` cat UntilTime
                            go to HandleClicks
                        end
                    end
                    add 1 to E
                end
                put element 0 of Events into Event
                put property `until` of Event into Finish
                put property `temp` of Event into Temperature
                if property `linked` of RoomSpec is `yes`
                	set the content of ModeInfo to Temperature cat `&deg;C->` cat Finish
                else set the content of ModeInfo to `->` cat Finish
            end
            else if Mode is `boost`
            begin
                attach BoostName to `room-` cat RoomIndex cat `-mode-info`
                put now into Now
                put property `boost` of RoomSpec into Boost
                if char 0 of Boost is not `B`
                begin
                    take the timestamp from Boost
                    divide Boost by 60
                    add 1 to Boost
                    if Boost is 1 set the content of BoostName to Boost cat ` min`
                    else set the content of BoostName to Boost cat ` mins`
                end
            end
            else if Mode is `on`
            begin
                attach TargetTemp to `room-` cat RoomIndex cat `-mode-info`
                put empty into TVal
                if property `sensor` of RoomSpec is not empty
                begin
                	if property `linked` of RoomSpec is `yes`
                		put property `target` of RoomSpec cat `&deg;C` into TVal
                end
                set the content of TargetTemp to TVal
            end
            else if Mode is `off`
            begin
            end
		end

	HandleClicks:
		! Handle a click on the row hamburger
        on click RoomToolsButton
        begin
        	if Blocked stop
        	put the index of RoomToolsButton into ClickIndex
            go to RoomTools
        end

	    ! Handle a click on a mode button
        if the json count of Relays is not 0
        begin
            attach ModeButton to `room-` cat RoomIndex cat `-mode-button`
            on click ModeButton
            begin
                if Blocked stop
                set Blocked
                gosub to MaskScreen
                on click ProfilesButton begin end
                put the index of ModeButton into ClickIndex
                if ModeScript is empty
                    rest get ModeScript from `/resources/ecs/mode.ecs?v=` cat now
            			or go to HandleRestError
    			put `{}` into Result
                run ModeScript with MainPanel
                    and ModePanel
                    and ModeButton
                    and Mode
                    and Map
                    and ClickIndex
                    and Result
                go to ProcessResult
            end
        end
        add 1 to RoomIndex
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Reset the MAC address and password
Reset:
	put empty into storage as `MAC`
    put empty into storage as `password`
    clear Blocked
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Deal with the main hamburger menu
ShowMenu:
	set Blocked
    gosub to MaskMainPanel
    wait 20 ticks
	clear MainPanel
    if MainMenuWebson is empty
    	rest get MainMenuWebson from `/resources/webson/mainmenu.json?v=` cat now
            or go to HandleRestError
    render MainMenuWebson in MainPanel
    attach SystemManagerButton to `button-systemmanager`
    attach AddButton to `button-add`
    attach BackupButton to `button-backup`
    attach RestoreButton to `button-restore`
    attach WelcomeButton to `button-welcome`
    attach HelpButton to `button-help`
    attach CancelButton to `button-cancel`
    
    on click SystemManagerButton go to SystemManager

    on click AddButton
    begin
        rest get RoomSpec from `/resources/json/newroomspec.json?v=` cat now
            or go to HandleRestError
        append RoomSpec to Rooms
    	clear MainPanel
        clear Blocked
        set Redraw
        put `{}` into RequestData
        set property `action` of RequestData to `addroom`
        set property `spec` of RequestData to RoomSpec
        gosub to PostAndConfirm
    	stop
    end

    on click BackupButton go to Backup

    on click RestoreButton go to Restore

    on click WelcomeButton
    begin
        put `home` into SID
        put `Welcome` into TID
      	go to RunHelp
    end

    on click HelpButton
    begin
        put `home` into SID
        put `content` into TID
      	go to RunHelp
    end

    on click CancelButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
        stop
    end

	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	The system manager
SystemManager:
!	set Blocked
!    clear MainPanel
!    rest get Script from `/resources/ecs/systemman.ecs?v=` cat now
!    	or go to HandleRestError
!    run Script with MainPanel and MAC and Password and Map and Flag and Result
!    if Result is `OK`
!    begin
!        put MAC into storage as `MAC`
!        put Password into storage as `password`
!    	rest get Map from Server cat `/map/` cat MAC cat `?v=` cat now
!        	or begin
!        		print `Server not responding`
!                put empty into Map
!            	continue
!        	end
!        if Map is empty
!        begin
!            set Redraw
!            clear Blocked
!            stop
!        end
!        put Map into storage as `map`
!        put property `name` of Map into SystemName
!        if SystemName is empty put `New` into SystemName
!        put `{}` into System
!        set property `mac` of System to MAC
!        set property `password` of System to Password
!    end
!    else if Result is `Help`
!    begin
!        put `home` into SID
!        put `SystemManager` into TID
!      	go to RunHelp
!    end
!    set Redraw
!    clear Blocked
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	System parameters
DoSystemParameters:
	set Blocked
    clear MainPanel
    rest get SysParamsWebson from `/resources/webson/sysparams.json?v=` cat now
        or go to HandleRestError
    render SysParamsWebson in MainPanel
    
    attach ConfigButton to `button-config`
    set style `display` of ConfigButton to `none`	! At least for now
	get Value from storage as `email`
    if Value is not empty
    begin
    	get Value from storage as `userpassword`
    	if Value is not empty
        begin
	    	on click ConfigButton go to Configuration
            go to GSP2
        end
    end
    set style `display` of ConfigButton to `none`
GSP2:
    attach GetSystemNameButton to `button-getname`
    attach GetRequestButton to `button-getrequest`
    attach CopyMapButton to `button-copymap`
    attach HaltButton to `button-halt`
    attach CancelButton to `button-cancel`
    attach HelpButton to `button-help`
    attach SystemStatusPanel to `rbr-status`
    
    set the content of SystemStatusPanel to SystemStatus

    on click GetSystemNameButton
    begin
    	set Flag
        go to GetSystemName
    end

    on click GetRequestButton
    begin
    	set Flag
        go to GetRequestRelay
    end

    on click CopyMapButton
    begin
    	set Flag
        go to CopySystemMap
    end

    on click HaltButton
    begin
        attach DialogPanel to `dialog-panel`
        rest get Webson from `/resources/webson/dialog-confirm.json?v=` cat now
        render Webson in DialogPanel
        attach DialogTitle to `dialog-title`
        attach DialogText to `dialog-text`
        attach DialogButton1 to `dialog-button1`
        attach DialogButton2 to `dialog-button2`
        set the content of DialogTitle to `Confirm halt`
        set the content of DialogText to `This will halt the system controller. It will need to be restarted manually.`
        set the content of DialogButton1 to `OK`
        set the content of DialogButton2 to `Cancel`
        on click DialogButton1
        begin
            clear DialogPanel
            set style `color` of HaltButton to `lightgray`
            put `{}` into RequestData
            set property `action` of RequestData to `halt`
            fork to PostRequest
            set Redraw
            clear Blocked
        end
        on click DialogButton2 clear DialogPanel
    end

    on click HelpButton
    begin
        put `home` into SID
        put `SysParamsMenu` into TID
      	go to RunHelp
    end

    on click CancelButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Configuration
Configuration:
	set Blocked
    rest get Script from `/resources/ecs/config.ecs?v=` cat now
    	or go to HandleRestError
    run Script with MAC and Password and Map and Result
    go to ProcessResult

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Ask the user for the system request relay
GetRequestRelay:
	set Blocked
    clear MainPanel
    if GetRequestWebson is empty
        rest get GetRequestWebson from `/resources/webson/getrequest.json?v=` cat now
            or go to HandleRestError
    render GetRequestWebson in MainPanel
    attach Input to `getrequest-input`
    attach OKButton to `getrequest-ok`
    attach CancelButton to `getrequest-cancel`
    attach HelpButton to `getrequest-help`
    put property `request` of Map into RequestIP
    if RequestIP is `undefined` put empty into RequestIP
    if Flag set the content of Input to RequestIP
    else set style `display` of CancelButton to `none`
    on click OKButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
        put `{}` into RequestData
        set property `action` of RequestData to `request`
        set property `request` of RequestData to the content of Input
        go to PostRequest
    end
    on click CancelButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
        stop
    end
    on click HelpButton
    begin
        clear MainPanel
    	set style `display` of OuterPanel to `none`
      	set style `display` of HelpPanel to `block`
    	put `home` into SID
        put `Request` into TID
        go to RunHelp
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Copy the system map and config file from another system
CopySystemMap:
	set Blocked
    clear MainPanel
    rest get Webson from `/resources/webson/copysystemmap.json?v=` cat now
        or go to HandleRestError
    render Webson in MainPanel
    attach Input to `copymap-input`
    attach OKButton to `copymap-ok`
    attach CancelButton to `copymap-cancel`
    attach HelpButton to `copymap-help`
    on click OKButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
        if the content of Input is empty stop
    	rest get Config from `/resources/php/rest.php/config/` cat the content of Input or go to HandleRestError
        rest post Config to `/resources/php/rest.php/config/` cat MAC cat `/` cat Password or go to HandleRestError
    	rest get Map from `/resources/php/rest.php/map/` cat the content of Input or go to HandleRestError
        put `{}` into RequestData
        set property `action` of RequestData to `map`
        set property `map` of RequestData to Map
        go to PostRequest
    end
    on click CancelButton
    begin
    	clear MainPanel
        clear Blocked
        set Redraw
        stop
    end
    on click HelpButton
    begin
        clear MainPanel
    	set style `display` of OuterPanel to `none`
      	set style `display` of HelpPanel to `block`
    	put `home` into SID
        put `Request` into TID
        go to RunHelp
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Copy the current room set to the map
CopyRoomsToMap:
    put property `profiles` of Map into Profiles
    put element CurrentProfile of Profiles into Profile
    set property `rooms` of Profile to Rooms
    set element CurrentProfile of Profiles to Profile
    set property `profiles` of Map to Profiles
	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Deal with profiles
DoProfiles:
	if ProfileModule is running stop
	set Blocked
    gosub to MaskMainPanel
    put `{}` into Result
    rest get Script from `/resources/ecs/profiles.ecs?v=` cat now
    run Script with MainPanel and Map and Result as ProfileModule
    go to ProcessResult

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	The room tools
RoomTools:
	set Blocked
    gosub to MaskMainPanel
    wait 20 ticks ! to allow things to complete before running the editor!
	clear MainPanel
    if RoomToolsMenuWebson is empty
    	rest get RoomToolsMenuWebson from `/resources/webson/roomtoolsmenu.json?v=` cat now
            or go to HandleRestError
    render RoomToolsMenuWebson in MainPanel
    put element ClickIndex of Rooms into RoomSpec
    put property `name` of RoomSpec into Name
!    put property Name of Sensors into RoomSpec
    put `{}` into Result
    rest get Script from `/resources/ecs/roomtools.ecs?v=` cat now
    run Script with MainPanel and Map and RoomSpec and ClickIndex and Result
    go to ProcessResult


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Backup the system
Backup:
	set Blocked
    attach DialogPanel to `dialog-panel`
    if DialogWebson is empty
    	rest get DialogWebson from `/resources/webson/dialog-confirm.json?v=` cat now
            or go to HandleRestError
    render DialogWebson in DialogPanel
    attach DialogTitle to `dialog-title`
    attach DialogText to `dialog-text`
    attach DialogButton1 to `dialog-button1`
    attach DialogButton2 to `dialog-button2`
    set the content of DialogTitle to `System Backup`
    set the content of DialogText to `Tap "Save" to save your current `
    	cat `system configuration. Any previous backup will be over-written.`
    set the content of DialogButton1 to `Save`
    set the content of DialogButton2 to `Cancel`
    on click DialogButton1
    begin
    	clear DialogPanel
        put `{}` into RequestData
        set property `action` of RequestData to `backup`
        fork to PostRequest
        set Redraw
        clear Blocked
    end
    on click DialogButton2
    begin
    	clear DialogPanel
        set Redraw
        clear Blocked
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Restore from backup
Restore:
	set Blocked
    attach DialogPanel to `dialog-panel`
    if DialogWebson is empty
    	rest get DialogWebson from `/resources/webson/dialog-confirm.json?v=` cat now
            or go to HandleRestError
    render DialogWebson in DialogPanel
    attach DialogTitle to `dialog-title`
    attach DialogText to `dialog-text`
    attach DialogButton1 to `dialog-button1`
    attach DialogButton2 to `dialog-button2`
    set the content of DialogTitle to `System Restore`
    set the content of DialogText to `Tap "Restore" to restore your system configuration `
    	cat `from the saved backup (if any).`
    set the content of DialogButton1 to `Restore`
    set the content of DialogButton2 to `Cancel`
    on click DialogButton1
    begin
    	clear DialogPanel
        put `{}` into RequestData
        set property `action` of RequestData to `restore`
        fork to PostAndConfirm
        set Redraw
        clear Blocked
    end
    on click DialogButton2
    begin
    	clear DialogPanel
        set Redraw
        clear Blocked
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Show a help page
RunHelp:
	set Blocked
    gosub to Unmask
    set style `display` of OuterPanel to `none`
    set style `display` of HelpPanel to `block`
    run HelpScript with HelpPanel and SID and TID as Storyteller
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Run the statistics module
Statistics:
	set Blocked
    set style `display` of OuterPanel to `none`
    set style `display` of StatisticsPanel to `block`
    rest get Script from `/resources/ecs/statistics.ecs?v=` cat now
        or go to HandleRestError
    run Script with StatisticsPanel and Map
    print `Statistics finished`
    set style `display` of OuterPanel to `block`
    set style `display` of StatisticsPanel to `none`
    clear MainPanel
    clear Blocked
    set Redraw
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Mask the screen
MaskScreen:
	set style `display` of Mask to `block`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Mask the main panel
MaskMainPanel:
	set style `display` of BannerMask to `block`
    set style `display` of TitleMask to `block`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Unmask everything
Unmask:
	set style `display` of Mask to `none`
	set style `display` of BannerMask to `none`
    set style `display` of TitleMask to `none`
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Process the result of running another module
ProcessResult:
!print Result
	put property `request` of Result into Request
    put property `data` of Result into RequestData
    if Request is `Update`
    begin
    	gosub to PostAndConfirm
    	clear MainPanel
        clear Blocked
        set Redraw
        stop
    end
    if Request is `Help`
    begin
        split RequestData on ` ` giving Result
        index Result to 0
        put Result into SID
        index Result to 1
        put Result into TID
        go to RunHelp
    end
    if Request is `Redraw` set Redraw
    clear Blocked
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Post data and wait for the server to confirm
PostAndConfirm:
    fork to PostRequest
    fork to FlashHourglass
    ! Wait for a response from the server
        put 10 into Count
        while Count is greater than 0
        begin
            wait 5 seconds
            rest get Confirm from Server cat `/confirm/` cat MAC cat `?v=` cat now
            or goto HandleRestError
            if Confirm is `Y` go to PAC2
            take 1 from Count
        end
    	put `System did not respond to a request` into SystemStatus
        fork to PAC4
        return
PAC2:
	clear Waiting
    put 0 into Count
    while Count is less than 10
    begin
    	if not Flashing go to PAC3
        wait 10 ticks
    	add 1 to Count
    end
PAC3:
    return

PAC4:
    wait 10
!    put `System healthy` into SystemStatus
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!	Flash the hourglass while waiting
FlashHourglass:
    set Waiting
    set Flashing
    put 0 into Flashes
	while Waiting
    begin
    	set style `display` of Hourglass to `block`
    	wait 50 ticks
        set style `display` of Hourglass to `none`
        wait 50 ticks
        add 1 to Flashes
        if Flashes is greater than 99 go to FH2
    end
FH2:
    clear Flashing
    clear Waiting
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Post a request to the server
PostRequest:
! print `Request: ` cat RequestData cat newline cat Server cat `/request/` cat MAC cat `/` cat Password
    rest post RequestData to Server cat `/request/` cat MAC cat `/` cat Password
   	or go to HandlePostError
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Handle a POST error
HandlePostError:
	print `POST error`
    add 1 to MapErrorCount
    if MapErrorCount is greater than 5
    begin
        alert `A series of network errors have occurred.`
        	cat newline cat `Some functionality may not be available.`
        put 0 into MapErrorCount
    end
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Handle a REST error
HandleRestError:
    print `A REST error occurred`
	add 1 to RestErrorCount
    if RestErrorCount is less than 10
    begin
        clear MainPanel
    	clear DialogPanel
        set Redraw
        clear Blocked
        set Redraw
    	stop
    end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 	Warn the user and abandon this run
AbandonShip:
	alert `An error has occurred while communicating with the web server.`
    	cat newline cat `Please refresh this browser page to restart.`
    exit