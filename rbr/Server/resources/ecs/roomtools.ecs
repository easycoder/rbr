!	Room Tools

	script RoomTools
    
    import div MainPanel
    	and variable Rooms
        and variable ClickIndex
        and variable Result

	div RoomName
    div DialogPanel
    div DialogTitle
    div DialogText
    button RoomUpButton
    button RoomDownButton
    button RoomEditButton
    button RoomDeleteButton
    button HelpButton
    button CancelButton
    button DialogButton1
    button DialogButton2
    variable RoomSpec
    variable ThisRoom
    variable ThatRoom
    variable NewRooms
    variable Webson
    variable Script
    variable Changed
    variable N

!    debug step
    
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! The room tools menu
Editor:
    attach RoomName to `title-room-name`
    attach RoomUpButton to `button-up`
    attach RoomDownButton to `button-down`
    attach RoomEditButton to `button-edit`
    attach RoomDeleteButton to `button-delete`
    attach HelpButton to `button-help`
    attach CancelButton to `button-cancel`

    put element ClickIndex of Rooms into RoomSpec
    set the content of RoomName to property `name` of RoomSpec
    
    if ClickIndex is 0 set style `display` of RoomUpButton to `none`
    put the json count of Rooms into N
    take 1 from N
    if ClickIndex is N set style `display` of RoomDownButton to `none`
    
    clear Changed
    
    on click RoomUpButton
    begin
    	put element ClickIndex of Rooms into ThisRoom
        take 1 from ClickIndex
        put element ClickIndex of Rooms into ThatRoom
        set element ClickIndex of Rooms to ThisRoom
        add 1 to ClickIndex
        set element ClickIndex of Rooms to ThatRoom
        go to ExitWithChanges
    end
    
    on click RoomDownButton
    begin
    	put element ClickIndex of Rooms into ThisRoom
        add 1 to ClickIndex
        put element ClickIndex of Rooms into ThatRoom
        set element ClickIndex of Rooms to ThisRoom
        take 1 from ClickIndex
        set element ClickIndex of Rooms to ThatRoom
        go to ExitWithChanges
    end
    
    on click RoomEditButton
    begin
        clear MainPanel
    	rest get Webson from `/resources/webson/roomeditmenu.json?v=` cat now
        render Webson in MainPanel
        put empty into Result
    	rest get Script from `/resources/ecs/roomedit.ecs?v=` cat now
        run Script with MainPanel and Rooms and ClickIndex and Result
        exit
    end
    
    on click RoomDeleteButton
    begin
        attach DialogPanel to `dialog-panel`
    	rest get Webson from `/resources/webson/dialog-confirm.json?v=` cat now
        render Webson in DialogPanel
        attach DialogTitle to `dialog-title`
        attach DialogText to `dialog-text`
        attach DialogButton1 to `dialog-button1`
        attach DialogButton2 to `dialog-button2`
        set the content of DialogTitle to `Delete '` cat the content of RoomName cat `'`
        set the content of DialogText to `Are you sure you want to delete this room?`
        set the content of DialogButton1 to `Yes`
        set the content of DialogButton2 to `No`
        on click DialogButton1
        begin
            clear DialogPanel
            put `[]` into NewRooms
            put 0 into N
            while N is less than ClickIndex
            begin
                append element N of Rooms to NewRooms
                add 1 to N
            end
            add 1 to N
            while N is less than the json count of Rooms
            begin
                append element N of Rooms to NewRooms
                add 1 to N
            end
            put NewRooms into Rooms
            go to ExitWithChanges
        end
        on click DialogButton2
        begin
        	clear DialogPanel
        	go to ExitWithoutChanges
        end
    end
    
    on click HelpButton
    begin
      	put `Help home RoomTools` into Result
        exit
    end
    
    on click CancelButton
    begin
    	if Changed
        begin
        	set element ClickIndex of Rooms to RoomSpec
            go to ExitWithChanges
        end
        go to ExitWithoutChanges
    end

    stop

ExitWithoutChanges:
    put `OK` into Result
    exit


ExitWithChanges:
    put `Changed` into Result
    exit