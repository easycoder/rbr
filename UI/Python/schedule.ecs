!   schedule.ecs - the schedule editor

    script Schedule

    use graphics
    use plugin RBR_UI from /home/graham/dev/rbr/rbr_ui.py

    import variable RoomName
        and rbrwin Window
        and list Periods
        and dictionary Request

!    debug compile
    
    panel Container
    panel Editor
    layout Layout
    layout Panel
    layout EditLayout
    label Label
    label TimeLabel
    label TempLabel
    pushbutton HourUpButton
    pushbutton HourDownButton
    pushbutton MinuteUpButton
    pushbutton MinuteDownButton
    pushbutton TempUpButton
    pushbutton TempDownButton
    pushbutton PeriodButton
    pushbutton DeletePeriodButton
    pushbutton CloseEditorButton
    pushbutton AddButton
    pushbutton SaveButton
    pushbutton CancelButton
    dictionary CurrentPeriod
    dictionary Period
    dictionary PeriodP
    dictionary PeriodQ
    list Working
    variable ListStyle
    variable IconStyle
    variable NPeriods
    variable SelectedPeriod
    variable Time
    variable Temp
    variable Changed
    variable Reordered
    variable P
    variable Q
    variable T
    variable T1
    variable T2

!    debug step

    copy Periods to Working
    set SelectedPeriod to -1
    clear Changed
    clear Reordered
    gosub to ConvertUntilToFrom
    create Container
    set second view of Window to Container
    set the style of Container to `background: #ffc;border: 1px solid gray;`
    gosub to DrawPanel
    release parent
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
HourUp:
    raise time Time 1 hour
    go to TimeChanged

HourDown:
    lower time Time 1 hour
    go to TimeChanged

MinuteUp:
    raise time Time 5 minutes
    go to TimeChanged

MinuteDown:
    lower time Time 5 minutes

TimeChanged:
    set the text of TimeLabel to Time
    set entry `from` of CurrentPeriod to Time
    set item SelectedPeriod of Working to CurrentPeriod
    set Changed
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
TempUp:
    put item SelectedPeriod of Working into CurrentPeriod
    raise temp Temp 5 tenths
    set the text of TempLabel to Temp cat `ºC`
    set entry  `temp` of CurrentPeriod to Temp
    set item SelectedPeriod of Working to CurrentPeriod
    set Changed
    stop

TempDown:
    lower temp Temp 5 tenths
    set the text of TempLabel to Temp cat `ºC`
    set entry  `temp` of CurrentPeriod to Temp
    set item SelectedPeriod of Working to CurrentPeriod
    log CurrentPeriod
    set Changed
    log `stop`
    stop

DeletePeriod:
    log `Delete this period`
    put SelectedPeriod into P
    put P into Q
    increment Q
    while Q is less than NPeriods
    begin
        set item P of Working to item Q of Working
        increment P
        increment Q
    end
    delete item SelectedPeriod of Working
    decrement NPeriods
    gosub to RedrawPanel
    set Changed
    stop

CloseEditor:
    log `Close the editor`
    set SelectedPeriod to -1
    gosub to RedrawPanel
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
AddPeriod:
    log `Add period`
    put json `{}` into Period
    set entry `temp` of Period to `10.0`
    set entry `from` of Period to `0:00`
    append Period to Working
    increment NPeriods
    gosub to SortPeriods
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SaveAndExit:
    log `Save and exit`
	if Changed
    begin
        set item SelectedPeriod of Working to CurrentPeriod
        gosub to SortPeriods
        gosub to ConvertFromToUntil
       	set entry `action` of Request to `Periods`
        set entry `room name` of Request to RoomName
        set entry `Periods` of Request to Working
    end
    else reset Request
AllDone:
    show main view of Window
    log `All done`
    send `OK` to parent
    wait 10 ticks
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Cancel:
    reset Request
    go to AllDone

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Sort the periods into ascending time order
SortPeriods:
    clear Reordered
    put 0 into P
    put 1 into Q
    while Q is less than NPeriods
    begin
        put item P of Working into PeriodP
        put entry `from` of PeriodP into T1
        put item Q of Working into PeriodQ
        put entry `from` of PeriodQ into T2
        if time T1 is after T2
        begin
            set item P of Working to PeriodQ
            set item Q of Working to PeriodP
            set Reordered
            set Changed
        end
        increment P
        increment Q
    end
    if Reordered go to SortPeriods
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert 'until' to 'from' for all periods
ConvertUntilToFrom:
    put the count of Working into NPeriods
    if NPeriods is not 0
    begin
        put empty into T2
        put 0 into P
        while P is less than NPeriods
        begin
            put item P of Working into Period
            if T2 is empty put entry `temp` of Period into T2
            put P into Q
            increment P
            if P is NPeriods put T2 into T
            else
            begin
                put item P of Working into Period
                put entry `temp` of Period into T
            end
            put item Q of Working into Period
            set entry `temp` of Period to T
            set item Q of Working to Period
        end
    end
    ! Change 'until' to 'from' (helps when debugging)
    put 0 into P
    while P is less than NPeriods
    begin
        put item P of Working into Period
        set entry `from` of Period to entry `until` of Period
        delete entry `until` of Period
        set item P of Working to Period
        increment P
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Convert 'from' to 'until' for all periods
ConvertFromToUntil:
    put the count of Working into NPeriods
    if NPeriods is not 0
    begin
        put empty into T2
        put NPeriods into P
        while P is greater than 0
        begin
            decrement P
            put item P of Working into Period
            if T2 is empty put entry `temp` of Period into T2
            if P is 0 put T2 into T
            else
            begin
                take 1 from P giving Q
                put item Q of Working into Period
                put entry `temp` of Period into T
            end
            put item P of Working into Period
            set entry `temp` of Period to T
            set item P of Working to Period
        end
    end
    ! Change 'from' back to 'until'
    put 0 into P
    while P is less than NPeriods
    begin
        put item P of Working into Period
        set entry `until` of Period to entry `from` of Period
        delete entry `from` of Period
        set item P of Working to Period
        increment P
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Draw the schedule panel
RedrawPanel:
    log `Redraw`
    clear Container
    wait 10 ticks
DrawPanel:
    log `Draw the panel`
    set the elements of PeriodButton to NPeriods
    create Layout type QVBoxLayout
    set the layout of Container to Layout
    create Panel type QHBoxLayout
    add Panel to Layout
    add stretch to Panel
    create Label text `Schedule for ` cat RoomName
    set the style of Label to `border: none; margin-top: 1px; font-weight: bold; font-size: 20px; color: blue`
    add Label to Panel
    add stretch to Panel
    put `border: 1px solid gray; background: white; height: 40px; border-radius: 5px;` into ListStyle
    put `border: none; background: none; height: 40px;` into IconStyle

    ! Do the period buttons. One may be an editor
    create Panel type QVBoxLayout
    add Panel to Layout
    put 0 into P
    while P is less than NPeriods
    begin
        put item P of Working into Period
        if P is SelectedPeriod gosub to CreateEditor
        else gosub to CreatePeriodButton
        increment P
    end
    add stretch to Layout
    if SelectedPeriod is not -1
    begin
        set CurrentPeriod to item SelectedPeriod of Working
        set Time to entry `from` of CurrentPeriod
        set Temp to entry `temp` of CurrentPeriod
    end

    ! Do the buttons at the bottom of the panel
    create Panel type QHBoxLayout
    add Panel to Layout
    create AddButton icon `img/boost.png`
    set the style of AddButton to IconStyle
    set the width of AddButton to 50
    add AddButton to Panel
    create SaveButton text `Save`
    set the style of SaveButton to ListStyle
    add SaveButton to Panel
    create CancelButton text `Cancel`
    set the style of CancelButton to ListStyle
    add CancelButton to Panel
    add spacer 10 to Layout
    show second view of Window
    on click AddButton go to AddPeriod
    on click SaveButton go to SaveAndExit
    on click CancelButton go to Cancel
    return

CreatePeriodButton:
!    log `Set up button for period ` cat P
    put item P of Working into Period
    put entry `from` of Period into Time
    put entry `temp` of Period into Temp
!    log `From ` cat Time cat `: Target = ` cat Temp cat `ºC`
    index PeriodButton to P
    create PeriodButton text `From ` cat Time cat `: Target = ` cat Temp cat `ºC`
    set the style of PeriodButton to ListStyle
    on click PeriodButton go to ClickPeriodButton
    add PeriodButton to Layout
    return

CreateEditor:
    log `Set up editor for period ` cat P
    put entry `from` of CurrentPeriod into Time
    put entry `temp` of CurrentPeriod into Temp
    create Editor
    set the style of Editor to ListStyle
    create EditLayout type QHBoxLayout
    set the layout of Editor to EditLayout
    add spacer size 40 to EditLayout
    create HourDownButton icon `img/down-green.png` size 28
    set the style of HourDownButton to IconStyle
    add HourDownButton to EditLayout
    on click HourDownButton go to HourDown
    create HourUpButton icon `img/up-green.png` size 28
    set the style of HourUpButton to IconStyle
    add HourUpButton to EditLayout
    on click HourUpButton go to HourUp
    create TimeLabel text Time
    add TimeLabel to EditLayout
    create MinuteDownButton icon `img/down-green.png` size 28
    set the style of MinuteDownButton to IconStyle
    add MinuteDownButton to EditLayout
    on click MinuteDownButton go to MinuteDown
    create MinuteUpButton icon `img/up-green.png` size 28
    set the style of MinuteUpButton to IconStyle
    add MinuteUpButton to EditLayout
    on click MinuteUpButton go to MinuteUp
    add spacer size 50 to EditLayout
    create TempUpButton icon `img/up-green.png` size 28
    set the style of TempUpButton to IconStyle
    add TempUpButton to EditLayout
    on click TempUpButton go to TempUp
    create TempLabel text Temp cat `ºC`
    add TempLabel to EditLayout
    create TempDownButton icon `img/down-green.png` size 28
    set the style of TempDownButton to IconStyle
    add TempDownButton to EditLayout
    on click TempDownButton go to TempDown
    add spacer size 50 to EditLayout
    create DeletePeriodButton icon `img/delete.png` size 28
    set the style of DeletePeriodButton to IconStyle
    add DeletePeriodButton to EditLayout
    on click DeletePeriodButton go to DeletePeriod
    add spacer size 20 to EditLayout
    create CloseEditorButton icon `img/close.png` size 28
    set the style of CloseEditorButton to IconStyle
    add CloseEditorButton to EditLayout
    on click CloseEditorButton go to CloseEditor
    add spacer size 40 to EditLayout
    add Editor to Layout
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Click a period button
ClickPeriodButton:
    log `Click PeriodButton ` cat the index of PeriodButton
    if SelectedPeriod is not -1
    begin
        ! Save the data in the current period
        log `Save period ` cat SelectedPeriod cat ` ` cat CurrentPeriod
        set item SelectedPeriod of Working to CurrentPeriod
    end

    ! Redraw the panel
    set SelectedPeriod to the index of PeriodButton
    set CurrentPeriod to item SelectedPeriod of Working
    gosub to RedrawPanel
    stop